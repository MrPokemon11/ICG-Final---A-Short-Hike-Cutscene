title: HomeSignStart
tags: 
colorID: 0
position: 7202,-4551
---
<sprite tint=1 name="LeftArrow"> White Coast Trail ##line:e1d917
Rangers Cabin <sprite name="RightArrow" tint=1> ##line:8e78cb
===
title: GoatStart
tags: 
colorID: 0
position: 11543,-5155
---
<<if $GoatWatchFound is true>>
    [[GoatDone]]
<<endif>>
<<if visited() is false>>
    excuse me do you have the time? ##line:2a55e5
    <<SetSpeaker Player>>
    um yeah, let me chec- ##line:b03acc
    <<SetSpeaker Original>>
    see... normally i would know the time ##line:445422
    i just lost my watch ##line:e86981
    <<SetSpeaker Player>>
    oh, um ##line:b74c71
    sorry ##line:78b6fa
    <<SetSpeaker Original>>
    it was very expensive ##line:a60085
    and priceless when you\nconsider the sentimental value ##line:ba52fa
    <<SetSpeaker Player>>
    i see ##line:3407a1
    <<SetSpeaker Original>>
    if you find it, can you\nbring it back to me? ##line:0d0268
    <<SetSpeaker Player>>
    sure ##line:aa6cf0
    <<SetSpeaker Original>>
    please don't sell it on\nthe internet or whatever ##line:e4e4ee
    it's very valuable to me ##line:b427c9
    <<SetSpeaker Player>>
    no, don't worry ##line:deb271
    i'll bring it right back ##line:6a5e2f
    <<SetSpeaker Original>>
    ok ##line:0c1b44
    thanks ##line:55e682
<<else>>
    <<if HasItem("Watch", 1) is true>>
        [[GoatGive]]
    <<else>>
        [[GoatExtra]]
    <<endif>>
<<endif>>
===
title: BeachHutSign
tags: 
colorID: 0
position: 7630,-4553
---
<sprite tint=1 name="UpArrow"> Rangers Cabin ##line:1cf998
<sprite name="DownArrow" tint=1> White Coast Trail ##line:6df2cf
===
title: WhiteCoastSign
tags: 
colorID: 0
position: 7416,-4552
---
<sprite tint=1 name="UpArrow"> Hawk Peak Trail\nWhite Coast Trail <sprite name="RightArrow" tint=1> ##line:6794ce
<sprite tint=1 name="DownArrow"> Sid Beach\n<sprite tint=1 name="DownArrow"> Visitor Center ##line:24d728
===
title: AuntRunOffStart
tags: 
colorID: 0
position: 10014,-6400
---
<<if $AuntIntro is false and $_ClimbedHawkPeak is false>>
    hey! where do you think you're going? ##line:c3feb2
    <<SetSpeaker Player>>
    <<Face Original>>
    oh, um... ##line:660020
    <<SetSpeaker Original>>
    you're not going to just wander\noff without saying hi to old\nAunt May, are you? ##line:94ad70
    i haven't seen you all day! ##line:0377ea
    <<SetSpeaker Player>>
    sorry... ##line:2ee5c6
    i just, didn't feel like\ntalking i guess ##line:393e8a
    <<SetSpeaker Original>>
    hmm what's going on? ##line:8bc66d
    <<SetSpeaker Player>>
    it's nothing! ##line:68da3e
    um ##line:cc2ab6
    i've just been waiting\naround for a call ##line:2a2a55
    [[AuntIntroEnd]]
<<endif>>
===
title: AuntIntroStart
tags: 
colorID: 0
position: 10253,-6393
---
hey how's it going? ##line:69f366
i haven't seen you all day ##line:f3c007
you been busy? ##line:13113d
<<SetSpeaker Player>>
uh, kinda ##line:15e70c
well, not really ##line:04670a
i've just been waiting\naround for a call ##line:de5495
[[AuntIntroEnd]]


===
title: AuntStart
tags: 
colorID: 0
position: 10528,-6472
---
<<if HasItem("ShellNecklace", 1)>>
    [[AuntNecklace]]
<<elseif $_ClimbedHawkPeak is true>>
    [[AuntFinished]]
<<elseif $AuntIntro is false>>
    [[AuntIntroStart]]
<<else>>
    [[AuntChat]]
<<endif>>
===
title: AuntIntroEnd
tags: 
colorID: 0
position: 10012,-6135
---
<<SetSpeaker Original>>
well there's your problem! ##line:ac2e52
there's no reception out here ##line:122c03
<<SetSpeaker Player>>
wait... ##line:895cf8
WHAT!? ##line:85a826
<<SetSpeaker Original>>
yeah, i mean, pretty much\nno reception ##line:264290
you might be able to get\nsome at <color=red>Hawk Peak</color> ##line:8a1b8c
<<SetSpeaker Player>>
oh... yeah i guess ##line:c175a4
but that's pretty far\nisn't it? ##line:f8213b
<<SetSpeaker Original>>
it's not that far! ##line:b5e4af
we've all made the\ntrek before ##line:2adc6a
i figured you would have\ngone already ##line:2cc023
<<SetSpeaker Player>>
oh... yeah ##line:22169d
i've been meaning to go ##line:87a10b
but ##line:bb94a0
i just ##line:97eccf
haven't gotten around to it yet ##line:43b28d
<<SetSpeaker Original>>
well today's as good a day\nas any! ##line:7a4449
<<SetSpeaker Player>>
... ##line:69a1a3
<<SetSpeaker Original>>
just take <color=red>White Beach Trail</color>\nand head <color=red>north</color> at the fork ##line:665732
then follow the signs\nfor <color=red>Hawk Peak</color> ##line:4a3aac
no problem! ##line:afa6b1
<<set $AuntIntro to true>>
===
title: AuntChat
tags: 
colorID: 0
position: 10772,-6153
---
<<if visitCount() == 0>>
    <color=red>Hawk Peak</color> really is wonderful ##line:e4f197
    you're going to love it! ##line:8866aa
    <<SetSpeaker Player>>
    yeah... i've heard a lot about it ##line:3f786f
    <<SetSpeaker Original>>
    it's quite a hike, sure ##line:1a3a17
    but our family has been going\nthere for generations ##line:5178bc
    <<SetSpeaker Player>>
    yup ##line:67dceb
    <<SetSpeaker Original>>
    the hot springs at the peak\ncreate these amazing updrafts ##line:e2d813
    the feeling of flying up there\nis incredible ##line:40798a
    <<SetSpeaker Player>>
    but ##line:f124e6
    isn't it ##line:a1168c
    kind of dangerous? ##line:c8cb65
    <<SetSpeaker Original>>
    not really ##line:65f725
    it'll be no problem for you! ##line:2dc6ae
    <<SetSpeaker Player>>
    hmm ##line:a9b82f
<<elseif visitCount() == 1>>
    so what kind of call\nare you waiting for? ##line:0acd94
    <<SetSpeaker Player>>
    oh.. um ##line:e495d6
    you know... ##line:e1eca7
    ... ##line:fe9497
    the thing ##line:39e326
    <<SetSpeaker Original>>
    ... ##line:a48a7c
    ohhh! i'm sorry ##line:9c8c35
    i forgot ##line:4d933a
    don't worry about it dear ##line:04095c
    i'm sure it'll all work\nout fine ##line:0c7e66
    i think ##line:69afd0
    a nice hike will help you\ntake your mind off it ##line:e13932
<<elseif visitCount() == 2>>
    ahh sure is a nice day ##line:109615
    i love it here, you know? ##line:923d86
    <<SetSpeaker Player>>
    yeah, it's nice ##line:2640d9
    the city's pretty good\ntoo though! ##line:797bbe
    it's got cool events ##line:a96f99
    great food ##line:af8fd9
    and also ##line:ba2c5e
    cellphone reception! ##line:2eade3
    <<SetSpeaker Original>>
    we'll get reception too\nsoon, ya know! ##line:c73cbb
    they're building a new\nradio tower ##line:89c559
    <<SetSpeaker Player>>
    oh ##line:49e489
    that'll be nice ##line:6b6e0e
<<else>>
    ahh, it's so lovely here ##line:d4c9d3
    <<SetEmotion Happy>>
    i could sit here all day ##line:223ea1
    <<ClearEmotion>>
    and i will ##line:de9d91
<<endif>>
===
title: KidStart
tags: 
colorID: 0
position: 9808,-4626
---
<<if visited() is false>>
    CLAIRE! ##line:b7a7b0
    did you know... ##line:c4bfc9
    you can <color=red>hold {{JumpButton}} while running</color>\nto glide through the air?!? ##line:89c5de
    <<SetSpeaker Player>>
    of course! ##line:63f4be
    everyone knows that ##line:bc1f77
    why do you ask? ##line:59da5d
    <<SetSpeaker Original>>
    i dunno ##line:11dc70
    i just had a feeling\nyou might want to know ##line:8d1f3a
    <<SetSpeaker Player>>
    um ##line:5f1cb6
    ok ##line:9857a1
<<elseif visitCount() == 1>>
    <<SetSpeaker Player>>
    wait, uh ##line:6b0d0c
    how do i glide again? ##line:2c740b
    <<SetSpeaker Original>>
    you <color=red>hold {{JumpButton}} while running</color> dummy! ##line:a5b030
    <<SetSpeaker Player>>
    cool just making sure\nyou haven't forgotten ##line:131a7c
    <<SetSpeaker Original>>
    i never forget anything! ##line:975e5c
    i'm a genius ##line:845d4e
<<elseif visitCount() == 2>>
    <<SetSpeaker Player>>
    ok one last time ##line:32fa08
    what was that thing you said? ##line:b08a47
    <<SetSpeaker Original>>
    <color=red>HOLD {{JumpButton}} WHILE RUNNING TO GLIDE!!!</color> ##line:703f8a
    <<SetSpeaker Player>>
    ok thanks ##line:5746d5
<<elseif visitCount() == 3>>
    <<SetSpeaker Player>>
    seriously just one last time ##line:509a8c
    <<SetSpeaker Original>>
    HOLD ##line:1a38b1
    {{JumpButton}} ##line:77a928
    TO ##line:f7a6f2
    GLIDE!!!! ##line:193b8b
    <<SetSpeaker Player>>
    ok i promise i won't forget ##line:abcac6
<<else>>
    <<SetSpeaker Player>>
    just one more- ##line:ff18cb
    <<SetSpeaker Original>>
    NO! ##line:e27b74
    NOOOO! ##line:81083b
    NOOOOOOOOOOOOOOOOOOO! ##line:9ab14d
    NO MORE!!!! ##line:767e99
<<endif>>
===
title: OtherKidStart
tags: 
colorID: 0
position: 9809,-4370
---
<<if $KidQuest1 is true>>
    [[OtherKid2]]
<<endif>>
<<if visitCount() is 0>>
    HEY YOU ##line:29cac4
    FIND ME 15 SHELLS! ##line:17a485
    <<SetSpeaker Player>>
    what! why? ##line:c5ecea
    <<SetSpeaker Original>>
    i want em ##line:e4e484
    <<SetSpeaker Player>>
    yeah, but why ##line:88d371
    <<SetSpeaker Original>>
    it's a secret! ##line:7a437e
    <<SetSpeaker Player>>
    heh, ok ##line:a889a8
    i'll keep an eye out ##line:ad9058
    <<SetSpeaker Original>>
    keep BOTH eyes out, please! ##line:875ed5
<<elseif GetItemCount("Shell") >= 15>>
    <<SetSpeaker Player>>
    hey, i got your shells ##line:feb9a5
    <<SetSpeaker Original>>
    ooooo! ##line:051e4c
    i'll take em! ##line:c72422
    <<GiveItem Shell -15>>
    <<Hide>>
    <<Wait 1>>
    <<SetSpeaker Player>>
    so now what ##line:e1241c
    <<SetSpeaker Original>>
    what ##line:3e628d
    <<SetSpeaker Player>>
    i mean ##line:831bce
    uh ##line:147c2e
    do i get anything ##line:1b9367
    <<SetSpeaker Original>>
    no! ##line:2fde5e
    <<SetSpeaker Player>>
    geez, not even a thank you? ##line:05680a
    that's not very nice ##line:16ea80
    <<SetSpeaker Original>>
    i do what it takes to survive! ##line:bae3c7
    i never promised you anything ##line:648a49
    <<SetSpeaker Player>>
    ... ##line:caf7cc
    <<SetSpeaker Original>>
    actually ##line:8e0304
    i do have something for you ##line:9d6eff
    a new job ##line:4d16a1
    <<SetSpeaker Player>>
    i already have a job ##line:921eeb
    <<SetSpeaker Original>>
    well make this your side gig ##line:4ab932
    <<SetSpeaker Player>>
    what if i don't take it ##line:1054b1
    <<SetSpeaker Original>>
    it's your loss! ##line:5ac191
    good jobs are hard to find ##line:2f60fc
    ... ##line:0c16fe
    so what will it be? ##line:c46b69
    <<set $KidQuest1 to true>>
    [[ConsiderKidQuest]]
<<elseif GetItemCount("Shell") < 3 or visitCount() is 1>>
    <<SetSpeaker Player>>
    so what's the deal with the shells? ##line:21d908
    <<SetSpeaker Original>>
    it's none of your business! ##line:414248
    except ##line:14cf86
    for the business of you\ngetting me 15 of them ##line:dfc345
<<else>>
    <<SetSpeaker Player>>
    i got {{Item_Shell}} shells so far! ##line:2e35f7
    <<SetSpeaker Original>>
    hey can i ask you\na quick question ##line:0e8aeb
    <<SetSpeaker Player>>
    sure ##line:0a19d0
    <<SetSpeaker Original>>
    is {{Item_Shell}} more or less than 15 ##line:8a7060
    <<SetSpeaker Player>>
    uh, ##line:02f9ee
    less? ##line:e8e0b5
    <<SetSpeaker Original>>
    yeah ##line:588d2f
    it is ##line:4b7c40
    if you don't have the goods... ##line:53148f
    ...then don't waste my time! ##line:96850b
    <<SetSpeaker Player>>
    okay geez! ##line:b94465
    <<SetSpeaker Original>>
    what can i say ##line:961fbd
    i run a tight ship ##line:974e98
<<endif>>
===
title: FoxStart
tags: 
colorID: 0
position: 10933,-3287
---
<<if visited() is false>>
    hmmm ##line:e38563
    i was planning on hiking\nto <color=red>Hawk Peak</color> ##line:b29d15
    but the path suddenly got really steep ##line:7ba030
    <<SetSpeaker Player>>
    do you think it's climbable? ##line:83c6cd
    <<SetSpeaker Original>>
    well, it would be if i had\na <color=red>golden feather</color> ##line:bb9ad1
    apparently they sell them\nat the <color=red>Visitor Center</color> ##line:c5407a
    <<set $GoldenFeatherAdvice to true>>
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so, how do <color=red>golden feathers</color> work? ##line:4cacb9
    <<SetSpeaker Original>>
    well, when you've got\none in your pocket ##line:37e9e6
    you'll feel much lighter ##line:477585
    that makes it easier to\nclimb up cliffs! ##line:3bbcd0
    <<SetSpeaker Player>>
    woah ##line:e82492
    that's pretty cool ##line:5dd075
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    it is indeed! ##line:9d3ba7
    <<ClearEmotion>>
<<elseif visitCount() is 2>>
    <<SetSpeaker Player>>
    so why are you going to Hawk Peak? ##line:2c9d43
    <<SetSpeaker Original>>
    i'm going to take some great\nnature shots from the top ##line:cf58a9
    i wouldn't call myself a photographer but... ##line:f9074d
    it's just a fun hobby ##line:02b8b9
    <<SetSpeaker Player>>
    i'm going there too! ##line:bf4b14
    <<SetSpeaker Original>>
    oh yeah? ##line:fd2045
    i heard it's a long hike ##line:6e4c47
    <<SetSpeaker Player>>
    it is, eh? ##line:34b8e9
    my aunt's always like ##line:ed5d70
    "it's just a short hike away!" ##line:215373
    but she's a total outdoors geek ##line:fb4e3a
    so ##line:8c7a1f
    i think she underestimates it ##line:b7e9d9
    <<SetSpeaker Original>>
    i'd say so ##line:7ba994
    <<set $KnowsFoxIsPhotographer to true>>
<<else>>
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    if we make it up at the same time ##line:726a9c
    you'll have to get my picture! ##line:4286c4
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    sounds like a plan! ##line:4a5a70
    <<SetSpeaker Player>>
    see ya there! ##line:b69196
    <<ClearEmotion Player>>
    <<ClearEmotion Original>>
    <<set $RequestFoxPhoto to true>> 
<<endif>>

===
title: VisitorCenterStart
tags: 
colorID: 0
position: 10286,-4643
---
<<set $TalkedToVisitorCenter to true>>
<<if $KnowsAboutTheStrawberries and GetHour() is 17 and visited() is true>>
    [[Strawberries]]
<<endif>>

<<if $_Artist6Talk is true and $JonEmbarrassed is false>>
    this is a little\nbit embarrassing ##line:d8da86
    i don't think i'm\nvery photogenic  ##line:b49975
    but i'm glad i can\nhelp with his art! ##line:790618
    anyway ##line:773c2e
    <<set $JonEmbarrassed to true>>
<<elseif "ParkHat" is $_WornHat>>
    lookin' sharp in that hat! ##line:7cc3e6
<<else>>
    howdy! ##line:9076cd
<<endif>>

<<if visited() is true>>
    what's up? ##line:40a49b
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    -> can I buy a <color=red>golden feather?</color> ##line:663e36
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        [[BuyFeatherStart]]
    -> what kind of merch do you sell? ##line:7c01cd
        <<HideCollection Coin>>
        [[BuyMerchStart]]
    -> nothing much ##line:c822cd
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        yup ##line:a7d95e
        that's how it goes ##line:e6c071
<<else>>
    how can i help you? ##line:d2ce1b
    <<SetSpeaker Player>>
    <<if $GoldenFeatherAdvice is true>>
        i heard you sell <color=red>\ngolden feathers</color> here ##line:21f53d
        <<SetSpeaker Original>>
        ah, we do! ##line:2c9feb
        you can buy one\nfor 40 coins ##line:65be46
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        -> i'll take it! ##line:bc4372
            <<HideCollection Coin>>
            [[BuyFeatherStart]]
        -> what else do you sell? ##line:893486
            <<HideCollection Coin>>
            <<SetSpeaker Original>>
            we also sell provincial\npark merchandise ##line:505bd5
            [[BuyMerchStart]]
        -> i'm good for now ##line:5274f3
            <<HideCollection Coin>>
            <<SetSpeaker Original>>
            alrighty, take care ##line:d739f6
    <<else>>
        um, ##line:bc241f
        i dunno ##line:27d375
        i guess i'm fine ##line:6295ae
        <<SetSpeaker Original>>
        alrighty! ##line:f627f3
        let me know if you need directions\nor want to buy anything ##line:9149a2
        <<SetSpeaker Player>>
        oh, what are you selling? ##line:8051f6
        <<SetSpeaker Original>>
        well, we have provincial\npark merch ##line:f74cad
        and some hiking supplies\nlike <color=red>golden feathers</color> ##line:03b0f8
        <<SetSpeaker Player>>
        what are those? ##line:4996e6
        they sound cool ##line:da421d
        <<SetSpeaker Original>>
        they are cool! ##line:f56321
        they make rock climbing easier ##line:fd1b15
        and in your case ##line:852b3a
        will also help you fly ##line:0f4a0c
        <<SetSpeaker Player>>
        !! ##line:a936ff
        <<SetSpeaker Original>>
        you can buy one for 40 coins ##line:24358e
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        -> i'll take it!! ##line:b56114
            <<HideCollection Coin>>
            [[BuyFeatherStart]]
        -> what kind of merch do you sell? ##line:5294c3
            <<HideCollection Coin>>
            [[BuyMerchStart]]
        -> i'm good for now ##line:68907f
            <<HideCollection Coin>>
            <<SetSpeaker Original>>
            alrighty, take care ##line:c2141d
    <<endif>>
<<endif>>
===
title: BuyFeatherStart
tags: 
colorID: 0
position: 10534,-4364
---
<<if $FeathersSold >= 2>>
    <<SetSpeaker Original>>
    actually, we're all sold out ##line:590cdd
    <color=red>a boy</color> came by earlier\nand bought most of the\nremaining stock ##line:811e78
    he must be going\non some hike! ##line:a6297a
<<elseif HasItem("Coin", 40) is true>>
    <<SetSpeaker Original>>
    sure, here ya go! ##line:8cdf8a
    <<set $FeathersSold to $FeathersSold + 1>>
    <<GiveItem Coin -40>>
    <<PlaySound BuySound>>
    <<Hide>>
    <<Wait 1>>
    <<GiveItem GoldenFeather 1>>
    <<if $FeathersSold is 2>>
        <<set $HeardAboutBoy to true>>
        that's actually all\nthe feathers we have ##line:069cfe
        <color=red>a boy</color> came by earlier\nand bought most our\nremaining stock ##line:4e8558
        you'll have to find\nmore elsewhere ##line:d958fc
        sorry! ##line:5fda34
    <<endif>>
    <<if $FeathersSold is 1>>
        [[ExplainGoldenFeather]]
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    sure! that'll be 40 coins ##line:522b92
    <<SetSpeaker Player>>
    <<if $NotEnoughYet is false>>
        oh wait ##line:bf6622
        i don't have enough money ##line:e93615
        ... ##line:f011d1
        yet! ##line:f9fe9a
        <<set $NotEnoughYet to true>>
    <<else>>
        oh ##line:df579f
        yeah i forgot ##line:1cb666
        i still need to find more cash ##line:8cc5ff
    <<endif>>
<<endif>>
===
title: VistorCenterCrossroadsStart
tags: 
colorID: 0
position: 7845,-4559
---
<sprite name="UpArrow" tint=1> Visitor Center\n<sprite tint=1 name="LeftArrow"> Hawk Peak Trail ##line:16706a
Sid Beach <sprite tint=1 name="RightArrow">\n<sprite tint=1 name="DownArrow"> Shirley's Point ##line:81143b
===
title: KioskSignStart
tags: 
colorID: 0
position: 7203,-4329
---
<sprite name="UpArrow" tint=1> Blackwood Trail\n<sprite name="LeftArrow" tint=1> Hawk Peak Trail ##line:eb7a43
<sprite name="DownArrow" tint=1> Visitor Center\nWhite Beach Trail <sprite name="RightArrow" tint=1> ##line:1024d7
===
title: HawkPeakRiverSignStart
tags: 
colorID: 0
position: 7430,-4329
---
<sprite name="UpArrow" tint=1> Hawk Peak Trail\n<sprite name="UpArrow" tint=1> Good Creek Path ##line:4957ee
<sprite name="LeftArrow" tint=1> Royal Ridge\nBlackwood Trail <sprite name="RightArrow" tint=1> ##line:628d3d
===
title: RoyalRidgeSignStart
tags: 
colorID: 0
position: 7645,-4319
---
<sprite name="UpArrow" tint=1> Hawk Peak Trail\n<sprite name="UpArrow" tint=1> Good Creek Path ##line:326ae7
<sprite name="DownArrow" tint=1> Blackwood Trail\n<sprite name="LeftArrow" tint=1> Royal Ridge ##line:ede0b0
===
title: FireTowerSnowSignStart
tags: 
colorID: 0
position: 7857,-4319
---
<sprite name="UpArrow" tint=1> Hawk Peak Trail\n<sprite name="DownArrow" tint=1> Good Creek Path\nMeteor Lake <sprite name="RightArrow" tint=1> ##line:b2763c
===
title: HawkPeakSignStart
tags: 
colorID: 0
position: 7205,-4108
---
<<set $ReadHawkPeakSign to true>>
<sprite name="UpArrow" tint=1> Hawk Peak Trail ##line:e9a55c
At least <color=yellow>7 Golden Feathers</color> are\nrequired to complete this hike. ##line:4da2e1
Novice hikers should consider using more. ##line:12ab30
<<if $Climbed1 is false and $Climbed2 is false>>
    <sprite tint=1 name="Warning"> HIKING TIP <sprite tint=1 name="Warning"> ##line:8368eb
    climbing uses less stamina than flying! ##line:4188b8
    to climb, push against a wall and hold {{JumpButton}} ##line:06e25d
<<endif>>
===
title: MeteorLakeSignStart
tags: 
colorID: 0
position: 7425,-4118
---
<sprite name="LeftArrow" tint=1> Meteor Lake Overlook <sprite name="RightArrow" tint=1>\n<sprite name="LeftArrow" tint=1> Hawk Peak Trail\n<sprite name="DownArrow" tint=1> Blackwood Trail ##line:369fc3
===
title: MeteorLakeLeftSignStart
tags: 
colorID: 0
position: 7645,-4109
---
<sprite name="LeftArrow" tint=1> Meteor Lake Overlook <sprite name="RightArrow" tint=1>\n<sprite name="DownArrow" tint=1> Hawk Peak Trail ##line:e9e473
===
title: BeachFoxStart
tags: 
colorID: 0
position: 10771,-4386
---
<<if HasItem("Compass", 1) is false>>
    <<SetEmotion Happy>>
    hey claire! ##line:8e47b8
    how's the day treatin' ya? ##line:7e2d19
    <<ClearEmotion>>
    <<SetSpeaker Player>>
    well ##line:3b05a7
    i'm just getting out\nof the house now ##line:81a9fb
    <<SetSpeaker Original>>
    ah ##line:d5b624
    it's one of those kinds of days ##line:ef8af4
    <<SetSpeaker Player>>
    yeah... ##line:42c449
    i've been kinda ##line:a6f8f9
    caught up in my head all morning ##line:977278
    <<SetSpeaker Original>>
    hmm ##line:b18566
    would you say you feel... ##line:8a30c4
    ...lost and directionless? ##line:f10c07
    <<SetSpeaker Player>>
    uh ##line:055150
    maybe ##line:031c7b
    <<SetSpeaker Original>>
    welp! ##line:a5d39a
    i've got just the thing for you! ##line:f293a6
    take this ##line:1c70ff
    <<Hide>>
    <<GiveItem Compass 1>>
    <<Face Original Player>>
    to use it ##line:0dc524
    <color=red>press {{MenuButton}}</color> to open\nyour inventory ##line:344cd9
    then select it from the <color=red>items menu</color> ##line:671a7f
    <<SetSpeaker Player>>
    oh, uh ##line:586ba8
    thanks ##line:78bc2f
    this should come in handy ##line:f67f9f
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    happy hiking! ##line:4eb391
    <<ClearEmotion>>
<<else>>
    <<if $_ShowCompass is false>>
        <<SetSpeaker Player>>
        how do i use my compass again? ##line:b848c9
        <<SetSpeaker Original>>
        just <color=red>press {{MenuButton}}</color> to\n<color=red>open your inventory</color> ##line:da694d
        then select it from\nthe <color=red>items menu</color> ##line:1cf7a2
        <<SetSpeaker Player>>
        ah right ##line:00e277
        thanks! ##line:c6adcc
    <<else>>
        <<SetSpeaker Player>>
        thanks for lending me the compass ##line:dc7149
        i'll get it back to you\nwhen i'm done my hike ##line:10a2da
        <<SetSpeaker Original>>
        oh, nah ##line:b883c4
        just keep it ##line:b29652
        you know what they say... ##line:062d5d
        it's good luck to know\nwhich way is north! ##line:6bb6e1
        <<SetSpeaker Player>>
        uh ##line:612a85
        do they say that? ##line:e1b0be
        <<SetSpeaker Original>>
        actually, i'm not sure ##line:0b1c04
        but ##line:40ba46
        it certainly can't be bad luck! ##line:918947
    <<endif>>
<<endif>>
===
title: VisitorsCenterSignStart
tags: 
colorID: 0
position: 7686,-3881
---
Welcome to Hawk Peak Provincial Park! ##line:a8b5f9
Enjoy a scenic trek to Outlook Point\nor follow <color=yellow>Hawk Peak Trail</color> up to the\nhighest point in the province! ##line:692bdd
Although this island is known for\nits titular mountain peak, ##line:b1e575
It is also home to a number of\nbeautiful beaches and forests! ##line:b107d7
Did you know? ##line:a83a22
The infamous local dish\n'Strawberries and Toast'\nwas invented HERE at Hawk Peak! ##line:fcd4a8
Please join us at 5PM to\nenjoy a delicious serving\nof the historical dish! ##line:ad24ec
<<set $KnowsAboutTheStrawberries to true>>

===
title: BuyMerchStart
tags: 
colorID: 0
position: 10035,-4366
---
<<SetSpeaker Original>>
<<if visited() is false>>
    we're kinda low on stock ##line:e688e0
    but that's good, actually ##line:a7cf46
    it's been a busy season! ##line:20178a
    provincial parks must be\ntrending or something ##line:66f7eb
    <<SetSpeaker Player>>
    it's possible! ##line:33c393
    <<SetSpeaker Original>>
    anyway ##line:d29584
<<endif>>
<<SetSpeaker Original>>
<<if $SoldHat is false>>
    <<set $KnowsAboutMerchHat to true>> 
    we only have one hat left ##line:cf8544
    it costs 100 coins ##line:9178cb
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    -> i'll take it! ##line:158b88
        <<HideCollection Coin>>
        <<if HasItem("Coin", 100) is true>>
            <<GiveItem Coin -100>>
            <<PlaySound BuySound>>
            <<Hide>>
            <<Wait 1>>
            <<GiveItem ParkHat 1>>
            <<set $SoldHat to true>>
            <<SetSpeaker Original>>
            why don't you try it on? ##line:ec5216
            <<Hide>>
            <<Wait 1>>
            <<if HasItem("GoldenFeather", 1) is false>>
                hmm, you're all out of cash now... ##line:c96132
                how about I throw in a\nfree golden feather! ##line:fb4cd6
                <<set $FeathersSold to $FeathersSold + 1>>
                <<Hide>>
                <<GiveItem GoldenFeather 1>>
                [[ExplainGoldenFeather]]
            <<endif>>
        <<else>>
            <<SetSpeaker Original>>
            oh uhhh, ##line:d29351
            i don't think... ##line:41e1d2
            you can afford it ##line:e04b2d
            <<SetSpeaker Player>>
            oh ##line:bb012f
            right ##line:a0e28c
        <<endif>>
    -> nah, i'll pass ##line:87ad79
        <<HideCollection Coin>>
        <<SetSpeaker Original>>
        okay! ##line:d1acff
<<else>>
    we're all out! ##line:3e75b5
    you bought the last hat ##line:ff23f2
    <<SetSpeaker Player>>
    are you guys gonna order more? ##line:976599
    <<SetSpeaker Original>>
    not until next season ##line:44a915
    each season is a limited run ##line:10e2e7
    <<SetSpeaker Player>>
    i guess i'm lucky i\ngot the last one! ##line:1154fb
    <<SetSpeaker Original>>
    haha, i suppose so! ##line:cd0ade
<<endif>>
===
title: ExplainGoldenFeather
tags: 
colorID: 0
position: 10299,-4118
---
let me explain how they work! ##line:a2c392
<<Face Original Player>>
if you press <color=red>{{JumpButton}}</color> while\nin midair, you can flap your\nwings for a double jump! ##line:be6485
but that's not all! ##line:ef3fcb
to climb a wall, push against it and <color=red>hold {{JumpButton}}</color> ##line:c6d6ba
the more golden feathers you\nhave, the more you'll be able\nto climb and fly ##line:4577c7
have fun out there! ##line:a6796e
===
title: HikingDalmationStart
tags: 
colorID: 0
position: 11794,-4688
---
<<set $HikerTalkCount to $HikerTalkCount + 1>>
<<if GetSpeakerY() > 250.5>>
    [[AtTopOfHike]]
<<else>>
    <<if GetNPCSpeakerNextNode() < 18>>
        [[HikingUp]]
    <<else>>
        [[HikingDown]]
    <<endif>>
<<endif>>
===
title: HikingUp
tags: 
colorID: 0
position: 11538,-4392
---
<<if $HikerTalkCount is 1>>
    heyo! ##line:bb7420
    how's the hike treating you? ##line:2d4461
    <<SetSpeaker Player>>
    oh, uh ##line:366ec1
    it's been nice ##line:591331
    it's really beautiful here ##line:a69641
    <<SetSpeaker Original>>
    i know right! ##line:75795e
    <<SetEmotion Happy>>
    the view from the top will be... ##line:dcdf46
    SO ##line:e5ee40
    GOOD!! ##line:e3110d
    <<SetSpeaker Player>>
    you're going to <color=red>Hawk Peak</color> too!? ##line:74b492
    <<SetSpeaker Original>>
    <<ClearEmotion>>
    oh, no ##line:562b58
    that's too intense for me ##line:d97d0c
    <<SetSpeaker Player>>
    ... ##line:f55ea7
    <<SetSpeaker Original>>
    i'm just going to Outlook Point ##line:41bad1
    will i see ya there? ##line:456d6f
    <<SetSpeaker Player>>
    -> yeah, i'll meet you at the top! ##line:bef5f5
        <<SetSpeaker Original>>
        <<SetEmotion Happy>>
        awesome! ##line:88e0af
        catch ya later! ##line:f6e445
    -> uh, maybe ##line:66d427
        well, see ya around! ##line:10254b
<<else>>
    <<if $MetHikerAtTop is true>>
        i'm heading back up to the tower ##line:d68d22
        i gotta get that exercise in ##line:be5ef3
        see ya around! ##line:ae00f9
    <<else>>
        <<if $DumbLore is false>>
            Good Creek Path is really pretty ##line:4f2346
            a long time ago, it used to\nbranch off to Harbord Ridge ##line:632631
            but the path was damaged in\na rainstorm and they took\nthe signs down ##line:0f34a2
            <<set $DumbLore to true>>
        <<else>>
            <<SetEmotion Happy>>
            what a pretty day! ##line:55a2ef
            <<ClearEmotion>>
        <<endif>>
    <<endif>>
<<endif>>
===
title: HikingDown
tags: 
colorID: 0
position: 11790,-4397
---
<<if $HikerTalkCount is 1>>
    heyo! ##line:66b8b5
    how's the hike treating you? ##line:2fd1f2
    <<SetSpeaker Player>>
    oh, uh ##line:1120be
    it's been nice ##line:a23900
    it's really beautiful here ##line:4eb4ce
    <<SetSpeaker Original>>
    i know right! ##line:4235b1
    <<SetEmotion Happy>>
    the view from the top was... ##line:45d8a8
    SO ##line:0ded11
    GOOD!! ##line:ce0874
    <<SetSpeaker Player>>
    you made it up to <color=red>Hawk Peak</color>!? ##line:9c2490
    <<SetSpeaker Original>>
    <<ClearEmotion>>
    oh, no ##line:e8cea1
    that's too intense for me ##line:39b4ec
    <<SetSpeaker Player>>
    ... ##line:4587d1
    <<SetSpeaker Original>>
    i've just come from Outlook Point ##line:c40345
    i'm gonna do a few rounds\nto get my exercise in ##line:ebbebf
    not to brag, but ##line:5b64a7
    i'm pretty fast ##line:8a6f60
    maybe i'll see you\nup there in a few! ##line:413a40
    <<SetSpeaker Player>>
    -> yeah, i'll meet you at the top! ##line:6b2c09
        <<SetSpeaker Original>>
        <<SetEmotion Happy>>
        awesome! ##line:cf9a29
        catch ya later! ##line:879d6e
    -> uh, maybe ##line:1b843f
        well, see ya around! ##line:e3cd9c
<<else>>
    <<if $MetHikerAtTop is true>>
        <<if $HikeDownChitChat is false>>
            man ##line:57905d
            i wished i lived here ##line:665f1b
            i would do this hike every single day ##line:cff274
            and i'd never get tired of it ##line:35527c
            <<SetSpeaker Player>>
            you could get a job at the park! ##line:cfec4b
            my aunt works here, and she\ngets to live here all summer ##line:fd152d
            <<SetSpeaker Original>>
            *sigh* ##line:7f4368
            i wish ##line:e74b7a
            i can't just up and\nleave the city ##line:f5ed00
            <<SetSpeaker Player>>
            -> well, you could! ##line:97619f
                i mean, it wouldn't be easy ##line:762691
                might be worth it though ##line:b61c4e
                <<SetSpeaker Original>>
                ha, yeah... ##line:d1f04e
                maybe someday! ##line:d27b91
            -> yeah, i get it ##line:1fd66e
                you'd have to leave behind, like ##line:07768a
                your friends ##line:97475a
                your apartment ##line:af85ff
                your job... ##line:33cdc0
                <<SetSpeaker Original>>
                yeah, it would tough ##line:7764fb
            <<set $HikeDownChitChat to true>>
        <<else>>
            well, i gotta get my hike on! ##line:22d165
            catch ya later! ##line:7e0585
        <<endif>>
    <<else>>
        heyyooo ##line:4f2bc3
        i was just up at Outlook Point ##line:b15c74
        i didn't see ya there though ##line:4c2206
        <<SetSpeaker Player>>
        whoops! ##line:eace63
        <<SetSpeaker Original>>
        you gotta check it out though ##line:62fa4b
        the view is fantastic. ##line:239198
        i'm planning on making a few rounds ##line:0de906
        gotta get that exercise in! ##line:f7fa28
    <<endif>>
<<endif>>
===
title: AtTopOfHike
tags: 
colorID: 0
position: 12063,-4404
---
<<if $HikerTalkCount is 1>>
    man ##line:f3b062
    you can really feel the wind up here! ##line:8608c5
    i love this ##line:df1b47
    it makes me feel so... ##line:4c0455
    happy ##line:b99060
    <<SetSpeaker Player>>
    yeah ##line:3bd9e4
    it's good ##line:7ec259
    [[GetMapFromHiker]]
<<else>>
    <<if $MetHikerAtTop is false>>
        hey! ##line:0ce335
        good to see ya up here ##line:75ec5c
        is it just me ##line:10d7e4
        or is this kinda epic ##line:4ec50c
        <<SetSpeaker Player>>
        -> yeah, it's epic ##line:b4d915
            <<SetSpeaker Original>>
            yes! ##line:a2534c
            the wind in my face ##line:25b162
            it makes me feel so good ##line:4ddcb4
        -> it's OK ##line:f2e1a1
            <<SetSpeaker Original>>
            it's OK?! ##line:35a271
            look at all this! ##line:897458
            it's EPIC ##line:b7f3ce
            you can see like, ##line:39c3b6
            the whole island! ##line:d3e4ae
        [[GetMapFromHiker]]
    <<else>>
        i love it up here! ##line:6bfe40
        ... ##line:9bcc69
        i should have brought\nsome snacks though ##line:103872
        when you get to the\ntop of a mountain... ##line:c0fc37
        you gotta have a snack! ##line:d98ad8
        it just seems right. ##line:173358
    <<endif>>
<<endif>>
===
title: TreasureMapsStart
tags: 
colorID: 0
position: 8502,-4415
---
<<if GetItemCount("TreasureMap") == 1>>
    <<if $_TMap1 is true>>
        [[TMap1]]
    <<endif>>
    <<if $_TMap2 is true>>
        [[TMap2]]
    <<endif>>
    <<if $_TMap3 is true>>
        [[TMap3]]
    <<endif>>
    <<if $_TMap4 is true>>
        [[TMap4]]
    <<endif>>
<<else>>
    Which map should I read? ##line:1e9600
    <<if $_TMap1 is true>>
        [[A Stormy View|TMap1]] ##line:e93076
    <<endif>>
    <<if $_TMap2 is true>>
        [[In Her Shadow|TMap2]] ##line:e51de7
    <<endif>>
    <<if $_TMap3 is true>>
        [[The King's Throne|TMap3]] ##line:9a1ae1
    <<endif>>
    <<if $_TMap4 is true>>
        [[The Treasure of Sid Beach|TMap4]] ##line:5bb1cf
    <<endif>>
<<endif>>
===
title: TMap1
tags: 
colorID: 0
position: 8486,-3819
---
it says... ##line:4308a7
<<SetStyle MapTextBoxProfile>>
Upon a stormy clifftop\nOne should pause to see the view ##line:8e7e8b
Peer through an earthy window\nGold is hidden in the blue ##line:458ec8
<<SetSpeaker Player>>
<<if $_Unearthed_StormyView>>
    i think i've already found this one! ##line:288113
<<endif>>
===
title: TMap2
tags: 
colorID: 0
position: 8875,-4071
---
it says... ##line:d96274
<<SetStyle MapTextBoxProfile>>
His vessel was lost on the water ##line:fdf2d8
But the beacon found and caught her ##line:a3b13c
So he buried his trade ##line:99cda9
At the end of its shade ##line:970b49
To keep safe for the lightkeeper's daughter ##line:6a17f8
<<SetSpeaker Player>>
<<if $_Unearthed_LighthouseShadow>>
    i think i've already found this one! ##line:185fed
<<endif>>
===
title: GetMapFromHiker
tags: 
colorID: 0
position: 12064,-4174
---
<<SetSpeaker Original>>
the visitor center is\nso tiny from here! ##line:96a1ad
heh ##line:88b430
oh, that reminds me! ##line:8a6425
take this ##line:3999a2
<<Hide>>
<<GiveItem TreasureMap 1>>
<<set $_TMap4 to true>>
apparently there's some sort of\ntreasure buried at Sid Beach ##line:22f5dc
i couldn't find anything, maybe\nyou'll have better luck! ##line:289fee
<<set $MetHikerAtTop to true>>
===
title: OtherKid2
tags: 
colorID: 0
position: 9939,-4062
---
<<if $AcceptedKidQuest2 is false>>
    so ##line:6df84f
    have you come CRAWLING\nback to reconsider? ##line:8b23e4
    [[ConsiderKidQuest]]    
<<elseif HasItem("ShellNecklace", 1)>>
    how's the delivery going? ##line:acd876
    <<SetSpeaker Player>>
    i'm working on it ##line:8bab11
    <<SetSpeaker Original>>
    not hard enough apparently ##line:fe4551
<<elseif $KidDone is false>>
    so my sources say ##line:b52bf2
    you've delivered the goods ##line:d985f3
    <<SetSpeaker Player>>
    your sources are correct ##line:6b9dca
    ... ##line:d4254f
    so what about that pay ##line:241579
    <<SetSpeaker Original>>
    ok ok ##line:87b222
    here ya go ##line:ea3c5c
    <<Hide>>
    <<GiveItem Shell 10>>
    <<Wait 1>>
    <<SetSpeaker Player>>
    ... ##line:cee2d4
    <<SetSpeaker Original>>
    enjoy your shells ##line:8b081d
    <<set $KidDone to true>>
<<else>>
    <<SetSpeaker Player>>
    so what's your deal ##line:e00588
    <<SetSpeaker Original>>
    i deal in shells ##line:24a68b
    <<SetSpeaker Player>>
    hmm ##line:602c59
<<endif>>
===
title: ToughBirdStart
tags: 
colorID: 0
position: 10539,-5086
---
<<if $ToughBirdSellIntro is true>>
    [[ToughBirdSell]]
<<endif>>

hey kid ##line:23b2bf
you wanna buy some golden feathers? ##line:90ce75
<<SetSpeaker Player>>
-> yeah ok ##line:76bd02
    <<SetSpeaker Original>>
    heh, cool ##line:db76a0
    it'll be 100 coins per feather ##line:f4eec9
    <<SetSpeaker Player>>
    ..but ##line:fea20f
    that's more than twice as\nmuch as the visitor center! ##line:8be3b9
    <<SetSpeaker Original>>
    go buy feathers from them then ##line:b6d16a
    <<if $HeardAboutBoy is true>>
        <<SetSpeaker Player>>
        but ##line:cb8a78
        they're all sold out! ##line:71146a
        <<SetSpeaker Original>>
        heh ##line:215221
        i guess you're out of luck ##line:b10f86
        ... ##line:498089
    <<else>>
        <<SetSpeaker Player>>
        maybe i will! ##line:32649d
        <<SetSpeaker Original>>
        ... ##line:78d68c
    <<endif>>
    <<set $ToughBirdSellIntro to true>>
    [[ToughBirdSell]]
-> nah i'm ok ##line:f3133d
    <<SetSpeaker Original>>
    whatever, suit yourself ##line:e79884
    
===
title: ToughBirdSell
tags: 
colorID: 0
position: 10849,-5087
---
<<if $ToughBirdFriend is true>>
    hey ##line:5303e7
    what's up? ##line:4e66f4
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    [[can i buy a feather?|ToughBirdBuyFeatherNice]] ##line:6a5ae2
    [[see ya|ToughBirdExitNeutral]] ##line:92eaee
<<else>>
    <<if $ToughBirdSales >= 4>>
        dang, you bought all my feathers... ##line:eaeb24
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        <<if $ToughBirdsProblem is false>>
            [[what's your deal?|ToughBirdWhatDeal]] ##line:2ff1f5
            [[see ya|ToughBirdExitNeutral]] ##line:c094ee
        <<else>>
            <<if HasItem("Coin", 400) is true>>
                [[do you want money for tuition?|ToughBirdTuition]] ##line:f01405
                [[see ya|ToughBirdExitNeutral]] ##line:053d25
            <<else>>
                [[what's up?|ToughBirdWhatsUp]] ##line:d9dd5e
                [[see ya|ToughBirdExitNeutral]] ##line:1bff1a
            <<endif>>
        <<endif>>
    <<else>>
        <<SetSpeaker Original>>
        ...so you gonna buy\nmy feathers or what? ##line:af914d
        <<SetSpeaker Player>>
        <<ShowCollection Coin>>
        [[fine|ToughBirdBuyFeather]] ##line:c82875
        [[nope!|ToughBirdExit]] ##line:df0fda
        <<if $ToughBirdSales > 0>>
            <<if $ToughBirdsProblem is false>>
                [[what's your deal?|ToughBirdWhatDeal]] ##line:385a65
            <<else>>
                <<if HasItem("Coin", 400) is true>>
                    [[do you want money for tuition?|ToughBirdTuition]] ##line:71fb21
                <<else>>
                    [[what's up?|ToughBirdWhatsUp]] ##line:2887d6
                <<endif>>
            <<endif>>
        <<endif>>
    <<endif>>
<<endif>>
===
title: ToughBirdBuyFeather
tags: 
colorID: 0
position: 10844,-5472
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
<<if HasItem("Coin", 100) is true>>
    heh ##line:66b166
    alrighty then, pay up ##line:a8cb2d
    <<set $ToughBirdSales to $ToughBirdSales + 1>>
    <<GiveItem Coin -100>>
    <<PlaySound BuySound>>
    <<Hide>>
    <<Wait 1>>
    <<GiveItem GoldenFeather 1>>
    smell ya later ##line:ac38da
<<else>>
    um ##line:358cb1
    you don't even have 100 coins ##line:9281af
    <<SetSpeaker Player>>
    ... ##line:96351a
    <<SetSpeaker Original>>
    i'm not gonna lower my prices ##line:ae3666
    i provide a service! ##line:ffc758
<<endif>>
===
title: CheckWatch
tags: 
colorID: 0
position: 8282,-4413
---
it's {{Time}} ##line:4773e7
===
title: TMap3
tags: 
colorID: 0
position: 8301,-4072
---
it says... ##line:e9a17a
<<SetStyle MapTextBoxProfile>>
South of the stone tower\nAcross the river bridge ##line:687188
A lonely seat sits crumbling\nUpon the Royal Ridge ##line:4a4ba8
<<SetSpeaker Player>>
<<if $_Unearthed_KingsThrone>>
    i think i've already found this one!     ##line:2315c9
<<endif>>
===
title: TMap4
tags: 
colorID: 0
position: 8632,-4075
---
it says... ##line:da94ef
<<SetStyle MapTextBoxProfile>>
I hid my treasure from the world\nBut never far from reach ##line:554d84
It rests between the mossy stones\nThat lie upon Sid Beach ##line:7165cf
<<SetSpeaker Player>>
<<if $_Unearthed_SidBeachTreasure>>
    i think i've already found this one!     ##line:e74a50
<<endif>>
===
title: RunRabbitStart
tags: 
colorID: 0
position: 9132,-3327
---
it's dangerous to go slow! ##line:380436
that's why ##line:919e94
i go fast ##line:ba4ba9
===
title: IceHikerStart
tags: 
colorID: 0
position: 10935,-3058
---
<<set $_MetIceHiker to true>>
    
<<if $_FoxClimbedToTop is true>> 
    [[IceHikerPeak]] 
<<elseif visited() is false>>
    <<Face Player>>
    <<if $GoldenFeatherAdvice is true>>
        hey it's you again! ##line:cd416b
    <<else>> 
        hey! ##line:38c79a
    <<endif>> 
    we're almost at the top... ##line:60dc80
    but ##line:b62814
    it looks like the\nbridge collapsed? ##line:02ff37
    ... ##line:d6397b
    there might be <color=red>another way</color> up... ##line:1b116a
    or ##line:e2a2f5
    i could just go collect\nmore golden feathers? ##line:dc526c
<<elseif $_ClimbedHawkPeak is true>>
    <<Face Player>>
    [[IceHikerAfterPeak]]
<<elseif visitCount() is 1>>
    <<Face Player>>
    dang ##line:eea8f5
    i really wanted to get\na photo from the top ##line:ec7a9d
    i'm not sure what\ni'll do now ##line:38ba05
    ... ##line:af56ff
    i heard ##line:8887af
    there used to be a route\nthat went along the dark\nside of the mountain ##line:e2cf23
    but ##line:899970
    they closed it a long time ago ##line:cc9db2
    i wonder if it's still usable? ##line:b20bd2
    <<set $KnowsFoxIsPhotographer to true>> 
<<else>>
    <<Face Player>>
    if you manage to find a way up ##line:c08238
    can you let me know? ##line:b831d9
    <<SetSpeaker Player>>
    sure! ##line:bc247c
    i'll come back for you ##line:932396
<<endif>>
===
title: BeachHikerStart
tags: 
colorID: 0
position: 10659,-3665
---
<<if visited() is false>>
    there's gonna be a marathon\nrace at Meteor Lake today! ##line:a3773d
    the competition this year is fierce! ##line:46507b
    <<set $KnowsAboutRace to true>>
<<elseif visitCount() is 1>>
    my nephew is running in the marathon ##line:582366
    he's been out training all morning ##line:a5c85c
    if you see him out there, ##line:e2275b
    make sure he's taking breaks! ##line:ff486e
    <<set $NephewHi to true>>
<<else>>
    also make sure he's\ndrinking lots of water! ##line:c6b196
    never overlook hydration! ##line:3e2b7d
    <<set $Hydration to true>>
<<endif>>
===
title: LowerHikerStart
tags: 
colorID: 0
position: 10412,-3662
---
<<if visited() is false>>
    i've heard some rumors ##line:e0de25
    do you like rumors? ##line:a70ba4
    <<SetSpeaker Player>>
    i dabble ##line:a6bbd0
    <<SetSpeaker Original>>
    ok hear me out ##line:b7af78
    jim just bought a metal detector ##line:5b466b
    he was running around yesterday ##line:2de9cb
    looking for stuff around the island ##line:47eead
    it was going nutso! ##line:f610c5
    seems like there's stuff\nburied all over the island?! ##line:328d7a
    <<SetSpeaker Player>>
    what kind of stuff? ##line:bc4834
    <<SetSpeaker Original>>
    i dunno ##line:7b23af
    metal stuff? ##line:cb9371
<<elseif visitCount() is 1>>
    jim doesn't actually own <color=red>a shovel</color> ##line:2374ec
    so... ##line:58caf1
    he wasn't able to dig any of it up ##line:fd26dc
<<elseif visitCount() is 2>>
    ok so you wanna hear another rumor ##line:2fb782
    <<SetSpeaker Player>>
    sure why not ##line:9af4a9
    <<SetSpeaker Original>>
    so deborah was up at\nOutlook Point the other day ##line:9a8e19
    she said, that when she\nlooked through the binoculars ##line:f7c5cc
    she could see a... ##line:cbb413
    MYSTERIOUS ISLAND ##line:a4c81c
    <<SetSpeaker Player>>
    how mysterious!! ##line:353e7d
<<else>>
    so you want to hear another rumor? ##line:7781fd
    <<SetSpeaker Player>>
    alright ##line:d96cbb
    <<SetSpeaker Original>>
    well i'm all out ##line:fe272d
    <<SetSpeaker Player>>
    that's ok ##line:3a1dd5
<<endif>>
===
title: FrogMiniStart
tags: 
colorID: 0
position: 11175,-4155
---
<<if $_GotShovel is true>>
    <<if $_BuiltMoreSandcastles is true>>
        [[FrogElections]]
    <<else>>
        aww heck ##line:95505d
        my productivity is\ngonna skyrocket with\na shovel like this ##line:1a8c04
        i'll catch ya later, ##line:5e04ad
        i've got a lot\nof work to do ##line:5d36db
    <<endif>>
<<else>>
    <<if HasItem("ToyShovel", 1) is true and $AskedForShovel is true>>
        <<SetSpeaker Player>>
        hey check out this\nshovel i found ##line:f4121f
        it might be a little more your size ##line:1bc8fc
        [[FrogTrade]]
    <<else>>
        <<SetSpeaker Player>>
        hey dude ##line:1bf15e
        <<if visited() is false>>
            what's happening ##line:9e26ee
            <<SetSpeaker Original>>
            oh, you know! ##line:0dcad0
            just jammin' out on the beach ##line:456ef9
            building sandcastles\nand what not ##line:91fe35
            <<SetSpeaker Player>>
            sounds like a good time ##line:f7a787
            <<SetSpeaker Original>>
            you know it ##line:2d4144
        <<endif>>
        <<SetSpeaker Player>>
        -> can i borrow your shovel? ##line:b4e558
            <<if $AskedForShovel is false>>
                <<SetSpeaker Original>>
                sorry bud ##line:d68cec
                i need it for sandcastle construction ##line:7f7e58
                <<SetSpeaker Player>>
                isn't it a little big for that? ##line:043a03
                <<SetSpeaker Original>>
                yeah but ##line:3d0e29
                that's just how shovels are ##line:f0910a
                <<SetSpeaker Player>>
                you can get smaller ones ##line:6faeaa
                ones made for sandcastles ##line:4aac4b
                <<SetSpeaker Original>>
                whaat? ##line:49299e
                sounds fake ##line:286be3
                <<if HasItem("ToyShovel", 1) is true>>
                    <<SetSpeaker Player>>
                    i've got one on me! ##line:0ad3b3
                    here, take a look at this ##line:479942
                    <<Hide>>
                    <<Wait 1>>
                    [[FrogTrade]]
                <<else>>
                    but if you can show me one ##line:0782a1
                    i'd be glad to trade ##line:fafd82
                    <<set $AskedForShovel to true>>
                <<endif>>
            <<else>>
                <<SetSpeaker Original>>
                if you can find me\na better suited shovel ##line:996361
                i'd be totes down to trade ##line:ab9fa7
            <<endif>>
        -> see ya later ##line:41aac2
            <<SetSpeaker Original>>
            keep it fresh! ##line:2f89a6
    <<endif>>
<<endif>>
===
title: FrogElections
tags: 
colorID: 0
position: 11425,-4157
---
<<if $_BuiltSandProvince is true>>
    [[FrogProvincialElections]]
<<endif>>
<<if visited() is false>>
    my sandcastle is now\npart of a sandcity! ##line:cd4f15
    and also ##line:b9884b
    i'm running in a HOTLY\ncontested election for mayor ##line:212896
    oh hold on... ##line:98996e
    the election results\nare JUST coming in! ##line:2d62a8
    <<SetSpeaker Player>>
    !! ##line:980c18
    <<SetSpeaker Original>>
    okay okay ##line:d5569e
    im tallying them up ##line:c83615
    ... ##line:5f5ab3
    THIRD PLACE!? ##line:afa212
    whaaat! ##line:854dfd
    i built this city! ##line:4b0021
    <<set $_CityElection to true>>
<<else>>
    <<SetSpeaker Player>>
    sorry about the loss ##line:b7b7e3
    <<SetSpeaker Original>>
    it was a tough race ##line:9db94d
    but it's ok ##line:a34d21
    i managed to secure a seat\non the city council ##line:6d4cd5
<<endif>>
===
title: RaceRabbitStart
tags: 
colorID: 0
position: 12386,-4601
---
<<if "Headband" is $_WornHat and $BunnyQuestDone is false>>
    hmm ##line:42e074
    uh ##line:8a0950
    <<SetSpeaker Player>>
    yeah? ##line:ce3b30
    <<SetSpeaker Original>>
    that headband you're wearing ##line:b5c025
    <<SetSpeaker Player>>
    oh uh! ##line:7347fa
    right ##line:9c5e91
    <<Hide>>
    <<Wait 1>>
    <<set $_WornHat to "">>
    here, i found this ##line:0d9622
    [[GiveRabbitHeadband]]
<<elseif $RabbitQuest is true and HasItem("Headband", 1) is true and $BunnyQuestDone is false>>
    any luck finding\nmy headband yet? ##line:ca6f2a
    <<SetSpeaker Player>>
    well ##line:4aa411
    i found this... ##line:d16266
    <<Hide>>
    <<Wait 1>>
    [[GiveRabbitHeadband]]
<<elseif $BunnyQuestDone is true>>
    [[RabbitWithHeadband]]
<<elseif visited() is false>>
    uh.. ##line:3598c9
    hmm! ##line:526327
    <<SetSpeaker Player>>
    hi! ##line:ac78dd
    <<SetSpeaker Original>>
    HII!!! ##line:14465a
    <<SetSpeaker Player>>
    uh, are you ok? ##line:20f3db
    <<SetSpeaker Original>>
    uhh! ##line:04c38d
    not really... ##line:622b29
    i'm supposed to run\na marathon race today ##line:15e552
    <<set $KnowsAboutRace to true>>
    but i've lost my lucky\nheadband!! ##line:c4af1f
    i've worn it to EVERY\nrace i've ever run! ##line:3e1f3c
    i... ##line:0c18ad
    i don't know if i could\nrace without it ##line:1c4bea
    <<SetSpeaker Player>>
    where'd you lose it? ##line:14481a
    <<SetSpeaker Original>>
    somewhere in Blackwood Forest ##line:3e80d4
    ... ##line:aa27b9
    i think... ##line:3c03ee
    <<SetSpeaker Player>>
    i could take a look for you ##line:ddb376
    <<SetSpeaker Original>>
    really!? ##line:40eef8
    you'd do that for me? ##line:ea60ac
    <<SetSpeaker Player>>
    sure! ##line:ffac2a
    <<SetSpeaker Original>>
    !! ##line:9171bb
    it's a <color=red>red headband</color> ##line:8ab296
    ...i can give you a reward! ##line:678691
    i could give you a\n<color=red>pair of running shoes</color> ##line:da4c6c
    <<SetSpeaker Player>>
    don't you need those yourself? ##line:c4a22d
    <<SetSpeaker Original>>
    well ##line:70f431
    i just got sponsored\nby this shoe company ##line:cb2831
    they gave me like 10 pairs ##line:f5df81
    <<SetSpeaker Player>>
    woah! ##line:bc41ac
    congrats  ##line:30f083
    <<SetSpeaker Original>>
    oh!! ##line:abf228
    haha... thanks ##line:56b96b
    but uhh ##line:4df55c
    let me know if you see\na <color=red>red headband!</color> ##line:5c0042
    <<set $RabbitQuest to true>>
<<else>>
    [[RaceRabbitExtra]]
<<endif>>
===
title: RaceTurtleStart
tags: 
colorID: 0
position: 12856,-4609
---
<<if "Headband" is $_WornHat>>
    [[WearingTurtleHat]]
<<endif>>

<<if $BunnyQuestDone is true>>
    <<if $ToldTurtle is false>>
        <<SetSpeaker Player>>
        i gave it to her ##line:62734e
        <<SetSpeaker Original>>
        so she's back in the race? ##line:82f377
        <<SetSpeaker Player>>
        yes! ##line:1bbd37
        <<if $BunnyGiftHeadband is false>>
            <<SetSpeaker Original>>
            awesome ##line:4b1458
            <<SetEmotion Happy>>
            i'm glad i could help! ##line:8eae37
            <<ClearEmotion>>
        <<else>>
            <<SetSpeaker Player>>
            she returned the\nheadband, though ##line:808a70
            she seemed inspired ##line:e8d4f8
            i think she wants to\ntry and win luck-free ##line:a1d290
            <<SetSpeaker Original>>
            i don't think she needs it! ##line:d90a2d
            but you know what ##line:652de5
            why don't you keep the headband ##line:613427
            that way we'll be equal! ##line:26948a
            <<SetSpeaker Player>>
            uh ##line:75c136
            do you really think it matters? ##line:8fba5b
            <<SetSpeaker Original>>
            well... ##line:0b986b
            <<SetEmotion Happy>>
            probably not ##line:37440b
            but ##line:beab1b
            just to be safe ##line:cca72a
            <<ClearEmotion>>
            <<set $ExplainedHeadband to true>>
        <<endif>>
        <<set $ToldTurtle to true>>
    <<else>>
        [[RaceTurtleAfter]]
    <<endif>>
<<elseif HasItem("Headband", 1) is true>>
    let me know how it goes! ##line:e00e7e
<<elseif $RabbitQuest is true and $_BunnyGotHeadband is false>>
    <<SetSpeaker Player>>
    oh uh... ##line:4af7e6
    weird question, but ##line:3ff0aa
    where'd you get that headband? ##line:aae633
    <<SetSpeaker Original>>
    uh... ##line:eadc96
    i don't know ##line:d22c70
    i've had it for ages ##line:4fa42d
    why? ##line:afffa9
    <<SetSpeaker Player>>
    ah well ##line:399b5c
    i was just talking to this rabbit ##line:6c4ba7
    she said she lost her headband\nsomewhere in the forest ##line:59a3a6
    <<SetSpeaker Original>>
    oh! you mean Sue? ##line:93be9c
    <<SetSpeaker Player>>
    maybe? ##line:922d13
    i didn't get her name ##line:b55f42
    she was really worried\nabout it though ##line:ce568d
    she said it was lucky and\nshe needs it to race ##line:b847d8
    <<SetSpeaker Original>>
    i'm supposed to race\nher later today! ##line:dc0d86
    she's so talented, i've been\nreally looking forward to it! ##line:273442
    hmm ##line:2046b6
    what if i gave you mine? ##line:929f1d
    i'm pretty sure it's the same kind ##line:b240c1
    maybe she wouldn't notice? ##line:69d63c
    <<SetSpeaker Player>>
    oh, hmm... ##line:16d732
    but ##line:ef9f2b
    then it wouldn't be lucky? ##line:b1a869
    <<SetSpeaker Original>>
    ah, i dunno ##line:0258ab
    just take it ##line:791844
    see if she wants it ##line:9e0da2
    <<Hide>>
    <<set $_TurtleGivenHeadband to true>>
    <<GiveItem Headband 1>>
<<else>>
    <<if $TurtleTalk1 is false>>
        <<SetSpeaker Player>>
        what's up? ##line:fb32fb
        <<SetSpeaker Original>>
        i'm just jogging ##line:3b6dd5
        warming up for a race later today ##line:ea4716
        i'm a little nervous ##line:84fda1
        there's gonna be a lot of\nskilled runners there! ##line:724568
        <<set $KnowsAboutRace to true>>
        <<set $TurtleTalk1 to true>>
    <<else>>
        gotta go speedy! ##line:df4afe
        just me and my <color=red>red headband!</color> ##line:76aaed
    <<endif>>
<<endif>>
===
title: ToMainIslandStart
tags: 
colorID: 0
position: 7918,-3880
---
<sprite name="LeftArrow" tint=1> Back To Hawk Peak Island ##line:02fd75
===
title: FrogProvincialElections
tags: 
colorID: 0
position: 11681,-4161
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    woah ##line:1c0f97
    the city has really\nbeen growing huh? ##line:b78d1a
    <<SetSpeaker Original>>
    oh this isn't a sand city any more ##line:9816e0
    this is a sand province! ##line:4990c7
    this time ##line:fb702b
    i'm running for premier\nin the provincial elections! ##line:deae8d
    sure, i wasn't elected as mayor ##line:46ef13
    but this time ##line:7c8950
    i'm really doubling\ndown on outreach ##line:482eea
    and i'm promoting policies\ni really think will resonate\nwith my key districts ##line:454b91
    once i'm elected ##line:d300d2
    i'll be able to use\nmy provincial influence ##line:abd4e5
    to finally exert control over\nthe city that shunned me! ##line:583dfc
    AHAHAHAHAA! ##line:bd795e
    <<SetSpeaker Player>>
    ... ##line:b3acd5
    woah ##line:e46429
    <<SetSpeaker Original>>
    and the first legislation i'll pass ##line:16fd54
    will exist to consolidate\nmy allies in parliament ##line:2c0bfd
    the second legislation ##line:1a6669
    FREE CANDY FOR ALL!! ##line:763841
    AHAHAHAH!! ##line:3420b4
    <<set $ListenedToFrogSpeech to true>>
<<else>>
    <<SetSpeaker Player>>
    so... ##line:50517f
    how's the campaigning going? ##line:f93dca
    <<SetSpeaker Original>>
    super good!! ##line:e883ea
<<endif>>
===
title: RabbitWithHeadband
tags: 
colorID: 0
position: 12632,-4357
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    so do you really think it's luck? ##line:59142c
    <<SetSpeaker Original>>
    well ##line:b1968a
    i don't know ##line:b4ccae
    i've gotten this far ##line:fe2d86
    and ##line:820490
    it's just me ##line:d4a604
    like... ##line:217933
    ...it's got to be luck ##line:b2e215
    <<SetSpeaker Player>>
    well maybe a little ##line:3eb454
    but ##line:cb5918
    you're so fast! ##line:9c3840
    and you've trained so much! ##line:24806b
    you got this far\ncuz you're GOOD! ##line:8f1d61
    <<SetSpeaker Original>>
    aw ##line:d6b5ef
    ... ##line:972ef1
    ...yeah ##line:d39233
    i know ##line:22e728
    i think i've known all along ##line:9a4de1
    but ##line:a77040
    i don't know ##line:6c5cd7
    i get so nervous ##line:2f57d9
    sometimes i wonder if\nthis is all a fluke ##line:cd435e
    ... ##line:45cfa5
    but you're right! ##line:3db99d
    i don't need luck ##line:87c100
    here, you can have\nthe headband back ##line:05e2ec
    i'm gonna do this\nall on my own! ##line:6e004d
    <<Hide>>
    <<GiveItem Headband 1>>
    <<set $_BunnyGotHeadband to false>>
    <<set $BunnyGiftHeadband to true>>
    <<SetSpeaker Player>>
    woah ##line:46396b
    thanks! ##line:c4e495
    good luck in the race ##line:0072ab
    you've got this!! ##line:cedcc0
<<else>>
    sorry, i can't chat right now ##line:cbbe18
    i've gotta prepare mentally and\nphysically for the race! ##line:be4f50
    see ya! ##line:af92f5
<<endif>>
===
title: HawkPeakAltSignStart
tags: 
colorID: 0
position: 7216,-3890
---
<sprite tint=1 name="UpArrow"> Hawk Peak Trail ##line:f07c9b
<sprite tint=1 name="Warning"> NOTICE <sprite tint=1 name="Warning"> ##line:49d9a1
<color=yellow>Hawk Peak Trail</color> is a strenuous hike! ##line:29e1de
You might need <color=yellow>golden\nfeathers</color> to complete it. ##line:48c18d
<color=yellow>Golden feathers</color> are for sale\nat the <color=yellow>visitor center.</color> ##line:fa84c0
===
title: RaceTurtleAfter
tags: 
colorID: 0
position: 13107,-4357
---
speed's my name and\nracing is my game! ##line:7388e5
actually my name is Taylor ##line:23d25d
but yeah ##line:201ebc
the important thing\nto remember is that ##line:6d2090
i go fast ##line:8ce4e4
===
title: WearingTurtleHat
tags: 
colorID: 0
position: 12862,-4359
---
<<if $BunnyGiftHeadband is true>>
    <<if $ExplainedHeadband is false>>
        so uh ##line:21411e
        are you just gonna keep\nthe headband yourself? ##line:8f2c69
        <<SetSpeaker Player>>
        no! ##line:5546c7
        um, well ##line:404764
        Sue actually gave it back ##line:94dd0b
        she said she doesn't wanna\nrely on luck anymore ##line:46d435
        <<SetSpeaker Original>>
        that's great! ##line:535119
        it's NOT luck ##line:99d2d2
        she's very talented ##line:a8e13e
        <<SetSpeaker Player>>
        so do you want the headband back? ##line:8ce0f2
        <<SetSpeaker Original>>
        eh ##line:b92c11
        you seem to like it ##line:8bd0cd
        you can have it ##line:6bc6e4
        <<set $ExplainedHeadband to true>>
    <<else>>
        [[RaceTurtleAfter]]
    <<endif>>
<<else>>
    <<if visited() is false>>
        hmm ##line:771311
        that headband looks familiar ##line:766c86
        <<SetSpeaker Player>>
        oh um ##line:7c21a4
        i'm just... ##line:66a5b7
        seeing out how fits ##line:db3fc8
        it looked comfy  ##line:edfd80
        <<SetSpeaker Original>>
        yeah it's pretty good ##line:3e41a9
    <<else>>
        so... ##line:fc7c57
        you're still giving my\nheadband a trial run? ##line:d07a27
        <<SetSpeaker Player>>
        uhh ##line:66311b
        it's just ##line:df8a6c
        my pockets are pretty full ##line:e63954
        this is the easiest way to carry it ##line:4d554f
        <<SetSpeaker Original>>
        fair enough ##line:a9b055
    <<endif>>
<<endif>>
===
title: GiveRabbitHeadband
tags: 
colorID: 0
position: 12389,-4350
---
<<SetSpeaker Original>>
hmm... ##line:da259b
that's not it ##line:9fc921
the sweat stains aren't\nin the right place ##line:33e75b
<<SetSpeaker Player>>
ew ##line:488a7a
<<SetSpeaker Original>>
where did you find this? ##line:0f391c
<<SetSpeaker Player>>
actually, the turtle gave it to me ##line:6022ce
they said you could have it ##line:b8eac1
they're looking forward to\nthe race and didn't\nwant you to miss it! ##line:1c787f
<<SetSpeaker Original>>
what?! ##line:f89496
REALLY?? ##line:b09c25
TAYLOR THE TURTLE!? ##line:2db8e8
they're like, the regional champion!? ##line:b15c5f
this headband has to be like ##line:7e105f
at least TWICE as lucky  ##line:ad2191
...possibly even... ##line:3509ed
QUADRUPLE lucky ##line:22dcd9
<<SetSpeaker Player>>
!!! ##line:316e5d
<<SetSpeaker Original>>
ok ##line:f666e4
i'll take it ##line:64abcd
i'm so pumped! ##line:de12bd
take these running shoes ##line:b2cecd
<<GiveItem Headband -1>>
<<set $_BunnyGotHeadband to true>>
<<set $BunnyQuestDone to true>>
<<set $_BunnyHappy to true>>
<<Hide>>
<<GiveItem RunningShoes 1>>
you can hold <color=red>{{RunButton}}</color> to run with them! ##line:d25506
however ##line:7e631b
just keep in mind that running\nrequires <color=red>golden feathers</color> ##line:0ce32d
===
title: OutlookPointSignStart
tags: 
colorID: 0
position: 7462,-3886
---
<sprite tint=1 name="UpArrow"> Outlook Point ##line:489295
===
title: Strawberries
tags: 
colorID: 0
position: 10285,-4373
---
<<if visited() is true>>
    <<SetSpeaker Player>>
    still out of strawberries? ##line:32213c
    <<SetSpeaker Original>>
    yup ##line:2edee7
<<else>>
    <<SetSpeaker Player>>
    so i heard about the strawberries ##line:90688d
    ...and it's 5PM ##line:50c6f6
    <<SetSpeaker Original>>
    aw shucks ##line:28fb4e
    we're all out ##line:75bc5b
    <<SetSpeaker Player>>
    what!? ##line:5a0e10
    but ##line:4787bc
    that's false advertising! ##line:9d23e0
    <<SetSpeaker Original>>
    sorry ##line:792b7e
    we usually pick them locally ##line:fa09b1
    the yield was weirdly low this year ##line:74f9e3
    i mean ##line:445678
    we still have lots of toast ##line:a9f2f4
    you want some toast? ##line:2badff
    <<SetSpeaker Player>>
    hook me up with some toast! ##line:b2dc23
    <<SetSpeaker Original>>
    alrighty then... ##line:5a9a92
    <<Hide>>
    <<GiveItem Toast 1>>
    <<SetSpeaker Original>>
    so, is there anything else you need? ##line:7dd7f0
<<endif>>
<<SetSpeaker Player>>
<<ShowCollection Coin>>
-> can i buy a <color=red>golden feather?</color> ##line:cb857e
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    [[BuyFeatherStart]]
-> what kind of merch do you sell? ##line:38ecce
    <<HideCollection Coin>>
    [[BuyMerchStart]]
-> i gotta jet ##line:29e369
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    see ya ##line:6fed20
===
title: EatToastStart
tags: 
colorID: 0
position: 10298,-3896
---
<<SetSpeaker Player>>
sure, i'll eat this toast ##line:912017
why not ##line:c77269
<<Hide>>
<<GiveItem Toast -1>>
<<PlaySound EatToast>>
<<Wait 1.5>>
mmm ##line:ad9d4d
i feel.... ##line:34c2bd
the same ##line:e92388
<<set $AteToast to true>>
<<GiveAchievement AteToast>> 
===
title: TucanStart
tags: 
colorID: 0
position: 10671,-3920
---
<<if visited() is false>>
    yo ##line:818d86
    this updraft is ON POINT! ##line:6f4292
    you can get some sick dives on it! ##line:f6e187
<<else>>
    heyo! ##line:07d094
    <sprite name="Music" tint=1> yeah i'm just chillin' here ##line:216605
    <sprite name="Music" tint=1> soakin' up those sweet sweet rays ##line:27042d
    <sprite name="Music" tint=1> as long as i got my SPF fifty ##line:fff7e4
    <sprite name="Music" tint=1> i could lie here for sweet sweet days ##line:dd17df
<<endif>>
<<SetSpeaker Player>>
-> how do you dive? ##line:3143e6
    <<SetSpeaker Original>>
    oh dang, it's easy ##line:cbb188
    but it takes some practice ##line:cf9948
    you gotta hear me out ##line:3adfca
    first ##line:d3aad8
    you gotta be gliding... ##line:69d4a2
    as you know ##line:e0ec7f
    you glide by <color=red>holding {{JumpButton}}</color> ##line:33f613
    then, if you're high enough\noff the ground ##line:3b003c
    <<if IsUsingJoystick() is true>>
        <<if CurrentPlatform() is "PlayStation">>
            <color=red>release the left stick</color>\nto dive downward ##line:aaaaa3
        <<else>>
            <color=red>release your control stick</color>\nto dive downward ##line:ade2f6
        <<endif>>
    <<else>>
        <color=red>release all arrow keys</color>\nto dive downward ##line:ed17e5
    <<endif>>
    but you must still remember\nto <color=red>hold {{JumpButton}}</color> ##line:4c73ef
    that's the tricky part! ##line:38fdce
    with a good dive, you can\n<color=red>build up a lot of speed</color> ##line:2b369a
    you can practice by gliding\nover this updraft ##line:58343b
    that'll give you a lot of\nheight to work with! ##line:547d16
-> sounds nice ##line:288233
    <<SetSpeaker Original>>
    dang, you KNOW it ##line:b2dd9d
    it's nice like some rice with spice ##line:d3f65e
===
title: Cellphone2
tags: 
colorID: 0
position: 13432,-4275
---
<<Wait 1>>
<<SetSpeaker Player>>
*huff* ##line:1eaf2b
phew ##line:c2341c
<<Hide>>
<<Wait 1>>
i... ##line:9dbecf
<<Hide>>
<<Wait 0.6>>
...i did it! ##line:c8f624
<<Hide>>
<<SendMessage MountainTopCutscene ShowSitCamera>>
<<Wait 1.5>>
<<SetPose Sit>>
<<Wait 5>>
<<PlaySound Whippoorwill>>
<<Wait 10>>
<<PlaySound LoonCall>>
<<Wait 15>>
<<PlaySound PhoneRing>>
<<Wait 0.5>>
<<SetPose RaiseArms>>
!!! ##line:a329a8
CELLPHONE RECEPTION!! ##line:f43890
[[Cellphone3]]
===
title: CellphoneStart
tags: 
colorID: 0
position: 13425,-4521
---
<<set $_ClimbedHawkPeak to true>>
<<if $ReachedTopCutscene is false>>
    <<set $ReachedTopCutscene to true>>
    [[Cellphone2]]
<<endif>>
===
title: YellAtMayStart
tags: 
colorID: 0
position: 10914,-3918
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    i can see my house from here! ##line:8c98d3
    ... ##line:d88089
    ahem ##line:d84529
    <<SetPose RaiseArms>>
    HEYYYYYYYYY!!! ##line:a64258
    AUNT MAYY!!!!! ##line:715ae9
    <<SetSpeaker AuntMayNPC>>
    yes, claire? ##line:d8a3ab
    <<SetSpeaker Player>>
    YEAAAH! ##line:4df5b5
    IT'S ME!!!! ##line:445e93
    CAN YOU HEAR ME UP HERE!!??!? ##line:ccdb15
    <<SetSpeaker AuntMayNPC>>
    sure can ##line:fa6a18
    <<SetSpeaker Player>>
    OHHH ##line:8a59ef
    OKAY ##line:8ee909
    <<SetSpeaker AuntMayNPC>>
    have fun up there! ##line:ae3ea7
    <<SetSpeaker Player>>
    YEAAAHH ##line:90a5b4
    I WILL!! ##line:6541f6
    <<ClearPose Player>>
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    <<SetPose RaiseArms>>
    SO ##line:c00ec6
    WHATCHA DOING!? ##line:8f00a0
    <<ClearPose Player>>
    <<SetSpeaker AuntMayNPC>>
    oh, you know ##line:5d2a04
    the usual ##line:8c722c
    <<SetSpeaker Player>>
    OOKAAY ##line:ffba56
    COOL ##line:47aad8
<<elseif visitCount() is 2 and $TalkedAboutDinner is false>>
    <<SetSpeaker Player>>
    <<SetPose RaiseArms>>
    WHAT'S FOR DINNER TONIGHT!? ##line:9caf98
    <<SetSpeaker AuntMayNPC>>
    uhh ##line:488fb8
    i think i'm gonna\nmake curry tonight ##line:440bf7
    <<set $CurryDinner to true>>
    <<SetSpeaker Player>>
    SOUNDS TASTYY!! ##line:9ead4c
    <<ClearPose Player>>
<<else>>
    <<SetSpeaker Player>>
    <<SetPose RaiseArms>>
    HEY AUNT MAY ##line:b3537e
    <<SetSpeaker AuntMayNPC>>
    yeah? ##line:6e8868
    <<SetSpeaker Player>>
    I DUNNOOO! ##line:530dce
    I GUESS I'M BORED!!! ##line:e148b8
    <<SetSpeaker AuntMayNPC>>
    i can tell! ##line:1ae3c3
    <<ClearPose Player>>
<<endif>>
===
title: ExplainDayToMom
tags: 
colorID: 0
position: 13068,-3386
---
[[Cellphone4]]
===
title: Cellphone3
tags: 
colorID: 0
position: 13458,-4011
---
<<Hide>>
<<SendMessage MountainTopCutscene TakeOutCellphone>>
<<SendMessage MountainTopCutscene ShowCellCamera>>
<<PlaySound PhonePickUp>>
<<SetPose OnPhone>>
<<Wait 0.5>>
hello? ##line:938517
<<SetSpeaker CutsceneCellphone>>
hi, claire ##line:5f8c0e
<<SetSpeaker Player>>
<<PlayMusic MomMusicSet>>
mom! ##line:2887df
i'm so glad to hear from you ##line:db60b7
i've been waiting all day to hear from you ##line:ae46f6
i've been worried ##line:fb2a4f
<<SetSpeaker CutsceneCellphone>>
aw honey ##line:918f79
i told you i'd be fine! ##line:3aaa27
<<SetSpeaker Player>>
yeah ##line:c29b2a
but you always say that ##line:625c90
<<SetSpeaker CutsceneCellphone>>
and i always am! ##line:5fed41
<<SetSpeaker Player>>
... ##line:a8b951
<<SetSpeaker CutsceneCellphone>>
anyway ##line:7f98d9
how have you been? ##line:3e93b6
<<SetSpeaker Player>>
well ##line:a0dcaa
i'm at the top of Hawk Peak right now! ##line:c94b31
<<SetSpeaker CutsceneCellphone>>
ooh! ##line:a63e6e
wow ##line:2237e8
i love it up there ##line:6eb468
May and i used to go up\nevery summer together ##line:5ea065
it's about time you made it up! ##line:33337b
<<SetSpeaker Player>>
yeah... i know ##line:4094cb
<<SetSpeaker CutsceneCellphone>>
you're so big now ##line:3c83f7
my little girl's leaving the nest ##line:a36f51
you won't have any need\nfor little ol' mom ##line:ac8db8
<<SetSpeaker Player>>
but i DO need you ##line:efe93a
i'll never not need you! ##line:8ff0d0
when i heard about the surgery... ##line:943931
...how come you never told\nme this was a thing!? ##line:07950b
<<SetSpeaker CutsceneCellphone>>
i didn't want you to worry ##line:be076a
<<SetSpeaker Player>>
but ##line:6a1b43
<<SetSpeaker CutsceneCellphone>>
i know you can get worked up about things ##line:fae7e7
<<SetSpeaker Player>>
i could have been there for you ##line:6315da
you're all alone there, now ##line:b1f752
<<SetSpeaker CutsceneCellphone>>
really, it was nothing! ##line:66e500
everything was fine ##line:2acde0
<<SetSpeaker Player>>
<<Hide>>
<<Wait 1>>
i'm just glad you're ok ##line:666129
i love you, ##line:d2ddf0
mom ##line:e25ebd
<<SetSpeaker CutsceneCellphone>>
i love you too ##line:6a17a6
<<Hide>>
<<Wait 1>>
[[ExplainDayToMom]]
===
title: Cellphone4
tags: 
colorID: 0
position: 13067,-3138
---
<<Hide>>
<<Wait 1>>
<<SendMessage MountainTopCutscene StartRumble>>
<<Wait 1>>
<<SetSpeaker Player>>
wait ##line:2e7f4d
something's happening?! ##line:545678
<<Hide>>
<<Wait 1>>
<<SendMessage MountainTopCutscene ShowLakeCamera>>
<<Wait 1>>
<<SendMessage MountainTopCutscene StartBubbles>>
<<Wait 2>>
<<SendMessage MountainTopCutscene ActivateHotSpring>>
<<SendMessage MountainTopCutscene FinishRumble>>
<<Wait 2>>
<<SendMessage MountainTopCutscene HideLakeCamera>>
<<Wait 1>>
<<SetSpeaker CutsceneCellphone>>
was that the famous Hawk Peak updraft!? ##line:9a8ed9
<<SetSpeaker Player>>
uh ##line:2a159c
looks like it ##line:9fcd15
<<SetSpeaker CutsceneCellphone>>
you should catch a ride on it now! ##line:9ef186
before it fizzles out ##line:1d16be
<<SetSpeaker Player>>
i wanna ##line:09fc02
but i'm kinda nervous ##line:a32f4d
<<SetSpeaker CutsceneCellphone>>
claire ##line:d5621b
you can do it! ##line:0d150f
i'll come visit you soon, ok? ##line:4c5080
also ##line:e45965
don't forget to say hi to <color=red>Aunt May</color> for me! ##line:82a853
she's probably waiting for\nyou back at the cottage ##line:e7b047
bye, love you! ##line:52f04c
<<SetSpeaker Player>>
love you too, mom ##line:230817
<<SendMessage MountainTopCutscene PutAwayCellphone>>
<<SendMessage MountainTopCutscene HideCellCamera>>
<<ClearPose>>
===
title: AuntFinished
tags: 
colorID: 0
position: 10534,-6157
---
<<if $HadFinalAuntDialogue is false>>
    <<SetSpeaker Original>>
    hey ##line:85188e
    [[AuntFinishedBody]]
<<else>>
    [[AuntFinalDialogue]]
<<endif>>
===
title: EndGameStart
tags: 
colorID: 0
position: 11032,-5880
---
i think it'd be nice to take a break ##line:2ed2fe
-> sleep ##line:be5dbc
    alrighty ##line:fd5647
    it's nap time! ##line:33151c
    <<set $_CanEndGame to true>>
-> not yet ##line:6b397e
===
title: Node101
tags: 
colorID: 0
position: 13301,-3379
---
so what has it been like there? ##line:0493bf
<<SetSpeaker Player>>
well ##line:f8414d
===
title: Node102
tags: 
colorID: 0
position: 13304,-3140
---
i need to talk about how to dive, if u havent been given the tutorial yet ##line:65055a
===
title: TitleScreenIntroStart
tags: 
colorID: 0
position: 8817,-4718
---
<<Wait 1.2>>
<<SetSpeaker AuntSpeaker>>
i never get tired of this view ##line:45a376
claire, ##line:cda187
you're gonna have a great\ntime here this summer! ##line:d7aeb5
i think ##line:760ec9
it'll be good to get\naway from the city ##line:62ea07
and take a break from\neverything else going on ##line:d3607e
<<Hide>>
<<Wait 1.5>>
are you still awake? ##line:3fd92a
<<SetSpeaker Original>>
mmmmm ##line:049cc1
maybe ##line:302117
<<SetSpeaker AuntSpeaker>>
hold on a little longer! ##line:dedd03
we're almost at the ferry ##line:a605f1
<<Hide>>
<<Wait 1>>
===
title: ConsiderKidQuest
tags: 
colorID: 0
position: 9677,-4061
---
<<SetSpeaker Player>>
-> i'll take your "side gig" ##line:126e5b
    <<SetSpeaker Original>>    
    good ##line:0d9c32
    take this... ##line:d2d27b
    <<set $AcceptedKidQuest2 to true>>
    <<Hide>>
    <<GiveItem ShellNecklace 1>>
    ...and deliver it to Ranger May ##line:85f4ce
    that is all ##line:f109e4
-> nope ##line:03a23c
    <<SetSpeaker Original>>    
    suit yourself ##line:3232ef
===
title: AuntNecklace
tags: 
colorID: 0
position: 10251,-6132
---
<<set $DeliveredNecklace to true>>
<<SetSpeaker Player>>
i've got a delivery! ##line:b0f25b
it's from Jen ##line:1dcac3
<<GiveItem ShellNecklace -1>>
<<Hide>>
<<Wait 1>>
<<SetSpeaker Original>>
oh wow! ##line:168e81
it's so pretty ##line:e06d87
she's such a sweet girl ##line:ca470a
collecting all those shells\nmust have been a lot of work ##line:13f4b4
<<SetSpeaker Player>>
well actually... ##line:07c5c9
<<SetSpeaker Original>>
hmm? ##line:e1489b
<<SetSpeaker Player>>
eh never mind ##line:a40af1
<<SetSpeaker Original>>
oh! ##line:240101
i remembered something ##line:fede28
i found this golden feather in the cabin ##line:312906
i figured you could use it ##line:21415c
<<Hide>>
<<GiveItem GoldenFeather 1>>
<<SetSpeaker Player>>
<<Face Original Player>>
woah, thanks ##line:95fb1a
this is ##line:31d44f
weird timing ##line:3ac7bb
is this like, a reward for\ndelivering the necklace? ##line:594e0f
<<SetSpeaker Original>>
uh, no? ##line:4f7537
<<SetSpeaker Player>>
huh ##line:324d4c
<<if $_ClimbedHawkPeak is true and $HadFinalAuntDialogue is false>>
    <<Hide>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    oh, by the way ##line:2d2083
    [[AuntFinishedBody]]
<<endif>>
===
title: FrogTrade
tags: 
colorID: 0
position: 11172,-3924
---
<<SetSpeaker Original>>
ohh that's perfect ##line:727813
let's trade! ##line:39b491
<<GiveItem ToyShovel -1>>
<<Hide>>
<<Wait 1>>
<<set $_GotShovel to true>>
<<GiveItem Shovel 1>>
when you want to use my shovel... ##line:0c7755
<color=red>press {{MenuButton}}</color> to open your inventory\nand <color=red>equip it</color> ##line:dfbca3
keep it fresh! ##line:52873b
===
title: PolarBearStart
tags: 
colorID: 0
position: 11950,-2823
---
<<if visited() is false>>
    hold on there,\nyoung friend ##line:08d344
    i hope you don't mind me saying ##line:acea5b
    but you seem a\nlittle unprepared ##line:1099bb
    it's mighty cold up here for\nfolks who aren't accustomed ##line:817652
    <<SetSpeaker Player>>
    i didn't expect it to get this cold! ##line:2136cc
    my feathers are freezing up ##line:eb8b41
    <<SetSpeaker Original>>
    Hawk Peak is unique in the\nprovince for geothermal activity ##line:e544b0
    if you get too chilly, stop\nat one of the hotsprings ##line:0c4c62
    that should give you\nback your strength! ##line:00e22d
    <<if $ReadHawkPeakSign is false and GetItemCount("GoldenFeather") < 7>>
        <<SetSpeaker Player>>
        thanks for the tip! ##line:53a164
        uh ##line:da7689
        am i almost at the top? ##line:4e76d8
        <<SetSpeaker Original>>
        well, yes... ##line:6535ef
        but it's a tricky climb ##line:035a7d
        you might need more <color=red>golden\nfeathers</color> than you have now ##line:c3ba01
    <<endif>>
    <<set $_TalkedToPolarBear to true>> 
<<else>>
    <<if $PunMade is false and $_ClimbedHawkPeak is true>>
        hey there ##line:6e4900
        did you get up safely? ##line:81a16c
        <<SetSpeaker Player>>
        yup! ##line:2f558c
        well ##line:1bd829
        <<SetEmotion Happy>>
        you could say i... ##line:05be99
        ...BEAR-ly made it! ##line:fef811
        haha ##line:bd9ba7
        <<SetSpeaker Original>>
        ... ##line:100ad6
        <<SetSpeaker Player>>
        <<ClearEmotion>>
        do you get it? ##line:e09a0c
        <<SetSpeaker Original>>
        oh ##line:c8da80
        i get it ##line:b44e22
        <<set $PunMade to true>>
    <<elseif $_CampfireFriendsBegin is true>> 
        [[PolarBearSitStart]] 
    <<else>>
        be safe out there! ##line:835d44
        i'm the one who has to\nradio your aunt if\nyou get into trouble!     ##line:53a781
    <<endif>>
<<endif>>
===
title: Runner2Start
tags: 
colorID: 0
position: 9355,-3323
---
<<if visited() is false>>
    running is my life ##line:c3e961
    i run marathons all around the world ##line:bfbadb
<<else>>
    what if ##line:773dc9
    running wasn't my life ##line:11536d
    but i was actually using running ##line:36d1dd
    to run FROM life!? ##line:8de64b
    ... ##line:c173c6
    that's a troubling thought ##line:670a59
<<endif>>
===
title: Runner3Start
tags: 
colorID: 0
position: 9576,-3315
---
<<if $AskedRunnerAboutRace is true>>
    SPEED FIRST!! ##line:f72c0e
    TALK LATER!! ##line:5fd249
<<else>>
    <<if visited() is false>>
        <<if $KnowsAboutRace is true>>
            <<SetSpeaker Player>>
            hey! ##line:82d424
            are you doing the race today? ##line:3875bf
            <<SetSpeaker Original>>
            yeah ##line:199978
            i'm kinda busy right now ##line:24dca3
            what do you want? ##line:205f2e
        <<else>>
            <<SetSpeaker Player>>
            what are you jogging for? ##line:4259b6
            <<SetSpeaker Original>>
            excuse me? ##line:575dc5
            i'm training for the race! ##line:ec50e2
            what do you want? ##line:f41820
        <<endif>>
    <<else>>
        you again? ##line:4c1ab9
        what do you want? ##line:6e934b
    <<endif>>
    <<set $KnowsAboutRace to true>>
    <<SetSpeaker Player>>
    -> can i join the race? ##line:bed782
        <<SetSpeaker Original>>
        <<set $AskedRunnerAboutRace to true>>
        *sigh* ##line:0a5f04
        registration ended\nlike, last month ##line:f09f2c
        why does no one\nread the rules!? ##line:d9fe0a
        you're <color=red>the second person</color>\ni've had to turn away today! ##line:538f2a
        anyway ##line:4b379d
        i gotta go ##line:9cdf84
    -> nothing ##line:f0d6a7
        <<SetSpeaker Original>>
        welp ##line:2fffae
        i gotta go ##line:b89bd1
<<endif>>
===
title: Runner4Start
tags: 
colorID: 0
position: 9795,-3306
---
<<if $NephewHi is true>>
    <<if $NephewHiDone is false>>
        ugh... ##line:6ce8aa
        i hate leg days ##line:682dfd
        <<SetSpeaker Player>>
        oh uh ##line:919d2e
        your uncle wanted me to remind you ##line:486301
        to take breaks ##line:412751
        <<if $Hydration is true>>
            and to stay hydrated ##line:dc639a
        <<endif>>
        <<SetSpeaker Original>>
        that's good advice ##line:c9231c
        just one more lap first... ##line:17a4fb
        <<set $NephewHiDone to true>>
    <<else>>
        <<SetSpeaker Player>>
            good luck! ##line:d74fa4
        <<SetSpeaker Original>>
            thanks ##line:41e6b6
    <<endif>>
<<else>>
    ugh... ##line:5cafa7
    i hate leg days ##line:771cf2
<<endif>>
===
title: BucketSignStart
tags: 
colorID: 0
position: 7660,-3657
---
The Helianthus Dissultantis "Rubber Flower" ##line:21cf22
<<if visited() is false>>
    Hawk Peak Provincial Park's Official Flower ##line:ac8167
<<endif>> 
This unique species <color=yellow>blooms instantly</color>\nin flash flooding conditions, inflating\nlike a water balloon! ##line:e36269
Experience it for yourself with one of\nthe park's communal <color=yellow>water buckets!</color> ##line:ec2521
<<if visited() is false>>
    <color=#D7876F>*All buckets are property of the park.</color> ##line:da1b6e
    <color=#D7876F>Please do not remove buckets\nfrom the park!!</color> ##line:9d3c78
<<endif>> 
<<SetSpeaker Player>>
<<if visited() is false>>
    hmm ##line:cdb11f
    a bucket full of water seems heavy... ##line:893c47
    i don't think i could fly\nor climb carrying a bucket\nfull of water... ##line:dc899d
    but i could probably still glide! ##line:c03601
<<else>>
    i don't think i could fly\nor climb carrying a bucket\nfull of water ##line:37428a
    but i could probably still glide! ##line:e51435
<<endif>>
===
title: ToughBirdExit
tags: 
colorID: 0
position: 11255,-5042
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
whatever, suit yourself ##line:0511de
===
title: ToughBirdWhatDeal
tags: 
colorID: 0
position: 11167,-4770
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
what's MY deal? ##line:cee433
HMPH! ##line:6cdec4
what's YOUR deal!? ##line:20b440
<<SetSpeaker Player>>
well... ##line:5cd3d2
i'm hiking up to hawk peak ##line:3a5721
<<SetSpeaker Original>>
wow ##line:763c29
sounds kinda basic ##line:ca2ad4
<<SetSpeaker Player>>
hey! ##line:c72c5f
<<SetSpeaker Original>>
i'm just saying ##line:a73677
tourists come from all over ##line:15eb7a
they're like ##line:d047c2
"i'm goonnaa hike to hawwk peaak" ##line:87fc49
then they get up there ##line:dc79a1
and see that it actually\ntakes real effort ##line:df37dd
and then they give up ##line:65f61c
<<SetSpeaker Player>>
geez ##line:8e3aa9
you don't have to be ##line:d4abe7
...like that ##line:1cc87c
<<SetSpeaker Original>>
i'm just saying ##line:0a3403
it's a little basic ##line:11e3a4
<<SetSpeaker Player>>
so ##line:47ba90
what IS your deal? ##line:cf0784
i gave you mine ##line:5ddd9c
<<SetSpeaker Original>>
i need money ##line:0d3cc3
<<SetSpeaker Player>>
... ##line:edd8e9
<<SetSpeaker Original>>
*sigh* ##line:7973bb
i'm gonna be short on\ntuition next semester ##line:adfb25
i'm nearly there though ##line:cf2e64
just trying to make some extra cash ##line:a6c367
<<SetSpeaker Player>>
huh ##line:dc1233
how much do you need ##line:acb2a8
<<SetSpeaker Original>>
... ##line:30eb73
well after i sell\nall my junk ##line:ac88f8
i'll still be short by about 400 coins... ##line:435122
<<SetSpeaker Player>>
oh ##line:72431d
<<SetSpeaker Original>>
yeah... ##line:6f84d5
but whatever ##line:a86edb
that's life i guess ##line:bad128
<<set $ToughBirdsProblem to true>>
===
title: ToughBirdTuition
tags: 
colorID: 0
position: 10932,-4765
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
wait what? ##line:9cf882
really? ##line:cbaa8d
<<SetSpeaker Player>>
yeah sure ##line:9284d3
<<SetSpeaker Original>>
but why? ##line:aff092
<<SetSpeaker Player>>
i dunno ##line:7a98aa
i found most of this just by\nlooking around the island ##line:9695ed
i'm pretty good at finding stuff ##line:131aa6
<<SetSpeaker Original>>
wow ##line:6428d7
uh ##line:fda1cc
i don't know what to say ##line:be049a
<<SetSpeaker Player>>
it's 400 coins, right? ##line:67bb03
here ya go! ##line:fb019d
<<PlaySound CoinGet>>
<<GiveItem Coin -400>>
<<Hide>>
<<Wait 1>>
<<SetSpeaker Original>>
... ##line:07ddb5
thanks ##line:a36d85
um ##line:2d0e86
you can have this ##line:d33091
<<Hide>>
<<GiveItem Watch 1>>
<<set $ToughBirdFriend to true>>
i uh ##line:bd574d
found it ##line:ffdcaf
i was gonna sell it on\nthe internet or whatever ##line:0f0128
but i don't need it anymore ##line:13777d
<<SetSpeaker Player>>
huh ##line:6efae9
okay ##line:c0e365
===
title: ToughBirdExitNeutral
tags: 
colorID: 0
position: 11245,-5253
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
later ##line:c84104
===
title: ToughBirdBuyFeatherNice
tags: 
colorID: 0
position: 11068,-5474
---
<<HideCollection Coin>>
<<if $ToughBirdSales < 4>>
    <<SetSpeaker Original>>
    <<if visited() is false>>
        oh yeah ##line:fafc6f
        no problem ##line:2068f8
        you can have it for the\nregular price of 40 coins ##line:cb2443
    <<else>>
        sure! ##line:6938d1
        you can have it for 40 coins ##line:9f5c4f
    <<endif>>
    <<if HasItem("Coin", 40) is true>>
        <<set $ToughBirdSales to $ToughBirdSales + 1>>
        <<GiveItem Coin -40>>
        <<PlaySound BuySound>>
        <<Hide>>
        <<Wait 1>>
        <<GiveItem GoldenFeather 1>>
        see ya later ##line:db7350
    <<else>>
        <<SetSpeaker Player>>
        oh wait ##line:99287e
        i'm short on cash ##line:6cf4bf
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    actually... ##line:f67146
    you bought all the\nfeathers i had left ##line:e5cbce
    sorry ##line:19dabf
    <<SetSpeaker Player>>
    that's OK ##line:7fcff9
<<endif>>
===
title: GoatDone
tags: 
colorID: 0
position: 11826,-5155
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    the watch is really nice ##line:3e8976
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    why thank you ##line:7f5ed0
    <<ClearEmotion>>
    it was my grandfather's ##line:97c9fe
    it's been all around the world ##line:546ed9
<<else>>
    i've actually lost this watch before ##line:c747af
    luckily ##line:fa221d
    it's always seemed to find its way home ##line:89b012
<<endif>>



===
title: GoatGive
tags: 
colorID: 0
position: 11832,-4928
---
<<SetSpeaker Player>>
hey, i think i found your watch ##line:7bd1ef
<<Hide>>
<<GiveItem Watch -1>>
<<Wait 1>>
<<SetSpeaker Original>>
yes! ##line:eff68a
that's it!! ##line:677360
thank you! thank you!! ##line:abc1c9
<<SetSpeaker Player>>
you're welcome! ##line:d4347e
<<SetSpeaker Original>>
i ought to return the favour ##line:094523
please ##line:03dcec
take this ##line:d95867
<<Hide>>
<<GiveItem SilverFeather>>
silver feathers are very rare ##line:85f9d5
with one in hand, ##line:72666e
you can climb faster ##line:6083ad
and fly higher! ##line:94f75d
<<set $GoatWatchFound to true>>
===
title: RhinoStart
tags: 
colorID: 0
position: 11708,-3815
---
<<if $JoinedClimbClub is true>>
    [[RhinoInClub]]
<<endif>>
<<if visited() is true>>
    hey again! ##line:4bd63e
    [[RhinoAsk]]
<<endif>>
hey there! ##line:dc151d
what are you up to today? ##line:036988
<<SetSpeaker Player>>
uh ##line:978863
i'm hiking up to Hawk Peak ##line:8f879a
<<SetSpeaker Original>>
that's a tough hike! ##line:af7466
you know ##line:2d5d1b
you'll need to learn to <color=red>rock climb</color>\nto make it to the top! ##line:73be56
why not join our\nrock climbing club? ##line:88b586
<<SetSpeaker Player>>
oh uh ##line:64794d
is it just the two of you? ##line:510fdb
<<SendMessage SquirrelCutsceneCamera Enable>>
<<SetSpeaker ClimbSquirrel>>
<<Face ClimbSquirrel Player>>
HEY! ##line:7149bd
<<LookAt ClimbSquirrel Player>>
THE ROCK CLIMBERS ASSOCIATION\nHAS HUNDREDS OF MEMBERS SPANNING\nOVER 35 COUNTRIES WORLDWIDE! ##line:1535cf
HUNDREDS OF MEMBERS GROWING AND\nREFINING THE ART OF ROCK CLIMBING ##line:ee02f6
PUSHING THE CRAFT TO ITS\nABSOLUTE LIMIT AND BEYOND ##line:239d8b
<<SetSpeaker Player>>
uh ##line:ee8510
i meant ##line:53d1c0
if it's just the two of you... ##line:258d4f
like, ##line:ab6dd5
here? ##line:47b978
<<SetSpeaker ClimbSquirrel>>
... ##line:195560
WHAT OF IT!? ##line:e54ad7
LISTEN KID ##line:451176
CLIMBING AIN'T FOR THE WEAK OF HEART ##line:d84a7a
MOST PEOPLE ##line:93890e
DON'T HAVE WHAT IT TAKES ##line:74a72e
<<SetSpeaker Original>>
<<SendMessage SquirrelCutsceneCamera Disable>>
<<Face Original Player>>
<<ClearLookAt Player>>
um ##line:f6efc5
it's not really that bad! ##line:5ff3ad
so anyway ##line:9b7f63
[[RhinoAsk]]
===
title: RhinoAsk
tags: 
colorID: 0
position: 11957,-3822
---
you want to join the climbers club? ##line:021ae9
<<SetSpeaker Player>>
-> sure ##line:132ea9
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    great!! ##line:95ed98
    we've got our third member! ##line:50d470
    <<SetSpeaker ClimbSquirrel>>
    NOT THIRD ##line:ce0480
    JUST ANOTHER DROP ##line:a3badc
    IN THE HUNDREDS ##line:3b1fed
    WORLDWIDE ##line:e7d66d
    <<SetSpeaker Original>>
    alright alright ##line:084264
    anyway ##line:a4fa72
    <<ClearEmotion>>
    <<set $JoinedClimbClub to true>>
    [[RhinoInClub]]
-> nah ##line:fa8e51
    <<SetSpeaker Original>>
    aww well ##line:99ea93
    let me know if you\nchange your mind ##line:8663c6
===
title: RhinoInClub
tags: 
colorID: 0
position: 11854,-3563
---
<<if $RhinoTut is true>>
    [[RhinoTut]]
<<endif>>
<<if HasItem("GoldenFeather", 1) is false and $RhinoSaidGetFeather is true and $TalkedToVisitorCenter is true and HasItem("Coin", 40) is false>>
    <<SetSpeaker Original>>
    so you got that feather? ##line:00fa8b
    <<SetSpeaker Player>>
    not yet... ##line:1758a1
    they cost 40 coins ##line:9ee6ae
    and i only have {{Item_Coin}} ##line:f629a3
    <<SetSpeaker Original>>
    oh! ##line:968bf9
    hmm ##line:e63187
    i can spot you the rest! ##line:e5a798
    <<Hide>>
    <<PlaySound CoinGet>>
    <<FillItem Coin 40>> 
    <<Wait 1>>
    <<SetSpeaker Player>>
    woah, thanks! ##line:1557db
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    no problem! ##line:8586a7
    <<ClearEmotion>>
<<else>>
    <<SetSpeaker Original>>
    <<if visited() is false>>
        so first things first ##line:606c86
    <<endif>>
    you got a <color=red>golden feather?</color> ##line:c6f899
    <<if HasItem("GoldenFeather", 1) is true>>
        <<SetSpeaker Player>>
        yup! ##line:18ca34
        <<SetSpeaker Original>>
        great! ##line:7ba1fd
        [[RhinoTut]]
    <<else>>
        <<SetSpeaker Player>>
        oh, not yet ##line:668b67
        <<SetSpeaker Original>>
        ah ##line:9703a6
        you can't climb without one ##line:2b1f0e
        but you can buy them at the <color=red>visitor center</color> ##line:03ff54
        they should be pretty cheap ##line:1ae06c
        <<set $GoldenFeatherAdvice to true>>
        <<set $RhinoSaidGetFeather to true>>
        come back when you've bought one! ##line:b884a1
    <<endif>>
<<endif>>
===
title: RhinoTut
tags: 
colorID: 0
position: 12103,-3564
---
<<SetSpeaker Original>>
<<if visited() is false>>
    so, to get started ##line:cd696e
    you can try climbing this <color=red>short wall</color> here ##line:d83ef3
    just <color=red>push against the wall</color> and <color=red>hold {{JumpButton}} ##line:869a9d
    <<set $RhinoTut to true>>
<<elseif $Climbed2 is true>>
    [[RhinoTalk]]
<<elseif $Climbed1 is true>>
    try climbing up the big one next! ##line:514aa4
    <<if HasItem("GoldenFeather", 3) is false>>
        you might need to jump\nfrom the other platform ##line:56818a
    <<endif>>
<<else>>
    try climbing the <color=red>short wall</color> here! ##line:db5c24
    just <color=red>push against the wall</color> and <color=red>hold {{JumpButton}} ##line:c4615f
<<endif>>
===
title: ClimbWall1Start
tags: 
colorID: 0
position: 11866,-3335
---
<<if $RhinoTut is true and $Climbed1 is false and $Climbed2 is false>>
    <<Face Original Player>>
    nice one! ##line:1ffe75
    <<SetSpeaker ClimbSquirrel>>
    WHATEVER ##line:7fe2ae
    <<SetSpeaker Original>>
    ... ##line:b36522
    <<SetSpeaker ClimbSquirrel>>
    that climb is easy ##line:6bfb52
    real pros use the big wall ##line:1d4aeb
    <<set $Climbed1 to true>>
<<endif>>
===
title: ClimbWall2Start
tags: 
colorID: 0
position: 11870,-3110
---
<<if $RhinoTut is true and $Climbed2 is false>>
    <<Face Original Player>>
    woah, good climbing! ##line:067362
    <<SetSpeaker ClimbSquirrel>>
    eh it was alright ##line:adcee5
    <<SetSpeaker Original>>
    so ##line:92f199
    i know you can fly and all ##line:e4504b
    but your <color=red>stamina will last longer</color>\nif you climb places instead ##line:2db8d4
    just something to keep in mind ##line:3963cb
    <<SetSpeaker Player>>
    cool ##line:9d5f6b
    so what's next? ##line:de1b34
    <<SetSpeaker ClimbSquirrel>>
    TO HAWK PEAK! ##line:10be3d
    <<SetSpeaker Player>>
    oh ok ##line:7a4c15
    <<SetSpeaker Original>>
    i still need to warm up myself ##line:862787
    but i'll see ya up there? ##line:7c9442
    <<SetSpeaker Player>>
    alright! ##line:b2c223
    <<set $Climbed2 to true>>
    <<set $_ClimbersBefriended to true>>
<<endif>>
===
title: FlyWall1Start
tags: 
colorID: 0
position: 12097,-3337
---
<<if $RhinoTut is true and $BadFly1 is false and $Climbed1 is false and $Climbed2 is false>>
    <<Face Original Player>> 
    uhh ##line:3395b7
    i know you can fly and all ##line:7d2364
    but this is the climbing club ##line:d53273
    <<SetSpeaker ClimbSquirrel>>
    yeah! ##line:677918
    the CLIMBING club! ##line:0cb735
    <<SetSpeaker Original>>
    ... ##line:1000df
    anyway ##line:e3d719
    could you try climbing\nup there instead? ##line:ed61f7
    <<set $BadFly1 to true>>
<<endif>>
===
title: FlyWall2Start
tags: 
colorID: 0
position: 12102,-3106
---
<<if $RhinoTut is true and $BadFly2 is false and $Climbed2 is false>>
    <<Face Original Player>> 
    flying up there is cool ##line:ec75d1
    but ##line:cc8125
    this is a club about climbing... ##line:ae24d2
    <<SetSpeaker ClimbSquirrel>>
    yeah! ##line:7a5fee
    it kinda seems like\nyou're just showing off ##line:c60dfd
    <<SetSpeaker Player>>
    no, i'm not! ##line:486524
    i just ##line:1acd7e
    like to fly places i guess ##line:d22a9a
    <<SetSpeaker Original>>
    give climbing a try! ##line:c63312
    it's fun too ##line:65c4e9
    <<set $BadFly2 to true>>
<<endif>>
===
title: RhinoTalk
tags: 
colorID: 0
position: 12363,-3567
---
<<if visited() is false>>
    uh ##line:61f2b9
    sorry about tim ##line:198b54
    it's not personal ##line:43219b
    he's just cranky today ##line:c08537
    <<SetSpeaker ClimbSquirrel>>
    I'M FINE! ##line:6afb4f
    <<SetSpeaker Original>>
    we're really glad you joined! ##line:e8b063
    <<SetSpeaker Player>>
    uh ##line:823b1b
    thanks! ##line:694ad1
<<else>>
    <<SetSpeaker Player>>
    so why is he cranky? ##line:f1bb42
    <<SetSpeaker Original>>
    uh ##line:669fe9
    actually ##line:3cc537
    he's always a little bit cranky ##line:a7c663
    <<SetSpeaker ClimbSquirrel>>
    I CAN HEAR YOU, YOU KNOW!? ##line:531d58
<<endif>>
===
title: SquirrelStart
tags: 
colorID: 0
position: 12325,-3831
---
<<if $JoinedClimbClub is false>>
    i'm busy ##line:52176b
<<elseif $RhinoTut to false>>
    want know the secret to climbing? ##line:0a065a
    <<SetSpeaker Player>>
    sure ##line:9e8880
    <<SetSpeaker Original>>
    climb climb all the time ##line:22ebd2
    <<set $HeardClimbingSecret to true>>
    <<SetSpeaker Player>>
    neat ##line:5e38c0
<<elseif $Climbed1 is false and $Climbed2 is false>>
    you can try out the big wall ##line:ef2eec
    once you master the short wall ##line:b8855d
<<elseif $Climbed2 is false>>
    if you're having trouble\nwith the big wall, ##line:732b78
    just watch my technique ##line:a36443
    it's highly refined from\nYEARS of climbing ##line:882439
<<else>>
    i guess i'll see\nyou at Hawk Peak ##line:2fe8f3
    <<SetSpeaker Player>>
    i guess so! ##line:19fd90
    <<if $HeardClimbingSecret is true and $SquirrelJoke1 is false>>
        <<SetSpeaker Original>>
        i'm not gonna lie ##line:e14dc0
        it'll be a tough climb ##line:e4ccc6
        <<SetSpeaker Player>>
        well, you know what they say ##line:69f05d
        <<SetSpeaker Original>>
        what? ##line:67aad7
        <<SetSpeaker Player>>
        climb climb all the time ##line:f0fea7
        <<SetSpeaker Original>>
        heh ##line:8291ec
        i guess so ##line:0bf099
        <<set $SquirrelJoke1 to true>>
    <<endif>>
<<endif>>
===
title: ToSecretIslandStart
tags: 
colorID: 0
position: 7924,-3656
---
<sprite name="LeftArrow" tint=1> Back To Hawk Peak Island ##line:4a8b2b
===
title: RhinoClimbStart
tags: 
colorID: 0
position: 12330,-3113
---
<<if visited() is false>>
    oh hey! ##line:14872b
    uh ##line:2434ce
    it seems like you've really\ngotten the hang of climbing! ##line:6308ee
    <<SetSpeaker Player>>
    yeah! ##line:6fa087
    <<SetSpeaker TimScared>>
    <<LookAt TimScared Player>>
    <<LookAt TimScared Original>>
    HEY UHH ##line:731080
    UMM ##line:122814
    I THINK ##line:ec820f
    I'M STUCK ##line:e53dfc
    <<SetSpeaker Original>>
    tim! ##line:a36c64
    <<SetSpeaker TimScared>>
    IT'S TOO HIGH ##line:1e459c
    WHAT HAVE I DONE ##line:7d3730
    I CAN'T DO THIS ##line:b09cc0
    <<SetSpeaker Original>>
    <<ClearLookAt Player>>
    TIM! ##line:44af6e
    take a deep breath ##line:7963cc
    try and remain calm ##line:5c0d67
    you're doing great ##line:705993
    and you KNOW you can handle this ##line:27857c
    <<SetSpeaker TimScared>>
    CAN I? ##line:257951
    WHAT AM I DOING!? ##line:bc94be
    <<SetSpeaker Original>>
    just focus on the next step! ##line:e2fbae
    you've GOT this! ##line:0dadb4
    <<SetSpeaker TimScared>>
    I'VE GOT THIS? ##line:f29970
    I'VE GOT THIS ##line:be8e75
    <<ClearLookAt Original>>
    <<set $_DiscussedSquirrelStruggle to true>>
<<else>>
    uh ##line:91671a
    this might take a while ##line:1338a8
    we'll catch up with you later ##line:a687a6
    i'll make sure he's OK! ##line:89ad8e
<<endif>>
===
title: AuntFinishedBody
tags: 
colorID: 0
position: 10197,-5874
---
<<SetSpeaker Original>>
did you make it to the top? ##line:03e39c
<<SetSpeaker Player>>
yeah! ##line:184abe
it was really nice ##line:019ffa
i can see why you like it so much ##line:00bc9b
though ##line:e40dfc
it wasn't exactly a "short hike" ##line:30d49e
i'm really tired ##line:5d3b37
<<SetSpeaker Original>>
<<SetEmotion Happy>>
haha ##line:3e962c
well ##line:2de29d
maybe that was a bit\nof an understatement! ##line:a6fd6c
<<ClearEmotion>>
<<SetSpeaker Player>>
<<SetEmotion Happy>>
only a bit!! ##line:2ee7f9
<<ClearEmotion>>
<<SetSpeaker Original>>
[[AuntDynamic]]

===
title: AuntFinalDialogue
tags: 
colorID: 0
position: 10789,-5880
---
<<if visitCount() is 0>>
    <<SetSpeaker Player>>
    oh ##line:afdb51
    i almost forgot ##line:4c10ab
    mom says hi ##line:02acdc
    and... ##line:d34760
    everything was ok ##line:cfbf14
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    that's great to hear ##line:851ea7
    your mom is a tough bird ##line:0aed09
    i knew she'd be alright ##line:7f4c32
    <<ClearEmotion>>
<<else>>
    <<SetSpeaker Player>>
    i'm really glad i came ##line:d54e22
    it's been nice ##line:67e9f4
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    i'm glad you're here ##line:88bf5f
    you can visit again anytime! ##line:f996c9
    <<ClearEmotion>>
<<endif>>
===
title: Artist1Start
tags: 
colorID: 0
position: 8721,-3018
---
<<if visitCount() is 0>>
    <<SetSpeaker Player>>
    hey! ##line:40255a
    whatcha painting? ##line:cb8556
    <<SetSpeaker Original>>
    oh... ##line:c5fd2d
    you can't tell? ##line:28f4eb
    um ##line:3cdd76
    it's supposed to be the coastline ##line:21edbb
    though ##line:65a659
    i'm trying to go for a more... ##line:d1a1d3
    ...abstract look ##line:5c67df
    <<SetSpeaker Player>>
    ah ##line:cc88da
    i think i can see it now ##line:887a86
    <<SetSpeaker Original>>
    <<Face Player>>
    it's still a work-in-progress! ##line:e89e72
    but i don't know if i\nlike where it's going ##line:ccf33e
    i think i'm gonna head over to the\nblackwood forest lighthouse soon ##line:a1cc69
    i'm still... ##line:1d8901
    ...trying to find the\nright inspiration ##line:992933
    <<FaceDefault>>
    <<set $_Artist1Talk to true>>
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so ##line:7fc822
    the beach is that big\nyellow smudge there? ##line:e77436
    <<SetSpeaker Original>>
    i... ##line:3acd9f
    think so? ##line:2e2ac9
    it's more about the\nmood of the beach ##line:c95c26
    than the beach itself ##line:f89d83
    <<SetSpeaker Player>>
    ooh ##line:759dff
    ok ##line:708412
<<else>>
    i think i'm gonna put\na pin in this one ##line:e04ed1
    hopefully i'll have better\nluck at the lighthouse... ##line:3a6654
<<endif>>
===
title: Artist2Start
tags: 
colorID: 0
position: 8953,-3021
---
<<if visited() is false>>
    hey, it's you again! ##line:22da5c
    <<SetSpeaker Player>>
    yup! ##line:396fcd
    classic me again!! ##line:867a10
    <<SetSpeaker Original>>
    so ##line:93ec75
    what do you think\nabout this so far? ##line:d13984
    <<SetSpeaker Player>>
    ... ##line:21c39a
    woahh ##line:7b243c
    this is REALLY good! ##line:1eb3f4
    it's like... ##line:c17a9f
    better than real life! ##line:353744
    <<SetSpeaker Original>>
    haha well ##line:1e10ce
    i don't think i can\ncompete with real life ##line:07dfb2
    but i am happy with it so far ##line:205dc0
    ... ##line:1ca871
    i don't know if it will\nfit into my exhibit, though ##line:c83c44
    i'm trying to come up with\nsomething more interesting... ##line:d55b99
    i'm gonna follow one of the\nrivers up to its source ##line:d28019
    i have an idea for a painting\nfrom that perspective ##line:e35277
    <<set $_Artist2Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    what river are you gonna follow? ##line:c39625
    <<SetSpeaker Original>>
    probably the one on\nthe west side of the mountain ##line:1a0e69
    it's higher up ##line:065421
<<endif>>
===
title: Artist3Start
tags: 
colorID: 0
position: 9174,-3033
---
<<if visited() is false>>
    hmm ##line:2df2aa
    this painting isn't working ##line:6795a5
    <<SetSpeaker Player>>
    what? ##line:71c3d6
    i like it! ##line:ff8410
    <<SetSpeaker Original>>
    thanks ##line:edd9eb
    this park is special to me ##line:2d1c54
    but ##line:5a9496
    i don't think i'm going to\nimpress anyone with these ##line:303aa2
    <<SetSpeaker Player>>
    hey! ##line:4787d7
    I'M impressed! ##line:55e756
    <<SetSpeaker Original>>
    i mean, like ##line:55791a
    leave an impression on anyone ##line:48a619
    ... ##line:027722
    i have to show people that i'm... ##line:ff135f
    achieving something worthwhile ##line:2c368b
    like, ##line:4b27c2
    academically ##line:c87b69
    <<SetSpeaker Player>>
    hmm ##line:fb4f56
    <<SetSpeaker Original>>
    i heard ##line:66b6eb
    there's a graveyard in the\nshadow of the mountain ##line:6d7a36
    i'm gonna try that next ##line:ae3f49
    <<set $_Artist3Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    why the graveyard? ##line:e051ca
    <<SetSpeaker Original>>
    i'm not sure ##line:e851bc
    i just wanna try\nsomething different ##line:7b1864
<<endif>>
===
title: Artist4Start
tags: 
colorID: 0
position: 8725,-2788
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    are you... ##line:77e86f
    painting in the rain? ##line:c71675
    <<SetSpeaker Original>>
    yup ##line:1e252d
    <<SetSpeaker Player>>
    is that possible? ##line:65ee85
    <<SetSpeaker Original>>
    i'm trying! ##line:f4008d
    i don't have a lot of time ##line:261d69
    the exhibition is next week! ##line:924b88
    <<SetSpeaker Player>>
    oh ##line:aceded
    okay ##line:c64067
    <<SetSpeaker Original>>
    nothing has been\nworking so far... ##line:341232
    i finally got invited to\nshow my work in the city ##line:f4e7b1
    but everyone there is doing\namazing contemporary work ##line:d1da28
    i don't know if i can keep up ##line:14b63a
    <<SetSpeaker Player>>
    but ##line:946864
    your stuff is beautiful ##line:e35f98
    <<SetSpeaker Original>>
    you know who is curating the event? ##line:f4a746
    ian artsworth! ##line:208fa6
    famous contemporary artist,\nian artsworth!! ##line:ee7951
    he PERSONALLY invited\nme to participate ##line:79f2c8
    and ##line:df4fee
    and ##line:c897b3
    i'm gonna show up with\nmy boring landscapes ##line:bf3999
    and he's gonna take\none look at 'em ##line:1de69e
    and whisper ##line:1d9512
    "this fails to resonate with me" ##line:9c05b9
    and probably karate\nchop them in half!! ##line:8a33d6
    <<SetSpeaker Player>>
    what!? ##line:0a04b0
    he won't do that! ##line:e80321
    <<SetSpeaker Original>>
    he COULD ##line:5cdf3a
    he's ALSO a blackbelt ##line:ac9433
    so it's definitely possible ##line:5814d8
    *sigh* ##line:4da0e0
    but you're right ##line:7f43e9
    trying to paint here is useless ##line:f6ab71
    maybe i'd have better\nluck at outlook point ##line:879ba1
    <<set $_Artist4Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    why don't you take a break? ##line:26c376
    you've already done so much today ##line:0ef726
    <<SetSpeaker Original>>
    i can't ##line:f4a611
    i need to show people\nthat i'm worth something ##line:3c3a15
    i mean ##line:216a6e
    my art is worth something ##line:c39bce
<<endif>>
===
title: Artist5Start
tags: 
colorID: 0
position: 8948,-2799
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    WOAH! ##line:a88b2d
    this is really good!! ##line:8265b1
    i love it!!! ##line:ce64ef
    <<SetSpeaker Original>>
    oh hey ##line:0a2bad
    i like it too but ##line:5a5c63
    i can't use this... ##line:367187
    outlook point has been\npainted to death ##line:341304
    <<SetSpeaker Player>>
    has it? ##line:17efbd
    you've got such a cool style ##line:720fa5
    <<SetSpeaker Original>>
    thanks but ##line:4cea8f
    it's just a landscape painting ##line:bfc5f7
    this isn't bringing anything\nnew to the table ##line:665f02
    i want... ##line:0901ce
    i want people to FEEL something\nwhen they look at my work ##line:b280b0
    <<SetSpeaker Player>>
    but ##line:b8aed6
    i feel something! ##line:a92fd6
    when i look at it ##line:00901b
    it... ##line:c9e8cc
    makes me happy ##line:69ccb7
    if i had that on my wall ##line:1c18b9
    i'd look at\nit everyday ##line:aa4866
    and ##line:500eb3
    it would make\neveryday better ##line:19ac34
    <<SetSpeaker Original>>
    really? ##line:dbfd5a
    <<SetSpeaker Player>>
    yes! ##line:a746d1
    i live out in the city ##line:52963e
    i love the city but... ##line:1e0ac9
    it's got none of this ##line:0cccc3
    <<SetSpeaker Original>>
    hmm ##line:bc9c22
    <<SetSpeaker Player>>
    you were invited to\nthis show right? ##line:e5b01f
    they invited you cuz they\nknow your work is good! ##line:2be995
    i'm sure they wanna\nsee this too! ##line:b23f3c
    <<SetSpeaker Original>>
    i don't know ##line:58885a
    i need to think\nabout this ##line:1bb420
    i'm gonna find something\nnew to paint ##line:200c86
    maybe ##line:3cd3e7
    i should head back\nto where i started... ##line:4f81af
    <<set $_Artist5Talk to true>>
<<else>>
    <<SetSpeaker Player>>
    are you ok? ##line:136964
    <<SetSpeaker Original>>
    yeah ##line:41a956
    i'm good ##line:53c24e
    i'll see you around ##line:d5f1ab
<<endif>>
===
title: Artist6Start
tags: 
colorID: 0
position: 9171,-2786
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    is ranger jon gonna\nbe in this painting? ##line:d01363
    <<SetSpeaker Original>>
    yup! ##line:eacf94
    he agreed to be in this one ##line:5fa9a3
    cuz he's the best ##line:984ee0
    <<LookAt CampRangerNPC>>
    <<LookAt CampRangerNPC Player>>
    <<SetSpeaker CampRangerNPC>>
    aw ##line:e741de
    <<SetEmotion Happy>>
    you're gonna make me blush ##line:76421b
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    it's ok! ##line:442ac1
    i can paint the\nnot-blushing version ##line:ceeccb
    if you want ##line:7d939f
    <<ClearLookAt>>
    <<ClearLookAt Player>>
    <<SetSpeaker Player>>
    woah ##line:0e670b
    this is looking really good! ##line:2abd45
    <<SetSpeaker Original>>
    i think i'm just gonna paint\nthe things i want to paint ##line:d26d5d
    instead of ##line:0551b4
    painting the things i think\nother people want me to paint ##line:195367
    at the very least ##line:587c79
    it's making things easier ##line:68b97d
    <<LookAt CampRangerNPC>>
    <<SetSpeaker CampRangerNPC>>
    <<ClearEmotion>>
    and ##line:f26794
    you want to paint me!? ##line:62e4dc
    <<SetSpeaker Original>>
    uhh! well! ##line:07c624
    <<SetEmotion Happy>>
    you're a part of the park too ##line:1866be
    i just want to capture\nall aspects of the park ##line:274641
    <<ClearEmotion>>
    <<ClearLookAt>>
    <<SetSpeaker Player>>
    that sounds cool! ##line:5b539b
    <<SetSpeaker Original>>
    oh also ##line:8c5f85
    on my way down from the\nmountain, i found this ##line:2100fa
    <<Hide>>
    <<GiveItem GoldenFeather 1>>
    jon told me you\nwere collecting them ##line:0d86ba
    so i thought you\nshould have it ##line:0dd6a8
    <<set $_Artist6Talk to true>>
<<elseif visitCount() is 1>>
    i'm still not so sure\nwhat i'll put in the exhibit ##line:ed4dad
    but ##line:927429
    i don't feel as\nworried about it ##line:7bdaee
    <<SetSpeaker Player>>
    i think you should\nput it all in! ##line:59ae27
    <<SetSpeaker Original>>
    yeah? ##line:ed26b6
    even the watered-down\nrainy painting? ##line:883f76
    <<SetSpeaker Player>>
    yeah! ##line:160a69
    it's all part of the experience! ##line:46270d
    plus it looked pretty cool ##line:da3502
    <<SetSpeaker Original>>
    hmm ##line:653f1e
    you know ##line:ba0290
    that's not a bad idea! ##line:43ae79
<<else>>
    i wanna spend some\ntime on this one ##line:c004f2
    really capture the\nmoment, you know? ##line:dc5423
    i'll talk to you later! ##line:d9e008
<<endif>>
===
title: GoatExtra
tags: 
colorID: 0
position: 11547,-4830
---
<<if visited() is false>>
    oh also what is the\ntime by the way ##line:3ddff8
    <<SetSpeaker Player>>
    it's {{Time}} ##line:3f0883
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so ##line:d74914
    do you think someone else\nmight have found it already? ##line:012c50
    <<SetSpeaker Original>>
    well, no ##line:dfd1df
    my phone number is\non the back ##line:5336a5
    so if someone found\nit, they'd call me! ##line:0260b8
    unless... ##line:c7900d
    they really needed\nmoney or something!?! ##line:d41a6a
    ...AND SOLD IT ON THE INTERNET!? ##line:5d1a1a
    <<SetSpeaker Player>>
    oh uh ##line:b08487
    don't worry! ##line:39e331
    i'm sure it's still out there ##line:3b5b8f
    <<SetSpeaker Original>>
    i just don't get the internet ##line:aeab6a
    don't trust it ##line:74bfc8
    never will ##line:5e700f
<<else>>
    please find my watch... ##line:3ea8a9
    BEFORE INTERNET THIEVES\nFIND IT FIRST!! ##line:8ac825
<<endif>>
===
title: RaceRabbitExtra
tags: 
colorID: 0
position: 12628,-4593
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    do you think someone\nmight have picked it up? ##line:d96b36
    <<SetSpeaker Original>>
    i don't think so..? ##line:88f46d
    unless ##line:b1b697
    someone thought it... ##line:5311dc
    ...it was trash... ##line:23e578
<<else>>
    any luck finding\nmy headband yet? ##line:214054
    <<SetSpeaker Player>>
    sorry ##line:a27dcf
    no luck yet ##line:bc62f4
    <<SetSpeaker Original>>
    ok... thanks ##line:bf2aa7
<<endif>>
===
title: RelaxedFrogStart
tags: 
colorID: 0
position: 11167,-3662
---
<<if visited() is false>>
    i'm so stoked\nto eat breakfast! ##line:3f1e65
    <<SetSpeaker Player>>
    but ##line:f33d5a
    it's already the afternoon? ##line:aeb6ed
    <<SetSpeaker Original>>
    tomorrow's breakfast! ##line:aeb91a
    it's the best meal\nof the day. ##line:a2dfe9
    sometimes when i'm\nabout to go to bed... ##line:2b7200
    ...i get so excited by the thought\nof it that i have trouble sleeping! ##line:3e86b6
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>>
    so what makes breakfast so great? ##line:e96648
    <<SetSpeaker Original>>
    dang ##line:1c4b18
    you ever had a pancake before? ##line:b51670
    <<SetSpeaker Player>>
    yeah ##line:1fd4aa
    <<SetSpeaker Original>>
    bacon? waffles? ##line:2db4db
    eggs? french toast? ##line:baa002
    A FRUIT BOWL!?! ##line:eaecf5
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    yes to all! ##line:cae41c
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    so you know what i'm talking about! ##line:a7c084
<<else>>
    don't mind me ##line:fc940c
    just daydreaming... ##line:3ca22e
    ...about cinnamon waffles ##line:4e5c44
<<endif>>
===
title: ToughBirdWhatsUp
tags: 
colorID: 0
position: 10694,-4766
---
<<HideCollection Coin>>
<<SetSpeaker Original>>
<<if visited() is false>>
    still trying to get my act\ntogether for next semester ##line:daaed9
    <<SetSpeaker Player>>
    you still need to find 400 coins? ##line:977ba3
    <<SetSpeaker Original>>
    yeah... ##line:657539
<<else>>
    the balance on my student loan ##line:06cf0f
    <<SetSpeaker Player>>
    oh ##line:7b59e6
    <<SetSpeaker Original>>
    but yeah ##line:b8e654
    other than that ##line:e699bc
    pretty much nothing ##line:535fb6
    <<SetSpeaker Player>>    
<<endif>>


===
title: VolleyKidStart
tags: 
colorID: 0
position: 7418,-3103
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    hey! ##line:282971
    looks like you got\nthe net set up! ##line:eace1c
    are you two gonna\nplay volleyball? ##line:1b230a
    <<SetSpeaker Original>>
    volleyball? ##line:b608c0
    nah ##line:5eae80
    we invented our OWN game ##line:236eea
    i think it's gonna be\nthe next big thing! ##line:940bfd
    <<SetSpeaker Player>>
    alright! ##line:c34252
    <<SetSpeaker Original>>
    <<SetPose RaiseArms>>
    CLAIRE! ##line:d452b2
    <<ClearPose>>
    <<SetSpeaker Player>>
    ?? ##line:25637c
    <<SetSpeaker Original>>
    i FORMALLY challenge YOU\nto a game of... ##line:ae6100
    <<SetPose RaiseArms>>
    BEACHSTICKBALL!!! ##line:0da49e
    <<ClearPose Original>>
    <<SetSpeaker RefereeKid>>
    <<LookAt Player>>
    <<LookAt RefereeKid Player>>
    ooooOOOOoooo ##line:0e7130
    <<SetSpeaker Original>>
    <<ClearLookAt Player>>
    that means you HAVE to play ##line:fcd4d9
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    yeah!! ##line:c54521
    you HAVE to!! ##line:5874b0
    it's the LAW!! ##line:a9599e
    <<ClearLookAt Player>>
    <<SetSpeaker Original>>
    you can't fight the law! ##line:1ed3fe
    <<SetSpeaker Player>>
    ok ok ##line:5d1349
    i'll play ##line:1ecb33
    <<SetSpeaker RefereeKid>>
    <<SetEmotion Happy>>
    <<SetEmotion Happy Original>>
    <<SetPose RaiseArms Original>>
    yaay!! ##line:9e0777
    <<ClearEmotion>>
    <<ClearEmotion Original>>
    <<SetSpeaker Player>>
    so... ##line:e6d27a
    how do you play? ##line:ab5157
    <<SetSpeaker Original>>
    <<ClearPose Original>>
    step over to the\nother side of court ##line:27516b
    and i'll teach you\neverything i know ##line:e49e25
    <<ClearLookAt RefereeKid>>
    <<set $_VolleyGameActive to true>>
<<elseif $_BallHitsBest is 0>>
    you gotta go to the\nother side to play! ##line:54218a
<<else>>
    [[VolleyKidChat]]
<<endif>>
===
title: RefereeKidStart
tags: 
colorID: 0
position: 7165,-2621
---
<<if visited() is false>>
    <<SetSpeaker Player>>
    hey julie! ##line:e1b41b
    <<SetSpeaker Original>>
    hi!! ##line:27af03
    <<SetSpeaker Player>>
    how's the day going? ##line:d9a92b
    <<SetSpeaker Original>>
    we're gonna play games!! ##line:ec6e4b
    ALL DAY!! ##line:335d06
    <<SetSpeaker Player>>
    sounds fun! ##line:f5d2c6
<<elseif visitCount() == 1>>
    <<SetSpeaker Player>>
    how do you like the big chair? ##line:bc462d
    <<SetSpeaker Original>>
    it's great!! ##line:d8124a
<<elseif visitCount() == 2>>
    <<SetSpeaker Player>>
    hmm ##line:165d45
    where'd you get that\nchair from anyway? ##line:fe982c
    <<SetSpeaker Original>>
    we stole it!! ##line:01280d
    <<SetSpeaker Player>>
    oh ##line:ff0db0
<<elseif visitCount() == 3>>
    <<SetSpeaker Player>>
    are you gonna return\nit when you're done? ##line:c12629
    <<SetSpeaker Original>>
    nope!! ##line:0a45f1
<<else>>
    <<SetSpeaker Player>>
    you two should probably\nreturn that chair ##line:269405
    <<SetSpeaker Original>>
    hmmm!! ##line:27aaa2
    maybe!! ##line:f6916a
<<endif>>
===
title: VolleyballGameStart
tags: 
colorID: 0
position: 7412,-2848
---
<<Face Player Original>>
<<Face Original Player>>
<<if visited() is false>>
    <<LookAt Player RefereeKid>>
    <<LookAt Original Player>>
    so ##line:825d07
    beachstickball has only\nthree simple rules ##line:4e330b
    RULE NUMBER ONE! ##line:2efd90
    hit the ball with your stick ##line:3feb13
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    yeah hit it real hard!! ##line:ddd9ff
    <<SetSpeaker Original>>
    <<LookAt Original Player>>
    RULE NUMBER TWO! ##line:d23681
    don't let it hit the ground ##line:0d0089
    i mean, for the most part... ##line:5d7570
    julie is the ref\nso it's all up to her! ##line:5c85e8
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    it's all up to me!! ##line:5bd30a
    <<SetSpeaker Original>>
    <<LookAt Original Player>>
    RULE NUMBER THREE! ##line:c7ce89
    believe in yourself ##line:b5583e
    that's the most important rule ##line:4156f6
    <<SetSpeaker RefereeKid>>
    <<LookAt RefereeKid Player>>
    don't EVER forget it!! ##line:3f7cc4
    <<SetSpeaker Original>>
    <<LookAt Original Player>>
    that's pretty much it ##line:f3d19e
    are you ready? ##line:d16a9a
    <<SetSpeaker Player>>
    i guess so? ##line:0a8cf6
    <<SetSpeaker Original>>
    <<if IsEquipped("Stick") is false>>
        the answer is NO! ##line:41bacf
        you need to equip your stick first! ##line:6c5cfa
        <<set $_AllowGameStart to false>>
    <<elseif $_BoatOnCourt is true>> 
        okay i forgot one more rule ##line:3e3c75
        <<SetSpeaker Player>> 
        ok? ##line:e6c2eb
        <<SetSpeaker Original>>
        NO BOATS ON COURT! ##line:706732
        get that thing outta here! ##line:2344d5
        <<set $_AllowGameStart to false>>
    <<else>>
        OK READY SET GO!!! ##line:e99927
        <<set $_AllowGameStart to true>>
    <<endif>>
    <<ClearLookAt Player>>
    <<ClearLookAt RefereeKid>>
<<else>>
    [[VolleyballGameMain]]
<<endif>>
===
title: VolleyballGameMain
tags: 
colorID: 0
position: 7414,-2608
---
<<if $_AllowGameStart is false and IsEquipped("Stick") is false>>
    you need to equip the STICK!! ##line:e7e466
<<elseif $_BoatOnCourt is true>> 
    alright i forgot one more rule ##line:129978
    <<SetSpeaker Player>> 
    ok? ##line:11199c
    <<SetSpeaker Original>>
    NO BOATS ON COURT! ##line:c56917
    get that thing outta here! ##line:87ce07
    <<set $_AllowGameStart to false>>
<<else>>
    <<if IsEquipped("Pickaxe") and $PoppedBall is true>>
        ... ##line:f20676
        i'm not gonna play if you're\ngonna use the pickaxe again... ##line:b88e3a
        <<set $_AllowGameStart to false>>
    <<else>>
        <<set $PlayedBeachStickBall to true>>
        <<if $VolleyStartTalk is 0>>
            OK READY SET GO!!! ##line:515e71
        <<elseif $VolleyStartTalk is 1>>
            OK HERE WE GO!!! ##line:365ee6
        <<elseif $VolleyStartTalk is 2>>
            OK LET'S DO THIS!!! ##line:e67793
        <<elseif $VolleyStartTalk is 3>>
            OK OK OK GOOO!!! ##line:c6acf5
        <<else>>
            TIME TO DUEL!!! ##line:574ca3
        <<endif>>

        <<set $VolleyStartTalk to $VolleyStartTalk + 1>>
        <<if $VolleyStartTalk >= 5>>
            <<set $VolleyStartTalk to 0>>
        <<endif>>
        <<set $_AllowGameStart to true>>
    <<endif>>
<<endif>>
===
title: VolleyballGameEnd
tags: 
colorID: 0
position: 7849,-3040
---
<<set $_BallHits to $_BallHits * 2>>
<<if $FirstBallCounts is true>>
    <<set $_BallHits to $_BallHits + 1>>
<<endif>>


<<if $_BallHits >= $_BallHitsBest>>
    <<set $_BallHitsBest to $_BallHits>>
    <<set $NewRecord to true>>
<<else>>
    <<set $NewRecord to false>>
<<endif>>


<<Wait 1>>
<<Face Player Original>>
<<Face Original Player>>
<<if visited() is false>>
    [[FirstVolleyLost]]
<<else>>
    [[RegularVolleyLost]]
<<endif>>
===
title: FirstVolleyLost
tags: 
colorID: 0
position: 7667,-2780
---
<<SetSpeaker Player>>
oops ##line:aa4bed
did i lose? ##line:5dadd5
<<SetSpeaker RefereeKid>>
<<LookAt RefereeKid Player>>
NOPE!! ##line:52cd58
you can never lose!! ##line:f8372e
<<ClearLookAt Player>>
<<SetSpeaker Original>>
<<SetEmotion Happy>>
when you play beachstickball... ##line:765cd5
<<SetPose RaiseArms>>
EVERYONE wins! ##line:f0410f
<<ClearEmotion>>
... ##line:28f450
<<ClearPose>>
except if you pop the ball ##line:d91e60
then i guess you lose ##line:b5a094
otherwise ##line:143677
<<SetSpeaker Original>>
beachstickball is a\npurely coop game! ##line:03da1b
<<SetSpeaker Player>>
you mean co-op game? ##line:557881
<<SetSpeaker Original>>
yeah, a coop game! ##line:cca8a2
it means we work together\nto get a high score! ##line:8ef8db
<<SetSpeaker Player>>
that sounds nice ##line:d6d71e
<<SetSpeaker Original>>
we get a point for\nevery pass we make ##line:99ffbb
if we can get to 10 points\ni'll give you a prize! ##line:f477a3
<<if $_BallHits >= 10>>
    <<SetSpeaker RefereeKid>>
    you already got 10 points!! ##line:22ba0c
    your score is {{BallHits}} ##line:d5064e
    <<SetSpeaker Original>>
    <<if $_BallHits < 20>>
        oh! ##line:bf1b68
        well ##line:0d566e
        i guess i owe you a prize! ##line:2da316
        [[GiveVolleyPrize]]
    <<else>>
        <<if $_BallHits >= 30>>
            we ALREADY hit 30 POINTS?!?! ##line:b69933
        <<else>>
            we ALREADY hit 20 points?! ##line:c88b21
        <<endif>>
        that's more than\nwe've ever done before!! ##line:ba370a
        claire... ##line:3e117b
        you're a PRODIGY! ##line:0ead04
        take this golden feather ##line:905691
        <<Hide>>
        <<GiveItem GoldenFeather 1>>
        <<set $GotVolleyPrize to true>>
        and also ##line:95a4d1
        [[GiveVolleyPrize2]]
    <<endif>>
<<endif>>
===
title: GiveVolleyPrize
tags: 
colorID: 0
position: 7662,-2504
---
<<set $GotVolleyPrize to true>>
take this ##line:dd9f1f
<<Hide>>
<<GiveItem GoldenFeather 1>>
you're really good! ##line:098c2d
<<if $_BallHits >= 20>> 
    hmm ##line:f7626c
    i also wanted to give you a\nsecond prize for making it to 20! ##line:0b898c
    ... ##line:b5b3e5
    [[GiveVolleyPrize2]] 
<<else>> 
    with your skill, we might even\nbe able to make it to 20! ##line:b32983
    but that would still\nbe pretty hard... ##line:fed812
<<endif>>
===
title: PoppedBallEnd
tags: 
colorID: 0
position: 8093,-3039
---
<<if visited() is false>>
    <<Wait 1>>
    NOOOOOOOOOOOOOOOO ##line:0905b1
    ...how? ##line:cf975b
    ...why?? ##line:3efb69
    <<SetSpeaker Player>>
    uh! ##line:5115e6
    sorry ##line:168874
    maybe that was a bad idea ##line:1ab33e
    <<SetSpeaker Original>>
    just don't do it again, ok? ##line:1bca05
    <<set $PoppedBall to true>>
<<elseif visitCount() is 1>>
    <<Wait 1>>
    HEY! ##line:f6567e
    i said not to do\nthat anymore! ##line:3de0b4
    <<SetSpeaker RefereeKid>>
    do it again!! ##line:16e478
    <<SetSpeaker Original>>
    <<LookAt RefereeKid>>
    julie! ##line:1cf085
    <<SetSpeaker RefereeKid>>
    i like it when it pops!! ##line:58eb8b
    <<SetSpeaker Original>>
    <<ClearLookAt>>
    but i'm trying to play the game! ##line:1b2f57
<<else>>
    <<SetSpeaker RefereeKid>>
    HAHAHA!! ##line:1e8e7e
    so fun!! ##line:5ce6cd
    <<SetSpeaker Original>>
    ugh ##line:13a4d9
<<endif>>
===
title: GiveVolleyPrize2
tags: 
colorID: 0
position: 7944,-2508
---
<<set $GotVolleyPrize2 to true>>
i'm gonna give ya ALL the\nmoney in my pocket! ##line:117144
<<Hide>>
<<PlaySound BuySound>>
<<GiveItem Coin 50>>
<<Wait 1>>
<<SetSpeaker Player>>
<<Face Original Player>>
woah, that's a lot ##line:07918d
i don't wanna take\nall your money ##line:49f634
<<SetSpeaker Original>>
it's ok! ##line:7ca8d6
my family's rich ##line:a714a1
<<SetSpeaker Player>>
oh ##line:8a4237
well ##line:3e0023
ok i guess ##line:384b5a
<<if $_BallHits >= 30>>
    <<SetSpeaker Original>>
    honestly ##line:e1940b
    i never even thought\n30 was possible... ##line:989dbe
    i wanna give you\none more thing ##line:43798e
    [[GiveVolleyPrize3_2]]
<<endif>>
===
title: RaceOpponentStart
tags: 
colorID: 0
position: 7351,-5975
---
<<set $_BeginRace to false>>
<<set $_ResetRace to false>>
<<set $JustMetAvery to false>>
    
<<if $_FinishedRace is false>>
    [[BeforeRaceIntro]]
<<else>>
    [[AfterRaceIntro]]
<<endif>>
===
title: RaceSelectMenu
tags: 
colorID: 0
position: 8507,-6161
---
<<SetSpeaker Original>>
totally! ##line:d80175
what race you wanna do? ##line:0aaa4a
<<SetSpeaker Player>>
<<if $LighthouseRaceFound is true>>
    [[the lighthouse race|LighthouseRaceRetry]] ##line:0c77af
<<endif>>
<<if $BuildingRaceFound is true>>
    [[the abandoned building race|BuildingRaceRetry]] ##line:49eef5
<<endif>>
<<if $MountainTopRaceFound is true>>
    [[the mountaintop race|MountainTopRaceRetry]] ##line:16f4bc
<<endif>>
[[nevermind|NothingRace]] ##line:dbcf58
===
title: BeforeRaceMenu
tags: 
colorID: 0
position: 8162,-6508
---
<<SetSpeaker Player>>
[[let's race!|BeginRace]] ##line:d7ff0b
[[not yet|RefuseRace]] ##line:90fa0a
<<if $RacesFound > 1>>
    [[let's do a rematch!|RaceSelectMenu]] ##line:24e047
<<endif>>
===
title: DiscussRaceResult
tags: 
colorID: 0
position: 7995,-5115
---
<<if $_WonRace is true>>
    <<if $_RaceVictories < $_HardcodedReplays>>
        [[WinRegular]]
    <<elseif $_RaceVictories == $_HardcodedReplays>>
        [[WinMedal]] 
    <<else>>
        [[WinGhost]]
    <<endif>>
<<else>>
    <<if $_UsedVehicle>>
        [[UsedVehicleInRace]] 
    <<elseif $_RaceVictories < $_HardcodedReplays>>
        [[LoseRegular]]
    <<else>>
        [[LoseGhost]]
    <<endif>>
<<endif>>
===
title: LighthouseRaceRetry
tags: 
colorID: 0
position: 8792,-6167
---
<<SetSpeaker Original>>
yes! ##line:e3bea3
let's head over there ##line:83f068
<<set $_RaceId to "LighthouseRace">>
<<set $_ResetRace to true>>
===
title: MountainTopRaceRetry
tags: 
colorID: 0
position: 8794,-5950
---
<<SetSpeaker Original>>
okey dokey!  ##line:a892f3
let's go to the\nstarting line! ##line:f2a32f
<<set $_RaceId to "MountainTopRace">>
<<set $_ResetRace to true>>
===
title: BeforeRaceIntro
tags: 
colorID: 0
position: 7609,-6518
---
<<if $AgreedToAvery is false>>
    [[AveryIntroduction]]
<<endif>>

<<if $_RaceId is "LighthouseRace">>
    [[LighthouseRaceIntro]]
<<elseif $_RaceId is "MountainTopRace">>
    [[MountainTopRaceIntro]]
<<elseif $_RaceId is "OldBuildingRace">>
    [[BuildingRaceIntro]]
<<else>>
    [[BeforeRaceMenu]]
<<endif>>
===
title: AfterRaceIntro
tags: 
colorID: 0
position: 7333,-5455
---
<<if $_RaceResultDiscussed is false>>
    <<set $_RaceResultDiscussed to true>>
    [[DiscussRaceResult]]
<<else>>
    <<if $_IsSwimming is true>>
        helloooo ##line:11d911
        just treading water here ##line:c042b5
        <<SetEmotion Happy>>
        it feels nice to take a dip! ##line:8aaa6d
        <<ClearEmotion>>
    <<else>>
        hey! ##line:a35292
        what's up? ##line:2120f5
    <<endif>>
    [[AfterRaceMenu]]
<<endif>>
===
title: MountainTopRaceIntro
tags: 
colorID: 0
position: 7893,-6391
---
<<set $MountainTopRaceFound to true>>

<<if visited() is false>>
    <<set $RacesFound to $RacesFound + 1>>
    <<if $JustMetAvery is false>>
        hello again! ##line:b5c178
        UH ##line:75273a
        i've got a bit of a crazy idea! ##line:771653
        but it's like... ##line:80b852
        a good crazy idea ##line:f5e753
        will ya hear me out? ##line:e5f127
        <<SetSpeaker Player>>
        sure! ##line:03f7fe
        <<SetSpeaker Original>>
        <<set $_CrazyIdea to true>> 
    <<endif>>
    ok so ##line:6bbdb1
    what if... ##line:3680ac
    we raced... ##line:57c371
    ALL THE WAY TO THE SUMMIT!? ##line:7ec37a
    <<SetSpeaker Player>>
    woah ##line:919738
    <<if $_CrazyIdea is true>> 
        that is a crazy idea ##line:06b00f
        <<SetSpeaker Original>>
        but a GOOD one! ##line:6f81db
        the rules are simple ##line:7deab5
    <<endif>> 
    <<SetSpeaker Original>>
    first one to the top WINS! ##line:d739b0
    i only have <color=red>9 feathers,</color> so ##line:43c890
    don't use more than that, k? ##line:ebac34
    <<if HasItem("WalkieTalkie", 1) is false>>
        OH ##line:316905
        and one more thing ##line:ccf582
        take this! ##line:05fbc8
        <<Hide>>
        <<GiveItem WalkieTalkie 1>>
        <<Face Original Player>>
        it's a long race so ##line:7d1c33
        you can call me if you want to restart ##line:43304b
        all good? ##line:8d9473
    <<endif>>
<<elseif $_LighthouseRace_Attempted is false>>
    so you wanna race to THE TOP?! ##line:380685
<<else>>
    hello again! ##line:2fe636
    you wanna rematch to the top? ##line:8d80dc
<<endif>>

[[BeforeRaceMenu]]
===
title: LighthouseRaceIntro
tags: 
colorID: 0
position: 7893,-6640
---
<<set $LighthouseRaceFound to true>>

<<if visited() is false>>
    <<set $RacesFound to $RacesFound + 1>>
    <<if $JustMetAvery is false>>
        hello again! ##line:5efe99
        i've got another route\ni want to race! ##line:575305
        <<SetSpeaker Player>>
        sure! ##line:6acfc5
        i'm interested ##line:e6d1ec
        <<SetSpeaker Original>>
    <<endif>>
    ok so ##line:ff97ca
    here's the rules ##line:9aad90
    first one to touch the\nlighthouse door WINS! ##line:172a64
    ALSO ##line:2bd292
    let's only use <color=red>3 golden\nfeathers</color> during the race! ##line:237683
    ...cuz that's all i\nhave on me right now ##line:cc633a
    sound good? ##line:574922
<<elseif $_LighthouseRace_Attempted is false>>
    so how about that lighthouse race? ##line:14c2f7
<<else>>
    heyo! ##line:268687
    you wanna rematch to the lighthouse? ##line:45f6f6
<<endif>>

[[BeforeRaceMenu]]
===
title: BeginRace
tags: 
colorID: 0
position: 8430,-6641
---
<<SetSpeaker Original>>

<<if HasItem("RunningShoes", 1) is false>>
    oh actually ##line:ab370b
    you might want to find\na pair of <color=red>running shoes</color> ##line:a54eab
    they're pretty crucial to\ngood running technique! ##line:803b99
    <<SetSpeaker Player>>
    -> i'll race anyway! ##line:123690
        <<SetSpeaker Original>>
        haha ok ##line:dd0e94
        lets do this! ##line:146e8c
    -> wait, nevermind ##line:93490f
        <<SetSpeaker Original>>
        [[CancelRaceShoes]]
<<elseif $_RaceId is "LighthouseRace" and HasItem("GoldenFeather", 3) is false>>
    [[ConsiderFeathersRace]]
<<elseif $_RaceId is "OldBuildingRace" and HasItem("GoldenFeather", 4) is false>>
    [[ConsiderFeathersRace]]
<<elseif $_RaceId is "MountainTopRace" and HasItem("GoldenFeather", 7) is false>>
    [[ConsiderFeathersRace]]
<<else>>
    AWESOME ##line:2aa83d
    let's do this! ##line:1b697e
<<endif>>

<<set $_BeginRace to true>>
===
title: RefuseRace
tags: 
colorID: 0
position: 8156,-7142
---
<<SetSpeaker Original>>
ok ##line:ce1080
see you later then? ##line:d49680
===
title: AfterRaceMenu
tags: 
colorID: 0
position: 8198,-5716
---
<<SetSpeaker Player>>
[[bye!|NothingRace]] ##line:594168
[[want to do a rematch?|RestartRace]] ##line:d65d10
<<if $AskedAboutAvery is false and $KnowsAboutRace is true>>
    [[why not join the marathon?|AveryStory]] ##line:1b252c
<<endif>>
<<if $RacesFound > 1>>
    [[let's do a different race!|RaceSelectMenu]] ##line:38a545
<<endif>>
===
title: RestartRace
tags: 
colorID: 0
position: 8496,-5811
---
<<SetSpeaker Original>>
<<if $_WonRace is false>> 
    i'm down!! ##line:31f6ee
    let's try it again! ##line:e981e0
<<else>> 
    totally!! ##line:48b1cc
    i'll do better this time! ##line:c6011c
<<endif>> 
<<set $_ResetRace to true>>
===
title: NothingRace
tags: 
colorID: 0
position: 8193,-5994
---
<<SetSpeaker Original>>
see ya! ##line:58704a
===
title: RegularVolleyLost
tags: 
colorID: 0
position: 7930,-2782
---
<<if $_BallHits >= 4>>
    <<Wait 0.5>>
    <<SetSpeaker RefereeKid>>
    <<if $FirstCountResult is false>>
        i counted {{BallHits}} hits!! ##line:a4f965
    <<else>>
        i counted {{BallHits}} hits!! ##line:146aab
        <<set $FirstCountResult to true>>
    <<endif>>
    <<SetSpeaker Original>>

    <<if $GotVolleyPrize is false and $_BallHits >= 10>>
        <<SetEmotion Happy>>
        YES! ##line:cbb0e6
        we did it! ##line:70f185
        as promised, i've got\na prize for you! ##line:7f2baf
        <<ClearEmotion>>
        [[GiveVolleyPrize]]
    <<elseif $GotVolleyPrize2 is false and $_BallHits >= 20>>
        <<SetEmotion Happy>>
        <<if $_BallHits >= 30>>
            HOLY MOLY WE HIT 30!! ##line:f6e9a8
        <<else>>
            WE HIT 20!! ##line:ef85aa
        <<endif>>
        THAT'S MY NEW RECORD!! ##line:43af48
        <<ClearEmotion>>
        uh uh ##line:5a7432
        i gotta come up with a reward!! ##line:0ad676
        [[GiveVolleyPrize2]]
    <<elseif $GotVolleyPrize3 is false and $_BallHits >= 30>>
        [[GiveVolleyPrize3]]
    <<else>>
        [[NoVolleyPrize]]
    <<endif>>
<<endif>>
===
title: NoVolleyPrize
tags: 
colorID: 0
position: 8420,-2508
---
<<if visited() is false>>
    <<set $_BallHitsMore to $_BallHits + 1>>
    but i counted {{BallHitsMore}}! ##line:ec7f6d
    <<SetSpeaker RefereeKid>>
    the serve doesn't count!! ##line:d6e93d
    <<SetSpeaker Original>>
    what? ##line:0ae27d
    it totally counts! ##line:86fe8b
    what do you think, claire? ##line:5ac4bd
    <<SetSpeaker Player>>
    -> it counts! ##line:d7cd87
        <<SetSpeaker Original>>
        yeah see! ##line:7b5de3
        <<SetSpeaker RefereeKid>>
        fine!! ##line:a4bd76
        <<set $FirstBallCounts to true>>
        <<set $_BallHits to $_BallHits + 1>>
        you got {{BallHits}} points then ##line:52d7a2
    -> it doesn't really count... ##line:c2f4b1
        <<SetSpeaker Original>>
        aw come on ##line:79124f
        <<SetSpeaker Player>>
        sorry.. ##line:34565b
        i'm with julie on this one ##line:3be986
        <<SetSpeaker RefereeKid>>
        yeah!! ##line:9cbf45
        <<SetSpeaker Original>>
        fine fine ##line:123b6b
<<else>>
    <<if $_BallHits >= 20>>
        <<if $NewRecord is true>>
            that's a new record!! ##line:a65662
        <<else>>
            <<SetEmotion Happy>>
            we make a pretty great team! ##line:c45080
            <<ClearEmotion>>
        <<endif>>
    <<elseif $_BallHits >= 10>>
        <<if $BallBrag is false>>
            you're doing great! ##line:fc0537
            <<SetSpeaker Player>>
            well ##line:f32d4c
            you're a lot better ##line:f17f95
            you never drop the ball! ##line:e2e15a
            <<SetSpeaker Original>>
            trueee ##line:0e9756
            <<set $BallBrag to true>>
        <<else>>
            solid! ##line:4ec1da
        <<endif>>
    <<elseif $_BallHits >= 8>>
        <<if $GotVolleyPrize is false>>
            oh, almost there! ##line:ce163d
        <<else>>
            oh, almost 10 again! ##line:a0fd91
        <<endif>>
    <<elseif $_BallHits >= 6>>
        not bad! ##line:96cc5f
    <<elseif $_BallHits >= 4>>
        that's a good start! ##line:d99dc5
    <<endif>>
<<endif>>
===
title: GiveVolleyPrize3
tags: 
colorID: 0
position: 8187,-2506
---
wait what? ##line:0f4a14
<<SetSpeaker RefereeKid>>
i said you got {{BallHits}} points! ##line:930ae0
<<SetSpeaker Original>>
WE GOT PAST 30 POINTS!? ##line:9c5f67
i didn't even know\nthat was possible! ##line:ed88e9
hmm ##line:d0bf44
that deserves the most specialest prize ##line:d0340b
but i already gave you all my money... ##line:109308
<<SetSpeaker Player>>
it's ok! ##line:e19113
i don't need anything ##line:fdad43
<<SetSpeaker Original>>
yes you do!! ##line:4e5eeb
maybe you should have this ##line:4f5e84
[[GiveVolleyPrize3_2]]
===
title: CampCatStart
tags: 
colorID: 0
position: 10225,-3275
---
<<if $ReturnedPermit>> 
    <<if $CampCatSorry is false>> 
        [[CampCatFreakout]] 
    <<else>> 
        <<SetEmotion Happy>> 
        it feels great to\nbe 100% legal again! ##line:c0606f
        <<ClearEmotion>> 
    <<endif>> 
<<elseif HasItem("CampingPermit", 1)>> 
    <<SetSpeaker Player>>
    you won't believe what i found! ##line:60d7be
    <<Hide>> 
    <<GiveItem CampingPermit -1>> 
    <<Wait 1>> 
    <<SetSpeaker Original>>
    <<SetPose RaiseArms>> 
    YOU FOUND MY PERMIT!? ##line:6f023c
    I THOUGHT IT WAS\nGONE FOREVER! ##line:6e23fc
    <<ClearPose>>
    <<SetSpeaker Player>>
    you weren't kidding! ##line:a3f456
    a fish really did have it ##line:73297a
    <<SetSpeaker Original>> 
    <<SetEmotion Happy>> 
    thank you so much! ##line:df5dfa
    <<ClearEmotion>> 
    you should have the\nrest of my bait ##line:70a309
    i'm feeling a little\ndone with fishing ##line:d88125
    <<Hide>> 
    <<PlaySound PickUp>>
    <<GiveItem Bait 5>>
    <<Wait 1>> 
    thanks again! ##line:2ef1a1
    you're a LIFESAVER! ##line:5eb61a
    <<set $ReturnedPermit to true>>
<<elseif visited() is false>>
    <<SetSpeaker Player>>
    hey ##line:d8d54d
    <<SetSpeaker Original>>
    <<SetPose RaiseArms>> 
    AAHHH! ##line:329927
    <<Face Player>> 
    YES THIS SETUP\nIS 100% LEGAL!!! ##line:f38073
    <<SetSpeaker Player>>
    uh- ##line:5081fb
    <<SetSpeaker Original>>
    YES I HAVE A VALID\nCAMPING PERMIT!!! ##line:68f49c
    <<SetSpeaker Player>>
    wait- ##line:27f51b
    <<SetSpeaker Original>>
    <<ClearPose>> 
    <<SetPose Shaking>> 
    FINE FINE! YOU GOT ME ##line:dde816
    <<SetSpeaker Player>>
    hold on- ##line:882762
    <<SetSpeaker Original>>
    I LOST IT ##line:3ccc3f
    I'M A TERRIBLE LIAR ##line:e600fa
    I'M A MESS ##line:c3f85e
    <<SetSpeaker Player>>
    uhhhhhhhhhh- ##line:fe2fc1
    <<SetSpeaker Original>>
    PLEASE DON'T WRITE ME UP ##line:b3aa9d
    I CAN BRIBE YOU ##line:e85302
    <<Hide>>
    <<PlaySound PickUp>>
    <<GiveItem Bait 5>>
    <<Wait 1>> 
    <<SetSpeaker Player>>
    <<Face Original>>
    ... ##line:085e0e
    did you really just hand\nme a fistful of worms? ##line:654eb8
    <<SetSpeaker Original>>
    IT'S FISHING BAIT!! ##line:a8530b
    I'M DESPERATE!! ##line:64224f
    WHAT WOULD YOU DO?! ##line:2b567d
    <<SetSpeaker Player>>
    well, uh ##line:bb2e31
    it doesn't matter ##line:148e27
    i'm not gonna tell anyone! ##line:57f777
    <<SetSpeaker Original>>
    <<ClearPose>> 
    wait, really? ##line:74f1fc
    aren't you a junior ranger? ##line:3ba4c7
    <<SetSpeaker Player>>
    nope ##line:92558e
    my aunt's a ranger ##line:827d58
    but i don't work for her ##line:74bd5d
    <<SetSpeaker Original>>
    oh well ##line:6aa300
    you won't tell right? ##line:db985f
    <<SetSpeaker Player>>
    nah ##line:eeb843
    <<SetSpeaker Original>>
    good! ##line:305a88
    i guess ##line:be4b3a
    you can keep the bait ##line:aaac24
    <<SetSpeaker Player>>
    ...thanks ##line:b093fa
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>> 
    where'd you lose the permit? ##line:ba3592
    <<SetSpeaker Original>>
    well... ##line:69c63f
    you might not believe me ##line:fa0875
    <<SetSpeaker Player>> 
    try me ##line:803d48
    <<SetSpeaker Original>>
    a fish ate it ##line:673f6f
    <<SetSpeaker Player>> 
    what? ##line:e89ded
    come on ##line:03a8fc
    <<SetSpeaker Original>>
    really! ##line:7585e0
    <<SetSpeaker Player>>
    it does sound a little... ##line:1192ab
    fishy! ##line:695d8a
    heh ##line:fc6810
    <<SetSpeaker Original>>
    ... ##line:62a7fc
    <<SetPose Shaking>> 
    this is serious! ##line:bb579f
    <<ClearPose>> 
    <<set $_MissingPermit to true>> 
<<else>> 
    [[CampCatFreakout]] 
<<endif>>
===
title: VolleyKidChat
tags: 
colorID: 0
position: 7165,-2861
---
<<if visited() is false>>
    so ##line:85d0f9
    do you love it? ##line:9b9647
    <<SetSpeaker Player>>
    beachstickball? ##line:0ddac1
    <<SetSpeaker Original>>
    yeah!! ##line:370aee
    <<SetSpeaker Player>>
    -> i like it! ##line:0c80b9
        thanks for showing me the game! ##line:73c36f
        <<SetSpeaker Original>>
        <<SetEmotion Happy>>
        thanks for playing! ##line:4a27f2
        you're the best, claire! ##line:80ddd6
        <<ClearEmotion>>
    -> i dunno ##line:ad2aa7
        i think i still prefer volleyball ##line:bda72b
        <<SetSpeaker Original>>
        WHAT ##line:b7dbab
        but beachstickball is like\nvolleyball with super powers! ##line:be2c6d
        the super power of WHACKING\nTHINGS WITH A STICK!! ##line:e67743
<<else>>
    our best score so far is {{BallHitsBest}}! ##line:b337bd
    <<if $_BallHitsBest >= 20 and $BallChat1 is false>>
        <<SetSpeaker Player>>
        that's pretty good! ##line:734516
        <<SetSpeaker Original>>
        yeah! ##line:ac8f02
        <<SetEmotion Happy>>
        we make a good team! ##line:4e13fc
        <<ClearEmotion>>
        <<set $BallChat1 to true>>
    <<elseif $_BallHitsBest >= 30 and $BallChat2 is false>>
        <<SetSpeaker Original>>
        i think this is history in the making ##line:b5a671
        <<SetSpeaker Player>>
        i'd say so ##line:05ad24
        <<set $BallChat2 to true>>
    <<endif>>
<<endif>>
===
title: GiveVolleyPrize3_2
tags: 
colorID: 0
position: 8083,-2252
---
<<Hide>>
<<set $_GotKidHat to true>>
<<set $GotVolleyPrize3 to true>>
<<GiveItem KidHat 1>>
<<Wait 1>>
<<SetSpeaker Player>>
<<Face Original Player>>
woah ##line:53839d
<<SetSpeaker Original>>
please keep it! ##line:0316a3
so you can remember\nthis moment forever!! ##line:2e1d70
<<SetSpeaker Player>>
i'll try ##line:9e97cf
<<GiveAchievement BeachstickballChamp>> 
===
title: WalkieTalkieStart
tags: 
colorID: 0
position: 9322,-6165
---
<<PlaySound RadioOn>>
<<SetPose OnPhone>>
<<Wait 0.5>>

<<if GetSceneName() == "GameScene">>
    <<if $_StartedRace is true and $_RaceResultDiscussed is false>>
        <<if IsVisible("RaceOpponent") is true and $_FinishedRace is true>>
            [[WalkieOnScreen]]
        <<else>>
            [[WalkieMidRace]]
        <<endif>>
    <<else>>
        <<if IsVisible("RaceOpponent") is true>>
            [[WalkieOnScreen]]
        <<else>>
            [[WalkieNotRacing]]
        <<endif>>
    <<endif>>
<<else>>
    [[WalkieNobody]]
<<endif>>
===
title: WalkieMidRace
tags: 
colorID: 0
position: 9064,-5910
---
<<SetStyle AveryRadioTextBoxProfile>>

<<if $_FinishedRace is false>>
    uhh ##line:6b4643
    i'm running and talking here ##line:2815c3
    but, what's up? ##line:fce2e0
<<else>>
    hey! ##line:42b890
    i'm waiting for you ##line:b78535
    what's up? ##line:9f0846
<<endif>>

<<SetSpeaker Player>>
-> let's start over! ##line:4664cb
    <<SetStyle AveryRadioTextBoxProfile>>        
    oh, uh ##line:2fad91
    sure, we can start over ##line:c725b9
    <<set $_RaceResultDiscussed to true>>
    <<SendMessage RaceCoordinator CallRestartRace>>
-> i give up ##line:0b9471
    <<SetStyle AveryRadioTextBoxProfile>>
    aww, well ##line:eeec64
    ok i guess ##line:cf0cb8
    <<set $_RaceResultDiscussed to true>>
    <<SendMessage RaceCoordinator CallAbandonRace>>
-> nothing much ##line:3f3cb7
    <<if $_FinishedRace is false>>
        <<SetStyle AveryRadioTextBoxProfile>>
        there's no time to chit chat ##line:cdbd9e
        a race is underway! ##line:b7ad54
    <<else>>
        <<SetStyle AveryRadioTextBoxProfile>>
        oh ok ##line:23d9ce
        see you soon? ##line:112092
    <<endif>>

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: WalkieNotRacing
tags: 
colorID: 0
position: 9304,-5906
---
hey, where are you? ##line:36e8b5
<<SetStyle AveryRadioTextBoxProfile>>
<<if $_RaceResultDiscussed is true>>
    <<if $RaceAbandoned is true>>
        uh... ##line:666c07
        just wandering around somewhere... ##line:3957bb
    <<elseif $_RaceId is "LighthouseRace">>
        i'm still at the lighthouse! ##line:ac1f13
    <<elseif $_RaceId is "OldBuildingRace">>
        i'm still at that old building! ##line:422cc9
    <<elseif $_RaceId is "MountainTopRace">>
        i'm still at the top of the mountain! ##line:c210da
    <<else>>
        um... ##line:80c82b
        i don't know! ##line:35662f
    <<endif>>
<<elseif $_RaceId is "LighthouseRace">>
    i'm hanging out at the royal ridge! ##line:9063e5
<<elseif $_RaceId is "OldBuildingRace">>
    i'm around the graveyard! ##line:c8f6dd
<<elseif $_RaceId is "MountainTopRace">>
    i'm near the visitor center! ##line:c37eaf
<<else>>
    uhh... ##line:b3b718
    i don't know! ##line:f6baec
<<endif>>

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: WalkieNobody
tags: 
colorID: 0
position: 9532,-5906
---
hello? ##line:0f245f
is anyone there? ##line:b840fb
... ##line:57b389

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: WalkieOnScreen
tags: 
colorID: 0
position: 9060,-6162
---
<<SetSpeaker RaceOpponent>>
<<Face Player>>
<<SetPose RaiseArms>>
hey, i'm right here! ##line:fcfe77
<<ClearPose>>

<<PlaySound RadioOff>>
<<ClearPose Player>>
===
title: FishingTutorialStart
tags: 
colorID: 0
position: 6793,-5142
---
<<if $DidFishTutorial is true>>
    [[FisherTalk]]
<<endif>>

<<if visited() is true>>
    <<SetSpeaker Player>>
    hey ##line:a2947e
    <<SetSpeaker Original>>
    hello again ##line:0ef52a
    <<SetSpeaker Player>>  
    -> can you teach me how to fish? ##line:0f9dd9
        <<SetSpeaker Original>>
        sure ##line:bc3e3a
        [[FishingTutorialBegin]]
    -> uh gotta go ##line:6a7217
        <<SetSpeaker Original>>
        alrighty ##line:ab1fac
        you get going then ##line:3a790b
<<else>> 
    <<SetSpeaker Player>>
    how's it going? ##line:b212b9
    <<SetSpeaker Original>>
    good ##line:789ef4
    the fish are biting today ##line:8e7edc
    <<SetSpeaker Player>>
    cool cool ##line:1f5189
    so ##line:c44b59
    uh ##line:a4385e
    is fishing hard? ##line:ded164
    <<SetSpeaker Original>>
    nope ##line:9666a7
    it's easy ##line:91cca3
    i could show you,\nif you'd like ##line:f48c91
    <<SetSpeaker Player>>
    -> yeah! ##line:1c9ae7
        [[FishingTutorialBegin]]
    -> not now... ##line:9caa9b
        ...i've got somewhere to be ##line:f4c5b4
        maybe later! ##line:f278bb
<<endif>> 
===
title: FishTutCastStart
tags: 
colorID: 0
position: 6796,-4915
---
casting yer line is pretty easy ##line:291979
just press <color=red>{{UseItemButton}}</color> ##line:6604e8
===
title: FishTutChatStart
tags: 
colorID: 0
position: 6788,-4681
---
<<Wait 4>>
<<SetSpeaker Player>>
so ##line:7c1ab4
uh ##line:0d5261
what do i do now? ##line:27214f
<<SetSpeaker Original>>
ya wait ##line:d61168
<<SetSpeaker Player>>
oh ##line:82e61b
ok ##line:57dd59
<<Hide>>
<<Wait 4>>
<<SetSpeaker Player>>
uhh ##line:4881ca
am i doing this right? ##line:208931
<<SetSpeaker Original>>
yup ##line:0ee4bd
<<Hide>>
<<Wait 5>>
<<SetSpeaker Player>>
maybe ##line:9738da
i should cast it again? ##line:d20e40
you know ##line:2b2920
find a better spot? ##line:a8b829
<<SetSpeaker Original>>
this spot is good ##line:143bf5
i saw some fish\naround here earlier ##line:a6ae7b
that's a good sign ##line:7aaa7b
sometimes ##line:bb4c5a
you just gotta wait ##line:fedeb0
patience is key ##line:fa0c59
<<SetSpeaker Player>>
hmm ##line:c98dd5
not my strong suit ##line:3391fe
===
title: FishTutNibbleStart
tags: 
colorID: 0
position: 6788,-4452
---
look alive! ##line:6176a1
a fish is nibbling ##line:711ad5
<<SetSpeaker Player>>
should i pull it out? ##line:ec2b87
<<SetSpeaker Original>>
not yet... ##line:ea2579
ya gotta wait\nfor it to bite ##line:2d1808

===
title: FishTutBiteStart
tags: 
colorID: 0
position: 6777,-4231
---
now! ##line:21ea75
press <color=red>{{UseItemButton}}</color> to yank it out! ##line:022e5a
===
title: FishTutPullStart
tags: 
colorID: 0
position: 6779,-3997
---
<<SetSpeaker Player>>
UHHH ##line:f28977
WHAT DO I DO NOW!? ##line:1b9660
<<SetSpeaker Original>>
<<if IsUsingJoystick() is true>>
    <color=red>pull back</color> AND <color=red>tap {{UseItemButton}}</color> to reel it in! ##line:62edd4
<<else>> 
    <color=red>pull back</color> AND <color=red>tap {{UseItemButton}}</color> to reel it in! ##line:aaaaa1
<<endif>> 
===
title: FishTutDoneStart
tags: 
colorID: 0
position: 6781,-3767
---
see? ##line:0a85bc
easy as pie ##line:797b04
<<SetSpeaker Player>>
<<LookAt Original>> 
i dunno if i'd say that ##line:a8f041
i suck at baking ##line:938324
<<SetSpeaker Original>>
i guess i mean ##line:5d1fb5
as easy as... ##line:951cea
...eating pie? ##line:d50e44
<<SetSpeaker Player>>
i can get behind that ##line:b792ef
<<ClearLookAt>>
===
title: FisherTalk
tags: 
colorID: 0
position: 6555,-5136
---
<<if visited() is false>>
    <<if $FellInWater is true>> 
        <<SetSpeaker Player>>
        sorry i fell in the water ##line:5a3560
        it's pretty embarrassing... ##line:301d2a
        <<SetSpeaker Original>>
        not at all ##line:6df5aa
        you've got spirit, kid ##line:106ccc
    <<else>>
        <<SetSpeaker Player>>
        so what do i do with the fish? ##line:2d127f
        <<SetSpeaker Original>>
        whatever you want, kid ##line:d22a1d
    <<endif>> 
<<else>>
    <<SetSpeaker Player>>
    well ##line:b3a844
    thanks for showing me how to fish ##line:edae7d
    <<SetSpeaker Original>>
    no problem ##line:7d90ad
    <<if $FellInWater is true>>
        you'll get the hang\nof it eventually ##line:4dde16
    <<else>>
        yer a natural ##line:a1f06c
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        i don't feel like one! ##line:e15e41
        <<ClearEmotion>> 
    <<endif>> 
<<endif>>
===
title: FishBuyerStart
tags: 
colorID: 0
position: 6218,-5826
---
<<if visited() is true>>
    <<if $_BrokenBoatEngine is true and $NoticedGrossBoatNoise is true>>
        [[FishBuyerBrokenBoat]] 
    <<else>> 
        howdy! ##line:f5e703
        got any fish? ##line:f86c61
        <<if HasItem("Fish", 1) is true>>
            [[FishBuyerMenu]]
        <<else>>
            <<SetSpeaker Player>>
            not right now! ##line:5f8ba4
        <<endif>>
    <<endif>> 
<<else>>
    <<SetSpeaker Original>>
    howdy ##line:3272ea
    <<SetSpeaker Player>>
    hi! ##line:1aa413
    whatcha doing? ##line:9c378b
    <<SetSpeaker Original>>
    takin' a break! ##line:3af5c8
    heh ##line:ebebee
    don't tell anyone, alright? ##line:55ce13
    <<SetSpeaker Player>>
    i won't ##line:35f2e4
    i don't even know\nwho i'd tell! ##line:465370
    <<SetSpeaker Original>>
    heh, good ##line:2dffb9
    i can't wait to\nbe done today ##line:f34e5d
    when my shift's over ##line:344b05
    i'll be out catchin'\nme some rare fish! ##line:c78884
    it's my hobby, ya see ##line:b1ae1b
    if you happen to net\nsome fish yourself... ##line:ffc7e7
    ...bring them to me and\ni'll trade ya coin! ##line:80b8d1
    <<SetSpeaker Player>>
    <<if $_BrokenBoatEngine is true and $NoticedGrossBoatNoise is true>>
        well ##line:a07a76
        i actually wanted to ask\nyou about something else... ##line:8fa496
        [[FishBuyerBrokenBoat]] 
    <<elseif HasItem("Fish", 1) is true>>
        i caught some fish already! ##line:498ec5
        <<SetSpeaker Original>>
        oh? ##line:e58d91
        can i take a look at 'em? ##line:e63e16
        [[FishBuyerMenu]]
    <<else>>
        <<if HasItem("FishingRod", 1) is false>>
            i would if i had\na fishing rod! ##line:474bc1
            <<SetSpeaker Original>>
            ya know ##line:9fd6f9
            my friend bill might be\nable to lend you one ##line:9970ab
            he's out fishing all the time ##line:c59c53
        <<else>>
            sounds good to me! ##line:5109d5
        <<endif>>
    <<endif>>
<<endif>>
===
title: FishBuyerMenu
tags: 
colorID: 0
position: 6213,-5584
---
<<SetSpeaker Player>>
-> take a look at this... ##line:440856
    <<SendMessage FishBuyer SellFish>>
-> not right now ##line:1a0169
    <<SetSpeaker Original>>
    alrighty ##line:367d35
===
title: BuyFish
tags: 
colorID: 0
position: 5926,-5523
---
<<if $_FishAlreadySold is true>>
    [[BuyFishAgain]]
<<endif>>

<<if $_FishRare is false>>
    [[BuyFishQuip]]
<<else>>
    [[BuyRareFishQuip]]
<<endif>>


===
title: BuyFishEnd
tags: 
colorID: 0
position: 5933,-4714
---
is that all? ##line:3f698b
<<SetSpeaker Player>>
<<if HasItem("Fish", 1) is true>>
    -> take a look at this... ##line:a459c7
        <<SendMessage FishBuyer SellFish>>
    -> that's all! ##line:b45043
        <<SetSpeaker Original>>
        pleasure doin'\nbusiness with ya ##line:50990b
<<else>>
    that's all for now! ##line:3a381e
<<endif>>
===
title: BuyFishAgain
tags: 
colorID: 0
position: 5686,-5253
---
<<set $_BaitSold to true>>
<<if visited() is false>>
    sorry pal ##line:6d06d0
    i've already got that fish ##line:12195a
    but... ##line:19ef60
    i can trade you <color=red>bait</color> for fish\nyou've already given me! ##line:767d57
    sure, you can catch\nfish without bait ##line:78df10
    but <color=red>it's a lot faster</color> with it! ##line:c0e82a
<<else>>
    i'll trade you bait for that ##line:5a2721
<<endif>>

<<Hide>>
<<GiveItem Bait 1>>
<<Face Original Player>>
<<Wait 1>>
[[BuyFishEnd]]
===
title: BuyFishCancel
tags: 
colorID: 0
position: 6170,-4717
---
'till next time! ##line:e92255
===
title: BuyFishAction
tags: 
colorID: 0
position: 6064,-4985
---
<<SendMessage FishBuyer PayCoins>>
<<PlaySound BuySound>>
<<Hide>>
<<Wait 1>>

<<if visitCount() is 2>>
    you're quite good at this! ##line:61c4db
    you should take my journal ##line:24427b
    it'll help ya keep track\nof the fish you've found ##line:e9c9ba
    <<Hide>>
    <<GiveItem FishEncyclopedia 1>>
    <<Wait 1>>
    <<Face Original Player>>
    so ##line:3ff5f0
<<endif>>

<<if HasSoldAllFish() is true and $SoldAllFishReward is false>> 
    well now! ##line:1d1e6e
    if i'm not mistaken ##line:f8c779
    it seems like you've gotten me\nall the fish from my journal! ##line:0aa198
    claire! ##line:01fa28
    you've REALLY got\na knack for this! ##line:ede695
    <<SetSpeaker Player>>
    thanks! ##line:fcd7dc
    <<SetSpeaker Original>> 
    hmm ##line:044f51
    i've got something i think\nyou might find useful ##line:bc62db
    i think i can trust you with this ##line:fb202a
    <<Hide>>
    <<GiveItem FishingRod -1>> 
    <<GiveItem GoldenFishingRod 1>>
    <<Wait 1>>
    <<Face Original Player>>
    <<SetSpeaker Player>>
    wow! ##line:ae9b42
    it's very pretty ##line:589568
    <<SetSpeaker Original>> 
    rare fish are drawn to it ##line:c61af9
    ya might find some\ninteresting fish with it! ##line:6cd696
    but don't lose it ##line:c99a3d
    it's very precious to me ##line:582f35
    it's been passed down for\ngenerations within my family ##line:20f45e
    <<SetSpeaker Player>>
    woah ##line:b56eb9
    <<SetSpeaker Original>> 
    also ##line:166488
    it's also made of gold ##line:91991d
    so ##line:92032c
    yeah ##line:dd66ae
    don't lose it! ##line:b9163e
    <<SetSpeaker Player>>
    i won't! ##line:c74434
    <<set $SoldAllFishReward to true>> 
    <<SetSpeaker Original>>
    <<GiveAchievement AllFish>> 
<<endif>> 

[[BuyFishEnd]]
===
title: BuyFishQuip
tags: 
colorID: 0
position: 5924,-5253
---
<<if $QuipLine is 0>>
    the {{FishName}}, eh? ##line:b56f2a
    i'll give you {{FishPrice}} for it ##line:97bebb
<<elseif $QuipLine is 1>>
    would you look at that! ##line:894a90
    the {{FishName}}! ##line:07d9c0
    i can give you {{FishPrice}} for it ##line:88a69a
<<elseif $QuipLine is 2>>
    nice catch ##line:8b666f
    the {{FishName}} is a\npersonal favourite of mine ##line:fd1bc1
    i'll trade you {{FishPrice}} coins for it ##line:e586e5
<<elseif $QuipLine is 3>>
    good one! ##line:7b947d
    the {{FishName}} has\nevaded me for a while ##line:afa6f1
    but no longer! ##line:85a662
    i'll give you {{FishPrice}} for it ##line:33eb67
<<elseif $QuipLine is 4>>
    the {{FishName}}... ##line:4b40dd
    not a fan of this one ##line:0f5fd0
    bit me once! ##line:34830c
    but i'm still looking for it! ##line:5182f6
    i'll give you {{FishPrice}} coin ##line:a781ea
<<elseif $QuipLine is 5>>
    ah the {{FishName}} ##line:b000f2
    this one is popular here at hawk peak ##line:94362f
    i'll give you... ##line:3f17f4
    ...{{FishPrice}} coins for it ##line:be6b8e
<<else>>
    the {{FishName}} ##line:b983f1
    hmm ##line:53d466
    i think {{FishPrice}} coins\nseems like a fair price ##line:09a6c0
<<endif>>

<<set $QuipLine to $QuipLine + 1>>
<<if $QuipLine > 6>>
    <<set $QuipLine to 0>>
<<endif>>

[[BuyFishAction]]
===
title: BuyRareFishQuip
tags: 
colorID: 0
position: 6153,-5255
---
<<set $FoundAndSoldRareFish to true>>
<<if $RareQuip is 0>>
    now hold on... ##line:af44d3
    what is THIS?! ##line:68b8f5
    the rare {{FishName}}?!? ##line:3932c2
    that's a mighty catch right there! ##line:33b39b
    i'll pay ya {{FishPrice}} coins for it! ##line:4b6053
<<elseif $RareQuip is 1>>
    ...now, what is this? ##line:92a95e
    i don't recognize this one ##line:57c68d
    ... ##line:628f42
    is that the {{FishName}}? ##line:64082f
    that's a rare find! ##line:318344
    i'll give you {{FishPrice}} to add\nit to my collection! ##line:55326e
<<elseif $RareQuip is 2>>
    you've GOT to be kidding me ##line:6a3b6c
    you netted the {{FishName}}!? ##line:649c04
    gosh dang it! ##line:f4fe1c
    you've really got\na knack for fishing! ##line:7ba8b6
    <<SetSpeaker Player>>
    i dunno about that! ##line:30fc6c
    <<SetSpeaker Original>>
    welp ##line:fe0468
    i gotta give you at\nleast {{FishPrice}} coins for this! ##line:c9145d
<<elseif $RareQuip is 3>>
    WHAT THE HECKY?! ##line:dd00eb
    claire ##line:167408
    you're a fishing legend! ##line:e73033
    the {{FishName}}\nis one TRICKY catch! ##line:95aed6
    i'll be givin' you\n{{FishPrice}} coin for that! ##line:0cee57
<<elseif $RareQuip is 4>>
    <<if $CrazyFishStory is false>>
        well i'll be! ##line:11948d
        the {{FishName}} huh? ##line:80ebac
        we go way back ##line:c4e192
        the last time i saw this guy\nwas almost FIVE whole years ago! ##line:ea1733
        ya see, ##line:e2023a
        i had it on the hook ##line:49307f
        it was givin' a real strong tug ##line:79fe4a
        and i mean ##line:08d8a6
        like this fish- ##line:fbeb38
        has got muscles like\nyou wouldn't believe! ##line:43ea55
        it nearly pulled me\noff the boat! ##line:7fa3cf
        <<SetSpeaker Player>>
        no way! ##line:5894ed
        <<SetSpeaker Original>>
        yeah way! ##line:1feda5
        anyway... ##line:5233ac
        i gotta give you {{FishPrice}}\ncoin for this one! ##line:c96826
        <<set $CrazyFishStory to true>>
    <<else>>
        the {{FishName}}!? ##line:9a2691
        now that's a spectacular find! ##line:e8eb1e
        i'll give you {{FishPrice}} for it! ##line:57e063
    <<endif>>
<<else>>
    WHA-WHAT!? ##line:231720
    i've heard LEGENDS of the\n{{FishName}} at hawk peak ##line:cfe338
    but here it is... ##line:19bd98
    ...in front of MY VERY OWN EYES!! ##line:ba2d62
    claire ##line:ca47a0
    i'll be givin' ya {{FishPrice}}\ncoins for this catch ##line:ece6f1
<<endif>>

<<set $RareQuip to $RareQuip + 1>>
<<if $RareQuip > 5>>
    <<set $RareQuip to 0>>
<<endif>>

[[BuyFishAction]]
===
title: FishingTutorialBegin
tags: 
colorID: 0
position: 7020,-5141
---
<<SetSpeaker Original>>
<<set $DidFishTutorial to true>>
here ##line:b283cd
i'll lend ya my spare rod ##line:34583b
<<Hide>>
<<GiveItem FishingRod 1>>
<<SendMessage FishingTutorial StartTutorial>>
===
title: AveryBuildingSignStart
tags: 
colorID: 0
position: 6889,-5970
---
<<if visited() is false>> 
    Dear Sign Reader: ##line:6339c3
    Do you like reading signs? ##line:41138b
    I assume so, since this\nis happening right now. ##line:f55731
    Well... ##line:dc8306
    ...I can introduce you to\nsomething WAY better than reading! ##line:3d605e
    Two words: ##line:22768c
    PARKOUR ##line:1995fc
    RACE ##line:5bca9d
    PARKOUR RACE!! ##line:279e34
    I'll be back right after I set up the\nfinish line at the <color=yellow>abandoned building.</color> ##line:b2c7ee
    -Avery ##line:867ec1
    <<set $ReadAverySignBuilding to true>>
<<else>>
    <<SetSpeaker Player>> 
    hmm ##line:a8922b
    maybe i better figure out where\nthis <color=red>abandoned building</color> is ##line:75c453
<<endif>> 
===
title: AveryLighthouseSignStart
tags: 
colorID: 0
position: 7119,-5972
---
<<if visited() is false>> 
    Dear Sign Reader: ##line:195ec3
    Hi! ##line:ea09e4
    I don't know you. ##line:28aa9d
    But you're probably pretty cool. ##line:594736
    I'm cool too! ##line:265117
    We should race each other. ##line:e4f834
    It would be cool. ##line:9c692f
    BRB! ##line:5bbb11
    I'm gonna setup a ribbon\nat the <color=yellow>lighthouse.</color> ##line:dec8bf
    -Avery ##line:349c42
    <<set $ReadAverySignLighthouse to true>>
<<else>>
    <<SetSpeaker Player>> 
    hmm ##line:2eedcb
    i wonder where that <color=red>lighthouse</color> is... ##line:f415cc
<<endif>> 
===
title: AveryIntroduction
tags: 
colorID: 0
position: 7511,-7034
---
<<if $AgreedToAvery is true>> 
    [[BeforeRaceIntro]]
<<endif>> 

<<if visited() is false>>
    <<set $_A to $ReadAverySignLighthouse is true and $_RaceId is "LighthouseRace">>
    <<set $_B to $ReadAverySignBuilding is true and $_RaceId is "OldBuildingRace">>
    <<if $_A or $_B>> 
        <<SetSpeaker Player>> 
        hi! ##line:be93a4
        are you avery? ##line:c41913
        <<SetSpeaker Original>> 
        YES! ##line:c5cb9e
        that's me!! ##line:f330be
        did you read my ad? ##line:57672b
        <<SetSpeaker Player>> 
        the sign? ##line:b6a40c
        yeah ##line:d7faa0
        <<SetSpeaker Original>> 
        GREAT! ##line:379365
        sooo ##line:ada9fe
        you wanna race? ##line:e5f6bc
        [[AveryIntroQuestion]]
    <<elseif $ReadAverySignBuilding or $ReadAverySignLighthouse>> 
        HI!! ##line:38eabe
        my name is AVERY!! ##line:138175
        it's a pleasure to meet you!! ##line:1f6831
        <<SetSpeaker Player>>
        hi ##line:0689f6
        i'm claire ##line:5d2d43
        i think i saw your sign ##line:3efef0
        <<SetSpeaker Original>>
        you DID!? ##line:c42308
        EXCELLENT! ##line:2a9577
        so have you come to race me? ##line:16a037
        [[AveryIntroQuestion]]
    <<else>>
        HI!! ##line:13a7d0
        my name is AVERY!! ##line:4058f3
        i'm on a quest to become a\nPARKOUR RACING MASTER!! ##line:e21c0f
        <<SetSpeaker Player>>
        neat! ##line:09b85d
        <<SetSpeaker Original>>
        <<SetPose RaiseArms>> 
        you BET YOUR BOTTOM\nDOLLAR it's neat!! ##line:e5d953
        <<ClearPose>> 
        however ##line:f112fa
        i'm trying to find someone\nto practice with! ##line:60e62b
        so... ##line:a3f689
        ...any chance you wanna\ngo for a race? ##line:07eebc
        [[AveryIntroQuestion]]
    <<endif>>
<<else>>
    <<SetEmotion Happy>>
    hello again! ##line:161a9e
    you wanna race me? ##line:d89d2a
    <<ClearEmotion>>
    [[AveryIntroQuestion]]
<<endif>>
===
title: AveryStory
tags: 
colorID: 0
position: 7912,-5807
---
<<SetSpeaker Player>>
there's this big race happening today! ##line:847103
<<SetSpeaker Original>>
WELL... ##line:0c7748
um ##line:c66965
it's a little embarrassing, but ##line:e86d3a
i was SUPPOSED to run in\nthe marathon today ##line:7eccb9
but it turns out i\nmissed registration!! ##line:5e5d79
<<SetSpeaker Player>>
ohh ##line:53ca5e
sorry, that sucks ##line:6899de
<<SetSpeaker Original>>
yup ##line:d03709
i gotta say ##line:fc23e2
i was pretty bummed out ##line:ae8420
kinda felt like giving up on racing ##line:70fec6
... ##line:c81a55
but then i spent like 2 hours\nwatching parkour videos ##line:336807
and now my new passion\nis PARKOUR RACING!! ##line:b11f26
so whatever ##line:152928
<<SetEmotion Happy>> 
i'm glad you were willing\nto help me practice! ##line:39df05
<<ClearEmotion>> 
<<set $AskedAboutAvery to true>>
===
title: AveryIntroQuestion
tags: 
colorID: 0
position: 7758,-7031
---
<<SetSpeaker Player>>
-> sure! ##line:1fc720
    <<SetSpeaker Original>>
    <<set $AgreedToAvery to true>>
    <<SetEmotion Happy>>
    <<SetPose RaiseArms>>
    FAAANTASTIC! ##line:5d6a3f
    <<ClearPose>>
    <<ClearEmotion>>
    <<set $JustMetAvery to true>>
    [[BeforeRaceIntro]]
-> later ##line:87caf1
    <<SetSpeaker Original>>
    oh well ##line:979808
    alright then ##line:b43347
    
===
title: BuildingRaceIntro
tags: 
colorID: 0
position: 7891,-6158
---
<<set $BuildingRaceFound to true>>

<<if visited() is false>>
    <<set $RacesFound to $RacesFound + 1>>
    <<if $JustMetAvery is false>>
        hiya! ##line:1dfc0b
        ok ok so ##line:1b45fa
        i've set up another route to race! ##line:b3bcd8
        you want in? ##line:dc7795
        <<SetSpeaker Player>>
        sure! ##line:ba60b5
        where we racing to? ##line:dff24b
        <<SetSpeaker Original>>
    <<endif>>
    so there's this abandoned building\nto the west of here ##line:021dc4
    at least ##line:f68100
    i think it's abandoned? ##line:12ad83
    anyway ##line:d00be3
    i'm thinking we should race\nto the top of it! ##line:2178c6
    whoever touches the post\nup there first WINS! ##line:d50cd3
    and also ##line:6185b8
    we should only use <color=red>5 golden\nfeathers</color> during this race! ##line:2c7375
    that'll make\nit more interesting! ##line:4d180f
    you ready? ##line:36faf6
<<elseif $_LighthouseRace_Attempted is false>>
    so how about that race? ##line:87bd3d
<<else>>
    hello again! ##line:fecfc9
    you wanna rematch to the abandoned building? ##line:eb3492
<<endif>>

[[BeforeRaceMenu]]
===
title: BuildingRaceRetry
tags: 
colorID: 0
position: 8789,-6404
---
<<SetSpeaker Original>>
alright! ##line:5b8929
LET'S GET TO IT! ##line:5a5e6e
<<set $_RaceId to "OldBuildingRace">>
<<set $_ResetRace to true>>
===
title: CancelRaceShoes
tags: 
colorID: 0
position: 8428,-6888
---
okay ##line:1a7c54
i'll wait 'till you've found a pair! ##line:6523af
===
title: ConsiderFeathersRace
tags: 
colorID: 0
position: 8677,-6648
---
hmm! ##line:cf5a2d
you might want to find more feathers\nbefore trying this race ##line:1a01ba
it might be hard to keep\nup without them! ##line:d65056
<<SetSpeaker Player>>
-> i'll race anyway! ##line:34ba2d
    <<SetSpeaker Original>>
    alrighty ##line:270726
    lets do this! ##line:842f68
    <<set $_BeginRace to true>>
-> wait, nevermind ##line:dbe9d2
    <<SetSpeaker Original>>
    i'll wait until you find more! ##line:8023cb
===
title: WinRegular
tags: 
colorID: 0
position: 7955,-5429
---
<<set $BeatOneRace to true>>
<<if visitCount() % 3 is 0>> 
    ok ##line:b8b05f
    i have to admit ##line:410521
    you are GOOD! ##line:d03dbb
    but ##line:ee7b3d
    i feel like i was\njust getting warmed up! ##line:ab236a
<<elseif visitCount() % 3 is 1>> 
    dang you got me again! ##line:34b061
    you've got some slick moves ##line:a25523
    but ##line:a3fcc4
    i messed up a bit ##line:73d813
    i think i could have\ndone much better ##line:667c1a
<<else>>
    you beat me!! ##line:d9b608
    i thought i had that one... ##line:e9765f
    well ##line:de783c
    if you ever want another go, ##line:4f85ff
    i think i could still improve! ##line:f30d81
<<endif>>
[[AfterRaceMenu]]
===
title: WinGhost
tags: 
colorID: 0
position: 8184,-5427
---
<<if visitCount() % 3 is 0>> 
    claire you are a MACHINE ##line:d1e24e
    you got here in just {{WonRaceTime}} seconds! ##line:84abb0
    i bet i can learn\na lot from you ##line:c81118
    welp ##line:02e40e
    i'll always be around\nif you want a rematch! ##line:9809e3
    i bet if i borrowed some\nof your strategies... ##line:88fb28
    ...i could beat ya next time! ##line:7235ad
<<elseif visitCount() % 3 is 1>> 
    woah ##line:3c0edf
    you beat me again ##line:59d4d9
    i can't believe it only\ntook you {{WonRaceTime}} seconds! ##line:037fb6
    you're definitely the better racer ##line:b6224a
    i oughta try your route next time! ##line:e462ab
<<else>>
    gosh dang it! ##line:c275da
    you're too good! ##line:e4105e
    how am i supposed to\nbeat {{WonRaceTime}} seconds? ##line:ab381e
    hmm... ##line:2633f1
    next time, ##line:cf79da
    i'll try doing what you did! ##line:8e7e1a
<<endif>>
[[AfterRaceMenu]]
===
title: LoseRegular
tags: 
colorID: 0
position: 8682,-5255
---
<<if visitCount() % 3 is 0>> 
    i won! ##line:4fe818
    you put up a good race ##line:68c867
    <<SetEmotion Happy>>
    that was really fun! ##line:9407b2
    <<ClearEmotion>>
    [[GiveWalkieTalkie]]
<<elseif visitCount() % 3 is 1>>
    good race! ##line:9147fc
    you had some smooth moves ##line:3df838
    i'm always be down for a\nrematch if you want one! ##line:a1f4a4
<<else>>
    yes!! ##line:024a95
    i love a good race! ##line:e56819
    thanks for racing me ##line:5ad305
    <<SetEmotion Happy>>
    i hope i haven't tired you out! ##line:775c7e
    <<ClearEmotion>>
<<endif>>
[[AfterRaceMenu]]
===
title: LoseGhost
tags: 
colorID: 0
position: 8422,-5351
---
<<if visitCount() % 3 is 0>> 
    i did it! ##line:f50d0e
    finally! ##line:80a866
    although, if i'm being honest ##line:3105f5
    i kinda... copied your strategy from last time ##line:86ab73
    [[GiveWalkieTalkie]]
<<elseif visitCount() % 3 is 1>>
    i won! ##line:7fd42d
    phew! ##line:26f439
    that was tight ##line:046e08
    i hope you don't mind but ##line:b00e7c
    i borrowed some of your\nmoves for that run! ##line:13d8b9
<<else>>
    ahh i won! ##line:242318
    claire... ##line:ec2585
    i've learned so much from racing you! ##line:f728a3
    <<SetEmotion Happy>>
    i really owe you one!! ##line:467ab2
    i'm glad i've got\nyou as my racing buddy! ##line:04f642
    <<ClearEmotion>>
<<endif>>
[[AfterRaceMenu]]
===
title: GiveWalkieTalkie
tags: 
colorID: 0
position: 8685,-5548
---
<<if HasItem("WalkieTalkie", 1) is false>>
    <<Hide>>
    <<Wait 1>>
    OH! ##line:7668e2
    i nearly forgot ##line:eafb86
    i've got something for you! ##line:f5011f
    <<Hide>>
    <<GiveItem WalkieTalkie 1>>
    <<Face Original Player>>
    if you ever get lost during a race ##line:504db6
    ring me up and we can start over! ##line:dfdf49
<<endif>>
[[AfterRaceMenu]]
===
title: AuntDynamic
tags: 
colorID: 0
position: 10202,-5629
---
<<SetSpeaker Player>>
it's kinda been a busy day ##line:bfec65
<<SetSpeaker Original>>
oh yeah? ##line:c89ff4
what have you been up to? ##line:404828
<<SetSpeaker Player>>
well... ##line:0a3c2e

<<set $TalkPoints to 0>>

<<if HasItem("Toast", 1) and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:5af845
    i got a piece of toast ##line:8e9024
    <<SetSpeaker Original>>
    ...and strawberries?! ##line:e6bd98
    <<SetSpeaker Player>>
    no, just the toast ##line:5f05ce
    <<SetSpeaker Original>>
    aw ##line:409a51
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $AteToast and $TalkPoints < 3  or $AuntCheat2 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:46b377
    i ate some toast ##line:261c74
    <<SetSpeaker Original>>
    ...and strawberries?! ##line:009979
    <<SetSpeaker Player>>
    no, just the toast ##line:b2a1c9
    <<SetSpeaker Original>>
    aw ##line:2e2822
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if HasItem("GoldenFishingRod", 1) and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:fad92e
    i caught a lot of fish! ##line:4701d0
    and i mean like, ##line:d92c73
    a LOT of fish ##line:d279c1
    i filled out a whole journal of them! ##line:2f76d7
    <<SetSpeaker Original>>
    wow! ##line:648e7e
    you HAVE been busy ##line:9b81a1
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $FoundAndSoldRareFish and $TalkPoints < 3 or $AuntCheat2 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:dfa256
    i caught a really rare fish! ##line:fb29a1
    <<SetSpeaker Original>>
    since when did you learn to fish? ##line:125b38
    <<SetSpeaker Player>>
    just today! ##line:ab56c2
    <<SetSpeaker Original>>
    wow! ##line:a25894
    you HAVE been busy ##line:58bf75
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $DidFishTutorial and $TalkPoints < 3>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:224db4
    i learned how to fish! ##line:8f1170
    <<SetSpeaker Original>>
    did you now? ##line:1adf1a
    <<SetSpeaker Player>>
    yeah! ##line:52fcb1
    bill lent me a fishing rod ##line:acf384
    <<SetSpeaker Original>>
    of course it was bill! ##line:b1fcde
    i don't think i've ever\nseen him NOT fishing! ##line:0588d0
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $GotVolleyPrize2 and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:9ae69f
    i don't mean to brag but ##line:aebc89
    i think i became a beachstickball\nworld champion today ##line:b4c4e6
    <<SetSpeaker Original>>
    what's beachstickball? ##line:766c37
    <<SetSpeaker Player>>
    it's gonna be the next big thing ##line:d60db0
    it's like volleyball\nbut with a stick ##line:f81b4f
    <<SetSpeaker Original>>
    i'd play that! ##line:991695
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $PlayedBeachStickBall and $TalkPoints < 3  or $AuntCheat2 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:c7ee6a
    i played a game of beachstickball ##line:c3d3b6
    <<SetSpeaker Original>>
    what's beachstickball? ##line:695b04
    <<SetSpeaker Player>>
    it's some game the kids play ##line:edbc45
    <<SetSpeaker Original>>
    they're always playing something new ##line:23899f
    i can't keep up with\nthe youth these days ##line:3983bc
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_ClimbedSecretIslandTop and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:c90080
    i swam out to a mysterious island! ##line:4ba184
    <<SetSpeaker Original>>
    you mean pat's point? ##line:bdf471
    <<SetSpeaker Player>>
    is that it's name? ##line:9d3c6f
    that makes it sound like ##line:1881c5
    way less mysterious ##line:c58a48
    <<SetSpeaker Original>>
    sorry! ##line:ebbf38
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_HitMagicRock is true and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:445da7
    i whacked my shovel against this rock... ##line:e04a0e
    ...and MONEY came out! ##line:cd7653
    it was a MONEY ROCK! ##line:2decf0
    <<SetSpeaker Original>>
    well now you're\njust making stuff up! ##line:5ee56c
    i highly doubt you\nfound this "money rock" ##line:b89fb0
    ...in ontario ##line:35d0ae
    they're more of a\nwest-coast thing ##line:c46fe3
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_BoatIsFixed and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:d5c01f
    i drove a boat off a ramp!! ##line:b2a142
    <<SetSpeaker Original>>
    you WHAT? ##line:eb16f6
    <<SetSpeaker Player>>
    i drove a boat off a ramp! ##line:57cf5f
    <<SetSpeaker Original>>
    yes but ##line:a2e608
    well ##line:4f5037
    that doesn't sound safe ##line:61048d
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    it definitely wasn't! ##line:ec0fb9
    <<ClearEmotion>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>


<<if $BeatOneRace and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:cff45e
    i won a parkour race today! ##line:25bb37
    <<SetSpeaker Original>>
    wow! ##line:f3b6c6
    uh ##line:7928ef
    what's parkour? ##line:4e9c9a
    <<SetSpeaker Player>>
    uh it's like... ##line:ad18f9
    jumping around and climbing stuff? ##line:934183
    <<SetSpeaker Original>>
    that sounds fun ##line:4c1ca4
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_Artist6Talk and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:923cea
    i met an artist today! ##line:62c03b
    they were painting landscapes here ##line:fc6c10
    their art was amazing ##line:375b88
    <<SetSpeaker Original>>
    well hawk peak is beautiful ##line:cb77e8
    <<SetEmotion Happy>>
    so i'm not surprised! ##line:d39337
    <<ClearEmotion>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $MetHikerAtTop and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:1bcea0
    i made a friend at outlook point! ##line:91ce9c
    the firetower is really cool ##line:249ef2
    <<SetSpeaker Original>>
    yeah! ##line:87cd22
    it's a great place\nto watch the sunset ##line:68d4d8
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $_Broke_BIG_ROCK and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:8fbef4
    i found a secret shortcut\nthrough the mountain! ##line:d6ac30
    <<SetSpeaker Original>>
    you mean... ##line:d11cfe
    you got through the\nblocked tunnels!? ##line:7da2ab
    that's very dangerous ##line:3a7aeb
    they were blocked off\nfor a reason! ##line:e7693c
    <<SetSpeaker Player>>
    <<SetEmotion Happy Player>>
    haha whooooops! ##line:c63da6
    <<SetSpeaker Original>>
    claire! ##line:c12b29
    <<ClearEmotion Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $ListenedToFrogSpeech and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:ba9c90
    i think i met a future\nprime minister today... ##line:e14636
    either that or a\nmaster architect! ##line:86d7f5
    <<SetSpeaker Original>>
    you mean sandcastle kid? ##line:1786cb
    <<SetSpeaker Player>>
    yeah the sandcastle kid ##line:1d6dff
    <<SetSpeaker Original>>
    i'm not surprised ##line:c24aa0
    that's classic sandcastle kid ##line:d4a6b1
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $JoinedClimbClub and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:f7fa0b
    i joined a rock climbing club ##line:9e187f
    <<SetSpeaker Original>>
    very nice! ##line:75a43e
    that's good exercise ##line:749356
    is it a big club? ##line:d4ed04
    <<SetSpeaker Player>>
    uh ##line:8c6ac2
    it... ##line:35befd
    ...depends? ##line:ff84e0
    <<SetSpeaker Original>>
    hmm ok ##line:2bb787
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $ToughBirdFriend and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:3a7c33
    i paid a semester's tuition today! ##line:1c15b4
    <<SetSpeaker Original>>
    you WHAT?! ##line:8e2ebf
    <<SetSpeaker Player>>
    yeah uh ##line:20474f
    not mine but ##line:70ca6f
    i did ##line:c47b6e
    <<SetSpeaker Original>>
    uhh ##line:3ba7b7
    that's good i guess ##line:094dfd
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif $DeliveredNecklace and $TalkPoints < 3 or $AuntCheat2 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:d71849
    i think i joined\nthe gig economy ##line:4fcad8
    <<SetSpeaker Original>>
    you what? ##line:a611b9
    <<SetSpeaker Player>>
    eh it's a long story ##line:edc3b2
    <<SetSpeaker Original>>
    well ##line:fed760
    i hope the pay was good! ##line:a8188b
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $SoldHat and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:b93bda
    <<if "ParkHat" is $_WornHat>>
        i bought this rangers hat! ##line:91bfb2
        you wear yours so well, ##line:885ef3
        i thought i'd better get one too! ##line:ebedd0
        <<SetPose RaiseArms>>
        <<SetEmotion Happy>>
        what do ya think? ##line:d8a4f2
    <<else>>
        i bought a rangers hat! ##line:6ad66d
        you wear yours so well, ##line:0b64f5
        i thought i'd better get one too! ##line:4b3970
        check it out ##line:defe11
        <<SetPose RaiseArms>>
        <<Hide>>
        <<Wait 0.25>>
        <<set $_WornHat to "ParkHat">>
        <<Wait 0.25>>
    <<endif>>
    <<SetSpeaker Original>>
    nice buy! ##line:7510e8
    <<SetEmotion Happy>>
    it looks great on you!   ##line:f6d137
    <<ClearEmotion>>
    <<ClearEmotion Player>>
    <<ClearPose Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<elseif HasItem("Headband", 1) and $TalkPoints < 3  or $AuntCheat2 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:db992b
    i got this cool headband! ##line:7db621
    <<if "Headband" is $_WornHat>>
        <<SetEmotion Happy>>
    <<else>>
        check this out ##line:1c2a5d
        <<SetPose RaiseArms>>
        <<Hide>>
        <<Wait 0.25>>
        <<set $_WornHat to "Headband">>
        <<Wait 0.25>>        
        <<SetEmotion Happy>>
        eh? ##line:b00b9c
    <<endif>>
    pretty cool huh? ##line:b2e01d
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    it IS pretty cool ##line:dc6a15
    <<ClearPose Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<set $AnyTreasureMapFound to $_Unearthed_LighthouseShadow or $_Unearthed_SidBeachTreasure or $_Unearthed_KingsThrone>>
<<if $AnyTreasureMapFound and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:59fff7
    i found some buried treasure! ##line:d72750
    i had to solve a tricky riddle ##line:a56577
    <<SetSpeaker Original>>
    a riddle huh? ##line:432678
    that's cool ##line:1e6537
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if HasItem("Coin", 100) and $TalkPoints < 3 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    {{Transition}} ##line:593a7d
    i filled up my pockets! ##line:a5f657
    <<SetEmotion Happy>>
    <<SetPose RaiseArms>>
    look at all these coins i found! ##line:5060b9
    <<Hide>>
    <<ShowCollection Coin>>
    <<Wait 1.5>>
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    you aren't kidding! ##line:f62121
    that's some serious dough! ##line:a59e7f
    <<ClearEmotion Player>>
    <<ClearPose Player>>
    <<set $TalkPoints to $TalkPoints + 1>>
<<endif>>

<<if $TalkPoints == 0 or $AuntCheat1 is true>>
    <<SetSpeaker Player>>
    uh ##line:7d89f7
    i don't actually remember! ##line:406e81
    <<SetSpeaker Original>>
    you must be pretty tired! ##line:cda327
<<elseif $TalkPoints < 3  or $AuntCheat2 is true>>
    <<SetSpeaker Player>>
    and ##line:1c38e6
    i probably did a bunch more! ##line:e6e2fe
    i just don't remember... ##line:f922ab
    it's been a long day! ##line:f5fff3
    <<SetSpeaker Original>>
    fair enough! ##line:bc6864
<<endif>>


[[AuntHungry]]
===
title: AuntHungry
tags: 
colorID: 0
position: 10456,-5624
---
<<SetSpeaker Original>>
anywhoo ##line:e2d3a4
you hungry? ##line:4c8e11
<<set $TalkedAboutDinner to true>>
i'm gonna break out the hotdogs soon ##line:a07cf2
<<if $CurryDinner is true>>
    <<SetSpeaker Player>>
    i thought you were\ngonna make curry? ##line:5d1a0b
    <<SetSpeaker Original>>
    eh ##line:94ae72
    now i'm feeling lazy ##line:205292
<<endif>>
<<SetSpeaker Player>>
well... ##line:ef42c2
i'm kinda tired out ##line:3d17a3
i'm gonna go take a nap or something ##line:191dd6
<<SetSpeaker Original>>
alrighty then! ##line:297618
<<SetEmotion Happy>>
have a good one! ##line:953143
<<ClearEmotion>>

<<set $HadFinalAuntDialogue to true>>
===
title: FoundFishPermit
tags: 
colorID: 0
position: 10229,-3043
---
huh? ##line:46c54d
it has something\nin its mouth... ##line:1f7d59
it looks like... ##line:594261
a camping permit!? ##line:24382b
<<Hide>> 
<<GiveItem CampingPermit 1>>
===
title: CampCatFreakout
tags: 
colorID: 0
position: 10479,-3274
---
sorry for freaking out earlier ##line:7360c2
i've been on edge ever\nsince i lost my permit ##line:4c11c2
the thought of... ##line:169c62
...being accused of camping\nwithout a permit... ##line:5971e9
that's the ultimate nightmare! ##line:64f213
<<set $CampCatSorry to true>> 
===
title: IceSquirrelStart
tags: 
colorID: 0
position: 12451,-2825
---
<<if $ClimberRecapGroupTalk is false>>
    <<SetSpeaker Player>>
    hey tim! ##line:9ad6a3
    how's it going? ##line:388534
    [[ClimbersRestTalk]]
<<elseif $DiscussedNovel is false>>
    sooooo ##line:e2689b
    did you know ##line:9b8d16
    [[DiscussedNovel]]
<<elseif $IceSquirrelTalkCount is 0>>
    <<SetSpeaker Player>> 
    so how'd you get up that cliff? ##line:e283d0
    <<SetSpeaker Original>> 
    well... ##line:c537ee
    i had some really good advice ##line:8ddafd
    <<SetSpeaker SitRhino>> 
    <<SetEmotion Happy>> 
    aw thanks ##line:0acaee
    i tried to my best to help ##line:115ae9
    <<ClearEmotion>> 
    <<if $HeardClimbingSecret is true>>
        <<SetSpeaker Original>> 
        well actually ##line:58d801
        i just remembered\nthe secret of climbing ##line:0fee38
        <<SetSpeaker Player>> 
        climb climb all the time? ##line:e27680
        <<SetSpeaker Original>> 
        bingo! ##line:87b62d
    <<endif>>
    <<set $IceSquirrelTalkCount to 1>> 
<<elseif $IceSquirrelTalkCount is 1>> 
    remember ##line:5fc1f8
    the rock climbers association\nisn't JUST about climbing ##line:cd37f8
    <<SetSpeaker Player>> 
    it isn't? ##line:66520c
    <<SetSpeaker Original>>
    you're also expected to be an ACTIVE\nmember of the online community! ##line:3bed22
    it's very important ##line:fce5ec
    <<SetSpeaker Player>> 
    i see ##line:4278de
    <<set $IceSquirrelTalkCount to 2>> 
<<else>> 
    i'm a moderator on the forums ##line:8512e4
    so ##line:53e3b5
    i'll know if you're\nnot participating! ##line:e14d93
<<endif>>
===
title: IceRhinoStart
tags: 
colorID: 0
position: 12684,-2824
---
<<if $ClimberRecapGroupTalk is false>>
    <<SetSpeaker Player>>
    hey you guys! ##line:401ca6
    how's it going? ##line:7c803f
    [[ClimbersRestTalk]]
<<elseif $DiscussedNovel is false>>
    <<SetSpeaker Player>>
    so what have you been up to? ##line:bb2f75
    <<SetSpeaker Original>>
    just chattin' with charlie ##line:af5bd4
    he's got a lot of good stories\nfrom working up here ##line:862125
    <<SetSpeaker SitSquirrel>>
    speaking of stories... ##line:244796
    i wanna read his NOVEL!! ##line:1c3656
    <<SetSpeaker Original>>
    tim! ##line:5a4264
    [[DiscussedNovel]]
<<elseif $IceRhinoTalk is 0>>
    claire ##line:d02724
    i've got to say ##line:d0469c
    you're a really impressive climber ##line:9fb0c2
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    well, it helps that\ni can also fly ##line:910ddf
    it makes it a lot less risky ##line:2191c1
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    don't sell yourself short ##line:aa4e6f
    <<SetEmotion Happy>> 
    you're awesome! ##line:6a2576
    <<ClearEmotion>>
    <<set $IceRhinoTalk to 1>> 
<<else>> 
    let's hike up to\nthe top tomorrow! ##line:04efdc
    <<SetEmotion Happy>> 
    you can show me the way! ##line:7a963a
    <<ClearEmotion>>
<<endif>>
===
title: SquirrelClimbStart
tags: 
colorID: 0
position: 12564,-3827
---
THIS AIN'T HELPING CLAIRE! ##line:cf7683
===
title: ClimbersRestTalk
tags: 
colorID: 0
position: 12682,-2518
---
<<LookAt Player SitBear>>
<<SetSpeaker SitSquirrel>>
<<LookAt SitSquirrel Player>>
<<LookAt Player SitSquirrel>>
OOHH ##line:927794
YOU KNOW ##line:e477d3
PRETTY GOOD ##line:2bbcba
<<SetEmotion EyesClosed>>
I JUST CONQUERED HAWK PEAK ##line:c045d4
LIKE ##line:1987df
NO BIG DEAL ##line:b2bac3
<<ClearEmotion>>
<<SetSpeaker Player>>
nice! ##line:bf0f1f
you made it the top too? ##line:bfbb60
<<SetSpeaker SitSquirrel>>
... ##line:8c0a68
<<LookAt SitBear SitSquirrel>>
WELL ##line:bde61f
ALMOST CONQUERED IT ##line:37739f
<<SetPose RaiseArms>>
<<LookAt Player SitSquirrel>>
WE'RE BASICALLY THERE RIGHT? ##line:4f2bd5
<<ClearPose>>
<<LookAt SitBear SitSquirrel>>
<<SetSpeaker SitBear>>
<<LookAt SitBear Player>>
<<LookAt SitSquirrel SitBear>>
well technically ##line:d42384
this whole area is\nconsidered hawk peak ##line:d45f04
<<SetSpeaker SitSquirrel>>
<<LookAt Player SitSquirrel>>
<<LookAt SitSquirrel Player>>
SEE? ##line:110e5e
NAILED IT ##line:b9890f
<<SetSpeaker SitRhino>>
<<LookAt SitRhino Player>>
<<LookAt SitRhino SitBear>>
we're gonna make it to\nhawk peak's peak another day ##line:f8688d
i think we're at our limit ##line:65e85b
<<LookAt SitBear SitRhino>>
it's better not to push\nyourself too hard, right? ##line:e05da1
<<SetSpeaker SitBear>>
<<LookAt SitRhino SitBear>>
<<LookAt SitBear Player>>
definitely ##line:3327fc
safety always comes first ##line:3f16c8
<<SetSpeaker SitRhino>>
<<ClearLookAt SitRhino>>
<<LookAt SitRhino Player>>
you made it up, though? ##line:699367
how was it? ##line:ee9498
<<SetSpeaker Player>>
<<LookAt Player SitBear>>
<<SetEmotion Happy>>
it was beautiful! ##line:b71884
the northern lights are\nreally bright up there ##line:88b0dd
<<ClearEmotion>>
<<SetSpeaker SitRhino>>
<<SetEmotion Happy>>
i can't wait to see it myself! ##line:3fa79c
<<ClearEmotion>>

<<ClearLookAt SitBear>>
<<ClearLookAt Player>>
<<ClearLookAt SitRhino>>
<<ClearLookAt SitSquirrel>>
<<set $ClimberRecapGroupTalk to true>>
===
title: PolarBearSitStart
tags: 
colorID: 0
position: 12203,-2817
---
<<if $DiscussedNovel is false>>
    it's nice to have\nsome company up here ##line:42c5be
    usually it's pretty quiet ##line:f6a84e
    <<SetSpeaker Player>>
    does it get boring up here? ##line:3e3036
    <<SetSpeaker Original>>
    well ##line:22e988
    i keep busy ##line:fc2c95
    <<SetSpeaker SitSquirrel>>
    claire, did ya hear? ##line:36fd46
    [[DiscussedNovel]]
<<elseif $BearSitTalkCount is 0>>
    <<SetSpeaker Player>> 
    can i read your story\nwhen it's done? ##line:824fb1
    <<SetSpeaker Original>> 
    well... ##line:73025a
    i suppose so ##line:f29b50
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    awesome! ##line:6f6be1
    i bet it'll be great! ##line:b590d9
    <<ClearEmotion>> 
    <<set $BearSitTalkCount to 1>> 
<<else>>
    keep warm out there! ##line:7fd6d2
<<endif>>
===
title: DiscussedNovel
tags: 
colorID: 0
position: 12420,-2515
---
<<set $DiscussedNovel to true>>

<<SetSpeaker SitSquirrel>>
<<Face SitSquirrel Player>>
<<LookAt SitSquirrel Player>>
charlie's SUPPOSEDLY writing a book ##line:62f860
<<LookAt SitBear SitSquirrel>>
<<LookAt SitBear SitRhino>>
<<SetSpeaker SitBear>>
<<LookAt SitSquirrel>>
<<LookAt SitBear Player>>
hey now! ##line:0874b1
i am... ##line:f04cb4
...almost done ##line:c665c5
and ##line:983d2d
<<LookAt Player>>
it's only a short story! ##line:5e5037
<<SetSpeaker SitSquirrel>>
<<LookAt SitSquirrel Player>>
then why can't i read it! ##line:22cec5
<<SetSpeaker SitBear>>
<<LookAt SitSquirrel>>
<<LookAt SitBear Player>>
it's ALMOST done ##line:80fb4e
it's not ready yet... ##line:fc2318
i'm having trouble with the ending ##line:cb51be
<<SetSpeaker SitSquirrel>>
<<LookAt SitSquirrel Player>>
oh well that's easy ##line:7fff81
just make it super sad ##line:6808b1
everyone cries everyone dies ##line:b1e5e1
that's how you make\na work of ART ##line:043d1a
<<SetSpeaker SitRhino>>
<<LookAt SitRhino Player>>
<<LookAt SitRhino SitSquirrel>>
i dunno ##line:24923a
i like happy endings ##line:476e3e
like ##line:905fbb
i don't really get it ##line:28e42d
why would you want to put down a\nbook and feel worse than you\ndid when you picked it up? ##line:f5e33b
<<SetSpeaker SitSquirrel>>
<<LookAt SitRhino>>
<<LookAt SitSquirrel Player>>
you don't feel worse! ##line:74a435
it's... ##line:3794f7
FULFILLING! ##line:d9e043
sometimes you need sad endings! ##line:6ff505
<<SetSpeaker SitBear>>
<<LookAt SitBear Player>>
ahem ##line:c866d1
it's okay guys ##line:818b29
i'll figure out the ending myself ##line:6920e0
<<ClearLookAt SitBear>>
<<ClearLookAt Player>>
<<ClearLookAt SitRhino>>
<<ClearLookAt SitSquirrel>>



===
title: FireOutRangerStart
tags: 
colorID: 0
position: 6222,-3977
---
<<Wait 1>> 
<<Face Original Player>>
<<if visited() is false>> 
    uh ##line:c1b989
    i guess ##line:007658
    it's good to be\nconcerned about fire safety... ##line:ccdc24
    ...but i've got this\none under control ##line:61212a
<<elseif visitCount() is 1>>
    so uhhhh ##line:e1c3b3
    i just relit that ##line:a1a27c
    i'd like to keep it lit,\nif you don't mind ##line:03f836
<<elseif visitCount() is 2>> 
    hey uh ##line:7891bb
    can you leave that alone? ##line:02effb
    <<SetSpeaker Player>> 
    but there's a fire ban! ##line:d7e8ef
    <<SetSpeaker Original>> 
    it's actually just a fire caution ##line:42db47
    <<SetSpeaker Player>> 
    oh ##line:78b103
    alright ##line:d1563a
<<elseif visitCount() is 3>> 
    claire! ##line:904461
    <<SetSpeaker Player>> 
    i'm sorry! ##line:9449bf
    it looked like it was\nstarting to get too big ##line:07b58e
    better safe than sorry ##line:438484
    <<SetSpeaker Original>> 
    well ##line:7773ff
    you just said you WERE sorry ##line:76a8cc
    <<SetSpeaker Player>>
    oops! ##line:d62784
    yeah i guess i did ##line:7103a6
<<else>> 
    claire!! ##line:f7ef7b
    <<SetSpeaker Player>> 
    sorry!! ##line:126b0c
<<endif>> 
===
title: FireOutAuntStart
tags: 
colorID: 0
position: 6218,-3734
---
<<Wait 1>> 
<<Face Original Player>>
<<if visited() is false>> 
    it is pretty hot out ##line:3ae172
    i guess we don't really\nneed a fire right now ##line:e2a29e
<<elseif visitCount() is 1>>
    too hot for you? ##line:c9b722
    <<SetSpeaker Player>> 
    i dunno ##line:7988a6
    i was just curious what you'd do ##line:40eca4
    <<SetSpeaker Original>> 
    huh ##line:4841da
<<else>>
    welp ##line:89181c
    i'm sure getting a lot\nof fire starting practice ##line:987fac
<<endif>> 
===
title: FireOutBearStart
tags: 
colorID: 0
position: 6226,-3501
---
<<if $_CampfireFriendsBegin is true>> 
    <<Wait 1>> 
    <<Face Original Player>>
    <<if visited() is false>>
        claire ##line:27e5e2
        i like your initiative! ##line:44da9b
        fire safety is no joke! ##line:2f1c4d
    <<elseif visitCount() is 1>>
        nice work claire! ##line:9c96ad
        the fire's nice to look at ##line:22586c
        but it's not worth\ncompromising our safety ##line:b41f2a
    <<elseif visitCount() is 2>> 
        who needs fire anyway ##line:9b01ed
        i'm comfortable with or without ##line:e98e0a
        <<SetSpeaker SitRhino>> 
        well ##line:de5d05
        i like the fire... ##line:64107b
    <<else>> 
        <<SetSpeaker SitRhino>> 
        maybe it's just me ##line:395426
        but i'd like to keep the fire going ##line:014d1e
        <<SetSpeaker SitSquirrel>> 
        seconded ##line:4ca94b
    <<endif>> 
<<endif>> 
===
title: IceHikerPeak
tags: 
colorID: 0
position: 11187,-3056
---
<<if visited() is false>>
    <<SendMessage Original CameraShift1>>
    woah ##line:0492f6
    claire ##line:17aeb8
    <<Face Player>>
    this is better than i\ncould have imagined! ##line:578df3
    <<SetEmotion Happy>>
    thanks for helping\nme get up here! ##line:659ed9
    <<ClearEmotion>>
    <<SetSpeaker Player>>
    <<SetEmotion Happy>>
    it was no problem! ##line:e05a75
    really! ##line:2fdf68
    <<ClearEmotion>>

    <<if $RequestFoxPhoto is true>>
        <<SetSpeaker Original>>
        so ##line:965bbf
        <<SetEmotion Happy>>
        you still want that photo? ##line:f88105
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        oh yeah! ##line:51be04
        let's do it! ##line:ab63a4
        <<SetSpeaker Original>>
        ok! ##line:f81d39
        hold on ##line:c9ca0e
    <<else>> 
        <<SetSpeaker Original>>
        these lights are so incredible ##line:ee0b4c
        i hope they turn out on film! ##line:91fab1
        i lugged this fancy camera\nall the way up here... ##line:7e7e8b
        it better be worth it! ##line:180c91
        <<SetSpeaker Player>>
        well ##line:2819d6
        the camera on my phone sucks ##line:c30b92
        so at least it's\nbetter than that! ##line:304fe1
        <<SetSpeaker Original>>
        hmm ##line:79d7df
        want me to take your photo? ##line:3f6943
        this would make a\nsweet profile pic! ##line:1af565
        <<SetSpeaker Player>>
        aww yeah! ##line:33d668
        that'd be great! ##line:a239ed
        <<SetSpeaker Original>>
        ok! ##line:f3adb3
        hold on ##line:71f153
    <<endif>> 

    <<Hide>>
    <<SetPose TakePhoto>>
    <<Wait 0.25>>
    <<SendMessage Original ShowPropCamera>>
    <<SendMessage Original CameraShift2>>
    <<SendMessageCoroutine Original WalkPlayer>>
    <<Wait 1>>
    <<Face Player Original>>

    strike a pose! ##line:539664
    <<SetSpeaker Player>>
    oh ##line:a88296
    uhhhhh ##line:8de5f5
    <<Hide>>
    <<SetPose CoolPose Player>>
    <<Wait 1.5>>

    <<SetSpeaker Original>>
    amazing ##line:984bc7
    
    <<Hide>>
    <<Wait 0.5>>
    <<SetEmotion Happy Player>>
    <<Wait 0.5>>
    <<PlaySound Camera>>
    <<GiveAchievement PhotoTaken>>
    <<Wait 1.5>>
    <<ClearEmotion Player>>
    <<Wait 0.5>>

    <<ClearPose Player>>
    <<Wait 0.5>>
    <<ClearPose Original>>
    <<Wait 0.1>>
    <<SendMessage Original CameraShift1>>
    <<Wait 0.1>>
    <<SendMessage Original HidePropCamera>>

    <<Wait 1.2>>
    <<SetSpeaker Original>>
    the northern lights\nturned out great! ##line:721709
    <<SetEmotion Happy>>
    what a nice photo! ##line:37af80
    <<ClearEmotion>>
    oh, before i forget ##line:f6a25c
    here's the feathers\nyou lent me ##line:c5c1c0
    <<Hide>>
    <<GiveItem GoldenFeather 6>>
    <<Wait 1>>
    <<SetSpeaker Player>>
    <<Face Original>>
    thanks! ##line:364e7b
    <<SetEmotion Happy>>
    make sure you email\nme that photo! ##line:caf3e6
    <<ClearEmotion>>
    <<SetSpeaker Original>>
    <<SendMessage Original CameraShift3>>
    i will! ##line:72292f
<<else>>
    i'm gonna chill here for a while ##line:13e9cc
    i don't know when\ni'll be back so... ##line:60922e
    i wanna soak it all\nup while i can ##line:186271
<<endif>>
===
title: IceHikerAfterPeak
tags: 
colorID: 0
position: 10939,-2805
---
<<if visited() is false>>
    any luck getting to the top? ##line:f2bc5c
    <<SetSpeaker Player>>
    yeah! ##line:9ffea6
    i was just up there ##line:d627dd
    <<SetSpeaker Original>>
    !! ##line:9976f6
    can you show me the route? ##line:36ff13
    <<SetSpeaker Player>>
    well ##line:d2cc9f
    you kinda need to... ##line:344d1f
    ...be able to fly ##line:222478
    <<SetSpeaker Original>>
    oh ##line:1c8362
    right... ##line:d18179
    <<SetSpeaker Player>>
    maybe you could climb up? ##line:b6432e
    <<SetSpeaker Original>>
    i don't have enough\ngolden feathers... ##line:a104e8
    <<SetSpeaker Player>>
    i could lend you some! ##line:090445
    <<SetSpeaker Original>>
    you would? ##line:87753f
    <<SetSpeaker Player>>
    tots! ##line:0d4008
    how many do you need? ##line:e99d91
    <<SetSpeaker Original>>
    uhh ##line:e3d84f
    it's kinda embarrassing ##line:b55406
    but i'd need a lot ##line:71f125
    like, 6 ##line:6e6921
    <<SetSpeaker Player>>
    <<if HasItem("GoldenFeather", 6) is true>>
        -> oh, sorry ##line:dc4904
            <<SetSpeaker Original>>
            don't worry about it! ##line:6a9b10
            i know it's a lot to ask ##line:b9723d
        -> i can lend you 6 feathers! ##line:edd929
            [[IceHikerLend]]
    <<else>>
        oh ##line:394c9b
        sorry... ##line:c06db8
        i don't have enough to help you ##line:d327b1
        <<SetSpeaker Original>>
        really, ##line:28498c
        don't worry about it! ##line:5bac36
    <<endif>>
<<else>>
    i'm so close yet so far! ##line:b38aa4
    <<if HasItem("GoldenFeather", 6) is true>>
        <<SetSpeaker Player>>
        -> i can lend you 6 feathers! ##line:a5e361
            [[IceHikerLend]]
        -> good luck! ##line:143009
            <<SetSpeaker Original>>
            thanks! ##line:627963
            i'll figure something out ##line:17ed6f
    <<endif>>
<<endif>>



===
title: IceHikerLend
tags: 
colorID: 0
position: 10942,-2556
---
<<SetSpeaker Original>>
<<if HasItem("GoldenFeather", 12) is true>>
    really!? ##line:871b82
    <<SetSpeaker Player>>
    of course! ##line:9f6a9c
    <<Hide>>
    <<GiveItem GoldenFeather -6>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    you're amazing, claire! ##line:44cd87
    i'll see you up there! ##line:9a5f71
    <<SendMessage Original StartClimbing>>
<<else>>
    but ##line:6b3ad9
    you'd be giving me more\nthan half of your feathers! ##line:20e114
    i don't wanna do that to you ##line:3da434
<<endif>>

===
title: WinMedal
tags: 
colorID: 0
position: 7717,-5428
---
<<set $BeatOneRace to true>>
<<if visitCount() % 3 is 0>> 
    <<SetPose LookingDown>> 
    <<SetEmotion EyesClosed>> 
    oof ##line:3171ec
    hold on ##line:8bff33
    i gotta... ##line:bc647b
    ...catch my breath ##line:e20898
    <<Hide>> 
    <<Wait 2>>
    okay! ##line:d11220
    <<ClearEmotion>> 
    <<ClearPose>> 
    *ahem* ##line:2eab3b
    claire ##line:6f2d1c
    you got me beat! ##line:b881b8
    i think i owe you this ##line:4cb0e9
    <<Hide>> 
    <<GiveItem Medal 1>> 
    <<Face Original Player>> 
    <<SetSpeaker Player>>
    woah! ##line:e71b96
    where'd ya get this? ##line:bcd529
    <<SetSpeaker Original>> 
    <<SetEmotion EyesClosed>>
    i have my ways! ##line:a1f8a6
    <<ClearEmotion>> 
    ...anyway ##line:f2290d
    next time we race, ##line:3c196d
    i'll be sure to use what\ni learned from you! ##line:04e333
<<elseif visitCount() % 3 is 1>> 
    claire! ##line:356395
    you are a SPEED DEMON ##line:1cb5a0
    and you know what speed demons get? ##line:602a5b
    <<SetSpeaker Player>>
    uhhhhhh? ##line:76915b
    <<SetSpeaker Original>>
    they get AWARDS!! ##line:ffa4ed
    <<Hide>> 
    <<GiveItem Medal 1>> 
    <<Face Original Player>>
    don't think i'm done yet! ##line:0c0393
    i'm still improving, so ##line:c67842
    i'll get you next time! ##line:68f8be
<<else>>
    that was one intense race ##line:2b9e18
    welp ##line:a21325
    i'd better hand this over ##line:3e8b84
    <<Hide>> 
    <<GiveItem Medal 1>> 
    <<Face Original Player>>
    that's all the medals i got ##line:f116d6
    but if you ever want a rematch, ##line:0059bd
    i'll do better next time! ##line:6dd56b
<<endif>>

[[AfterRaceMenu]]
===
title: InspectMedalStart
tags: 
colorID: 0
position: 7016,-5749
---
<<if visited() is false>> 
    it says ##line:a4d00a
    "certified better than Avery" ##line:4978dd
    huh? ##line:4a192d
    it looks like... ##line:511011
    ...it's just a gold coin glued a ribbon? ##line:30c4ec
<<else>> 
    it's a gold coin glued to a ribbon ##line:aad717
<<endif>>

-> extract the coin ##line:61d9f6
    [[ExtractCoin]] 
-> leave it be ##line:ae3694
===
title: ExtractCoin
tags: 
colorID: 0
position: 7015,-5499
---
<<if IsVisible("RaceOpponent") is true>>
    [[NotExtractCoin]] 
<<else>>
    <<if $BrokenMedal is false>> 
        i feel a little bad about this... ##line:d769a4
        but ##line:683a7a
        eh ##line:91c27d
    <<endif>>
    <<Hide>>
    <<SetPose LookingDown>> 
    <<PlaySound RipCoin>>
    <<Wait 1.2>>
    <<ClearPose>> 
    <<GiveItem Coin 7>> 
    <<GiveItem Medal -1>> 
    <<set $BrokenMedal to true>> 
<<endif>>
===
title: NotExtractCoin
tags: 
colorID: 0
position: 6769,-5499
---
<<if visited() is false>> 
    uhh ##line:63fa94
    ... ##line:6fc7d3
    maybe i shouldn't do this\nwhere they can see me ##line:0efb4c
<<else>> 
    i'm too close! ##line:cc6f0d
    i don't wanna hurt\ntheir feelings... ##line:e90e37
<<endif>> 
===
title: DeerStart
tags: 
colorID: 0
position: 9788,-2468
---
<<if $PickedUpTrash is true>> 
    [[Deer2]] 
<<elseif visited() is false>>
    *sigh* ##line:723763
    i just spent the past\nhour picking up garbage... ##line:705056
    it looks like someone threw\na party here and left\nhalf of it behind! ##line:dbd401
    <<SetSpeaker Player>>
    what!? ##line:82384f
    that's NOT cool ##line:6a0706
    <<SetSpeaker Original>>
    right!? ##line:2817e6
    ugh ##line:3a3516
    <<SetSpeaker Player>>
    i could help you with the rest! ##line:f4a97e
    it looks like you've\nalready done most of it! ##line:213c26
    <<SetSpeaker Original>>
    <<SetEmotion Happy>> 
    really? ##line:31a0a1
    that would be fantastic ##line:6af9aa
    thank you! ##line:230675
    <<ClearEmotion>> 
<<elseif HasItem("Trash", 9)>> 
    <<SetSpeaker Player>>
    i think i got it all! ##line:90c663
    <<Hide>>
    <<GiveItem Trash -9>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    <<SetEmotion Happy>> 
    that looks like everything! ##line:3ff573
    <<ClearEmotion>> 
    this is SO much better! ##line:92869c
    <<SetEmotion Happy>> 
    thanks for your help! ##line:3d9464
    <<ClearEmotion>> 
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    no problem! ##line:6ed35f
    <<set $PickedUpTrash to true>> 
    <<ClearEmotion>> 
<<elseif HasItem("Trash", 5)>> 
    <<SetSpeaker Player>>
    is this all of it? ##line:4aac4d
    <<SetSpeaker Original>>
    i think there's a little more ##line:a7e0df
<<else>>
    <<SetSpeaker Player>>
    they really left behind a mess huh? ##line:9da2b9
    <<SetSpeaker Original>>
    yeah... ##line:72bbfc
    can you believe the\nnerve of some people? ##line:50e895
<<endif>> 


===
title: Deer2
tags: 
colorID: 0
position: 9797,-2215
---
<<if $BoughtSunhat is true>>
    [[Deer3]] 
<<elseif visited() is false>> 
    <<SetSpeaker Player>> 
    i like your sunhat! ##line:c16420
    <<SetSpeaker Original>> 
    <<SetEmotion Happy>>
    thank you! ##line:914256
    i made it myself ##line:a3093d
    <<ClearEmotion>> 
    <<SetSpeaker Player>> 
    really?! ##line:872d01
    that's, like, intense! ##line:454bfc
    i didn't know you could\njust like... MAKE a hat! ##line:f7efe0
    <<SetSpeaker Original>>
    yup it's true ##line:e4cedd
    but it takes a while ##line:9baa58
    i'm making a bunch for\nthe upcoming craft fair ##line:9c3bbf
    if you're interested, ##line:b77cf6
    i could sell you one now! ##line:9c89b0
    <<SetSpeaker Player>> 
    oh yeah!? ##line:428fe0
    how much are they? ##line:2fdbc2
    <<SetSpeaker Original>> 
    <<SetEmotion EyesClosed>> 
    hmm ##line:ce3b2e
    <<ClearEmotion>> 
    pricing things is hard... ##line:1542d4
    how does 100 coins sound? ##line:7e216d
    <<SetSpeaker Player>>
    -> sounds good to me! ##line:03ce64
        <<SetSpeaker Original>> 
        great! ##line:0f43c6
        i think it's a fair price! ##line:556fae
        <<set $SunhatPrice to 100>>
        [[DeerHatSell]]
    -> i'd pay more! ##line:7cb823
        i REALLY like the hat! ##line:2fd3ea
        you can tell a lot\nof love went into it! ##line:ff244b
        <<if $KnowsAboutMerchHat is true>>
            the manufactured ranger\nhats cost 100 coins... ##line:3fdd6d
            ...but you can tell\nyours is handcrafted! ##line:3ed380
        <<endif>> 
        i wouldn't want to\nsell you short ##line:1d7030
        <<SetSpeaker Original>>
        aw ##line:977430
        <<SetEmotion Happy>> 
        thank you! ##line:d5b3de
        <<ClearEmotion>>
        do you think i could\ncharge 125 coins? ##line:542e18
        <<SetSpeaker Player>> 
        yeah, easily! ##line:f52735
        <<set $SunhatPrice to 125>>
        [[DeerHatSell]]
<<else>>
    <<SetSpeaker Player>> 
    hello! ##line:251b86
    how much was the sunhat, again? ##line:671a6c
    <<SetSpeaker Original>>
    {{$SunhatPrice}} coins! ##line:2ecc54
    [[DeerHatSell]]
<<endif>>
===
title: DeerHatSell
tags: 
colorID: 0
position: 10071,-2210
---
<<SetSpeaker Player>>
<<ShowCollection Coin>>
-> i'll take a hat! ##line:76523b
    <<HideCollection Coin>>
    [[BuyDeerHat]]
-> see ya later! ##line:214ecd
    <<HideCollection Coin>>
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    'till next time! ##line:aeec36
    <<ClearEmotion>>
    
===
title: BuyDeerHat
tags: 
colorID: 0
position: 10327,-2213
---
<<if HasItem("Coin", $SunhatPrice) is true>>
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    great! ##line:4a91ab
    <<ClearEmotion>>
    <<if $SunhatPrice > 100>>
        do you want to choose\nthe ribbon color? ##line:df2f12
        i don't mind changing\nit for you! ##line:e2da05
        <<SetSpeaker Player>>
        oh! hmm ##line:e2cbea
        do you have... ##line:424536
        -> pink? ##line:0fdaa0
            <<set $_SunhatRibbon to 0>>
        -> red? ##line:8591ce
            <<set $_SunhatRibbon to 1>>
        -> blue? ##line:9564e2
            <<set $_SunhatRibbon to 2>>
        -> purple? ##line:b37cb3
            <<set $_SunhatRibbon to 3>>
        <<SetSpeaker Original>>
        yup! ##line:867cc4
    <<endif>> 
    here you go! ##line:4e3683

    <<Hide>>
    <<set $SunhatCost to $SunhatPrice * -1>>
    <<GiveItem Coin $SunhatCost>>
    <<PlaySound BuySound>>
    <<Wait 1>>
    <<GiveItem Sunhat 1>>
    <<set $BoughtSunhat to true>>
    <<Face Original Player>> 

    <<if $SunhatPrice > 100>>
        <<SetEmotion Happy>>
        thanks so much!! ##line:1019b1
        i'm so happy you like it! ##line:c44b6e
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        thank YOU ##line:285190
        it's so pretty! ##line:f0b1f1
        i love it! ##line:46dde8
        <<ClearEmotion>>
    <<else>>
        thanks! ##line:2b28bb
        <<SetEmotion Happy>>
        you're my first customer! ##line:8e5ef2
        <<ClearEmotion>>
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        the first of many! ##line:1d4c4d
        it's a great hat! ##line:2114aa
        <<ClearEmotion>>
    <<endif>>
<<else>>
    <<SetSpeaker Original>>
    oh, um ##line:c23fd5
    i think you're short\nby a few coins ##line:5164e8
    <<SetSpeaker Player>>
    oops ##line:6435bc
    i'll be back i guess! ##line:69bcd0
<<endif>>
===
title: Deer3
tags: 
colorID: 0
position: 9793,-1969
---
<<if visited() is true and $SunhatPrice > 100 and "Sunhat" is $_WornHat>> 
    <<SetEmotion Happy>>
    so how do you like the color? ##line:add7cb
    <<ClearEmotion>> 
    <<SetSpeaker Player>>
    -> it's nice! ##line:223c02
        <<SetSpeaker Original>>
        cool ##line:a68323
    -> can i try a different one? ##line:90a0c4
        <<SetSpeaker Original>> 
        sure! ##line:772039
        whatcha thinking? ##line:506289
        <<SetSpeaker Player>>
        -> pink ##line:6dbd5b
            <<SetSpeaker Original>>
            the classic ##line:cab128
            <<set $_SunhatRibbon to 0>>
        -> red ##line:cc3da5
            <<SetSpeaker Original>>
            on it ##line:9ef25b
            <<set $_SunhatRibbon to 1>>
        -> blue ##line:0e8cf4
            <<SetSpeaker Original>>
            totally ##line:20f45f
            <<set $_SunhatRibbon to 2>>
        -> purple ##line:1f4ef8
            <<SetSpeaker Original>>
            no problem! ##line:99ec38
            <<set $_SunhatRibbon to 3>>
<<else>> 
    this is one of my favourite\nspots to come unwind ##line:ccfe90
    it's relaxing just to sit\nhere and work on my hats! ##line:45950e
<<endif>> 
===
title: FishTutWaterStart
tags: 
colorID: 0
position: 6539,-4003
---
<<Wait 1>>
<<Face Original Player>>
hmm... ##line:aa421c
interesting technique ##line:69bf68
<<SetSpeaker Player>> 
<<Face Original Player>>
this was just an accident!! ##line:8c60b9
<<SetSpeaker Original>>
that's all right ##line:b856bc
just give it another shot ##line:cf39ad
and remember... ##line:875a7f
<<LookAt Player Original>> 
once you've hooked the fish, ##line:11686f
<<if IsUsingJoystick() is true>>
    <color=red>pull back</color> AND <color=red>tap {{UseItemButton}}</color> to reel it in! ##line:2bdc88
<<else>> 
    <color=red>pull back</color> AND <color=red>tap {{UseItemButton}}</color> to reel it in! ##line:aaaaa2
<<endif>> 
<<ClearLookAt Original>> 
<<set $FellInWater to true>> 
===
title: UsedVehicleInRace
tags: 
colorID: 0
position: 8340,-5028
---
<<if visited() is false>>
    CLAIRE!! ##line:cbf1c5
    are you SERIOUS!? ##line:47b664
    incorporating a motorboat\ninto your route... ##line:aa5358
    now THAT's a level 2\nplay right there! ##line:8e613b
    <<SetSpeaker Player>> 
    ha, thanks! ##line:0aa571
    <<SetEmotion Happy>>
    i was worried it might\ncount as cheating! ##line:1491a8
    <<ClearEmotion>> 
    <<SetSpeaker Original>>
    cheating!? ##line:a12381
    i hadn't even\nthought of THAT! ##line:112b26
    hmm ##line:ea9ec6
    maybe i'd better check the\nofficial parkour handbook... ##line:40d4b1
    just in case! ##line:cc39c9
    <<SetPose LookingDown>>
    <<Hide>> 
    <<Wait 2>>
    hmmmmmm ##line:c8f95a
    <<ClearPose>> 
    aha! ##line:8fffed
    section 6.4... ##line:fab3d5
    "Regarding The Use Of\nVehicles During Parkour Races" ##line:e08d40
    ... ##line:4f6498
    hmm ##line:179581
    aw darn ##line:da9a01
    "Motorized vehicle operation is\nforbidden during ANY regulation\ncompliant parkour race..." ##line:c34403
    "...exceptions for e-bikes can\napply, see section 8.3" ##line:66255b
    well ##line:387257
    it sounds like it might\nbe cheating after all! ##line:852f84
    sorry! ##line:5a580c
    <<SetSpeaker Player>> 
    nah, my bad! ##line:69cc47
<<else>> 
    <<SetSpeaker Original>>
    if it was up to me,\ni'd allow the motorboat! ##line:55d6ed
    but we gotta follow regulation! ##line:2b40d2
    <<SetSpeaker Player>> 
    oops! ##line:34e175
    i forgot ##line:15deec
<<endif>> 
===
title: KidDeerStart
tags: 
colorID: 0
position: 10871,-7541
---
<<if visitCount() is 0>> 
    ugh... ##line:2ee729
    uuuUGGHHHHHH... ##line:a71e28
    <<SetSpeaker Player>> 
    um ##line:e8cad6
    are you ok? ##line:ba0be1
    <<SetSpeaker Original>> 
    no... ##line:906e6c
    i'm not! ##line:43ca47
    I'M DYING!!! ##line:465c91
    <<SetSpeaker Player>> 
    !? ##line:5d9ab3
    <<SetSpeaker Original>> 
    ...OF BOREDOM!! ##line:27f266
    <<Hide>> 
    <<Wait 1>> 
    <<SetSpeaker Player>> 
    oh ##line:afdb39
    <<Hide>> 
    <<Wait 1>> 
    <<SetSpeaker Original>> 
    *cough cough* ##line:dc1d7a
    this is a very\nserious problem ##line:5bd58a
<<elseif visitCount() is 1>>
    <<SetSpeaker Player>> 
    is there... ##line:822153
    anything i can do? ##line:016e51
    <<SetSpeaker Original>> 
    all i wanted to do was ride the boat ##line:267ad6
    but noooooo ##line:849465
    "you're not allowed" ##line:bb943d
    "you have to wait\nuntil you're older" ##line:d7811b
    well guess what ##line:ffa4a8
    i'm older now!! ##line:db0c4a
    <<SetSpeaker DadDeer>> 
    that was five minutes ago! ##line:d21402
    <<SetSpeaker Original>> 
    ugh ##line:9bb25d
    hashtag worst dad ever!! ##line:4039dd
    <<SetSpeaker DadDeer>>
    now hold on there! ##line:c84c06
    i'll take you out later ##line:8c0c33
    <<SetSpeaker Original>> 
    but later is a\nmillion years away... ##line:fadf32
    <<if $RentedBoat is true>>
        [[OfferBoat]] 
    <<endif>> 
<<else>>
    oh woe is me... ##line:b39d62
    i'll never get to ride the boat ##line:46c240
    *cough cough* ##line:ed5e58
    why must the world\nbe so cruel?! ##line:408710
    <<if $RentedBoat is true>>
        [[OfferBoat]] 
    <<endif>> 
<<endif>> 
===
title: DadDeerStart
tags: 
colorID: 0
position: 13413,-7533
---
<<if $RentedBoat is true>> 
    [[DadDeerRented]] 
<<endif>> 

<<if visited() is false>> 
    <<SetSpeaker Original>> 
    hey claire ##line:388a4e
    how's it going? ##line:df649e
    <<SetSpeaker Player>> 
    um ##line:dfb032
    i'm alright ##line:8b4495
    how's business? ##line:e243fc
    <<SetSpeaker Original>> 
    quiet ##line:43809e
    at least i'm getting some\ngood reading time in! ##line:5809db
    <<SetSpeaker Player>>
    is the motorboat booked today? ##line:122de8
    <<SetSpeaker Original>>
    nope, not yet ##line:388aed
    you wanna take it for a spin? ##line:4c35a6
    the rental fee is 100 coins ##line:b45743
    <<SetSpeaker Player>>
    <<ShowCollection Coin>>
    -> yes!! ##line:82f28d
        <<HideCollection Coin>>
        <<SetSpeaker Player>>
        i DO wanna take it for a spin! ##line:67ba47
        <<SetSpeaker Original>>
        sure thing! ##line:fe7c87
        [[DadDeerRent]]
    -> nah ##line:277232
        <<HideCollection Coin>>
        maybe later ##line:676e79
        <<SetSpeaker Original>>
        alright ##line:e40407
        talk to you later! ##line:370405
<<else>>
    <<SetSpeaker Original>> 
    hello again ##line:5eb4ae
    what's new? ##line:d5c3a6
    <<ShowCollection Coin>>
    <<SetSpeaker Player>> 
    -> can i rent the boat? ##line:ae3809
        <<HideCollection Coin>>
        <<SetSpeaker Original>> 
        of course! ##line:2b9c2a
        it's just 100 coins\nfor the whole day ##line:59d52b
        [[DadDeerRent]]
    -> not much, you? ##line:7e55d1
        <<HideCollection Coin>>
        [[DadDeerWhatUp]] 
<<endif>>
===
title: DadDeerRent
tags: 
colorID: 0
position: 13138,-7531
---
<<if HasItem("Coin", 100) is true>>
    <<PlaySound BuySound>>
    <<GiveItem Coin -100>>
    <<Hide>>
    <<Wait 1>>
    <<SetSpeaker Original>>
    and... ##line:43cae9
    here's the key! ##line:9334cb
    <<Hide>> 
    <<GiveItem BoatKey 1>>
    oh ##line:66e44a
    and uh ##line:207ecf
    you've read the provincial motorboating\nsafety and operations guide eh? ##line:2dffad
    <<SetSpeaker Player>>
    <<Face Original Player>>
    <<set $RentedBoat to true>> 
    um ##line:958f3a
    -> yeah! ##line:902ce4
        <<SetSpeaker Original>>
        great! ##line:703c5a
        welp, have a good time out there! ##line:e76b55
    -> ...yeah? ##line:ff9a51
        <<SetSpeaker Original>>
        <<Hide>>
        <<Wait 1>>
        well... ##line:7750eb
        just in case ##line:fcb8db
        you can borrow mine ##line:7c2557
        <<Hide>>
        <<GiveItem BoatManual 1>>
        have a good time out there! ##line:f6dcbe
<<else>> 
    <<SetSpeaker Player>>
    uh ##line:1f2a55
    i'm a bit short on cash ##line:e2fa37
    i'll come back later! ##line:2c8a89
<<endif>>
===
title: DadDeerWhatUp
tags: 
colorID: 0
position: 12816,-7265
---
<<if $_DadDeerFixingBoat is true>>
    <<SetSpeaker Original>> 
    just standing here ##line:a96655
    ... ##line:9ef62d
    it's pretty good ##line:b36f01
<<else>>
    [[DeerWhatsUpToBook]] 
<<endif>>




===
title: DadDeerRented
tags: 
colorID: 0
position: 13409,-7267
---
<<if $_BrokenBoatEngine is true>> 
    <<SetSpeaker Original>> 
    hey ##line:26b506
    what's up, claire? ##line:4f2175
    <<SetSpeaker Player>>
    -> SOMETHING IS WRONG WITH THE BOAT ##line:74402b
        <<SetSpeaker Original>>
        uh ##line:249dba
        what? ##line:16db2b
        <<SetSpeaker Player>>
        SORRY! ##line:7c995c
        um ##line:e80d92
        the engine's making a weird noise... ##line:179ab1
        <<SetSpeaker Original>>
        hmm ##line:924f07
        well ##line:ba347c
        let's take a look at it ##line:b3c873
        <<SendMessage BoatScripting ResetAndFixBoatDad>>
    -> uhh, can you anchor the boat for me? ##line:d87f95
        [[DadDeerNeedHelp]]
    -> EVERYTHING'S FINE! ##line:c9cf7f
        <<SetSpeaker Original>>
        uh... ##line:85db07
        glad to hear it? ##line:4779ab
<<else>>
    <<if $_DadDeerFixingBoat and $_BoatAnchored is true>> 
        <<SetSpeaker Player>>
        what's up? ##line:4f0f31
        [[DadDeerWhatUp]]
    <<elseif $_BoatAnchored is false or $BookIntroComplete is true>>
        hey ##line:dc9c53
        what's up, claire? ##line:378b0c
        <<SetSpeaker Player>>
        <<if $_BoatAnchored is false>> 
            [[can you anchor the boat for me?|DadDeerNeedHelp]] ##line:e42e3b
        <<endif>> 
        <<if $_DadDeerFixingBoat is false and $BookIntroComplete is true>>
            [[how's the book?|WhatchaReading]] ##line:a2fbb3
            [[nothing really!|DeerDadSeeYa]] ##line:bc08c1
        <<else>> 
            [[nothin' much, you?|DadDeerWhatUp]] ##line:b4bcf2
        <<endif>> 
    <<else>>
        [[DadDeerNoQuestions]]
    <<endif>> 
<<endif>> 
===
title: BoatManualStart
tags: 
colorID: 0
position: 13916,-7164
---
<<SetSpeaker Player>> 
i'll just skim through this... ##line:12b632
<<SetPose LookingDown Player>>
<<Hide>> 
<<Wait 1>>
so... ##line:4006dc
<<ClearPose>>
press <color=red>{{InteractButton}}</color> to accelerate ##line:f0695c
<color=red>{{UseItemButton}}</color> to reverse ##line:01c3ac
<color=red>{{MenuButton}}</color> to dismount ##line:eb4921
and <color=red>{{RunButton}}</color> to honk! ##line:ed2749
sounds easy enough ##line:17d9b0
... ##line:7bba76
it also says ##line:a9391c
"if your boat becomes stuck due\nto improper boating conduct," ##line:04d1b4
"do NOT panic!" ##line:df5fa4
"although you may feel embarrassed,\nswallow your pride and ask for help\nfrom a boating veteran!" ##line:782d29
<<if visited() is false>> 
    eh ##line:332024
    i should be fine ##line:f4a974
<<endif>> 

===
title: NoBoatKeyStart
tags: 
colorID: 0
position: 14141,-7167
---
looks like it needs\na key to start ##line:1ef4a0
===
title: OfferBoat
tags: 
colorID: 0
position: 11131,-7539
---
<<SetSpeaker Player>> 
-> i could take you out! ##line:8e64b4
    <<SetSpeaker Original>> 
    WAIT ##line:0961f2
    are you for real?! ##line:7eb40f
    <<SetSpeaker Player>> 
    yeah! ##line:139388
    i'm renting the boat now! ##line:178cbe
    <<SetSpeaker Original>> 
    omigosh!! ##line:58a1a9
    DAD I'M GOING\nBOATING WITH CLAIRE ##line:551c4e
    <<SetSpeaker DadDeer>>
    oookay have fun ##line:bd1c6e
    <<SetSpeaker Original>>
    CLAIRE YOU'RE THE BEST ##line:48f067
    just let me get ready... ##line:f64a4f
    <<SendMessage BoatScripting GetIntoBoat>>
-> ...feel better soon? ##line:56af4f
    <<SetSpeaker Original>> 
    *sniff* ##line:af6312
    maybe somedaay... ##line:d01b24
===
title: KidDeerBoatStart
tags: 
colorID: 0
position: 11369,-7543
---
<<set $_KidTalkInBoat to true>>
<<if $_BrokenBoatEngine is true>>
    [[KidBoatBroken]] 
<<elseif $_AbandonedKid is true>>
    [[KidBoatAbandoned]] 
<<elseif $_RanAwayBoatRace is true>>
    [[KidBoatRaceRunAway]] 
<<elseif $_BoatRaceActive is true>>
    [[KidBoatRaceActive]] 
<<elseif $_BoatMovedFast is false>>
    [[KidBoatFastPlease]] 
<<else>> 
    [[KidBoatRequestChallenge]] 
<<endif>> 
===
title: DadDeerNeedHelp
tags: 
colorID: 0
position: 13399,-7013
---
<<SetSpeaker Original>>
<<if $_BoatAnchored is false>>
    oh sure ##line:065de4
    let's bring her home! ##line:f4ff7d
    <<SendMessage BoatScripting ResetBoat>>
<<else>> 
    she's already anchored tight! ##line:c5381a
<<endif>> 
===
title: KidDeerChillStart
tags: 
colorID: 0
position: 11814,-6464
---
<<if visited() is false>> 
    my dad's got me reading\none of his fantasy novels ##line:fbfa79
    it sounded kinda lame at first ##line:f7663f
    he practically had to\nbeg me to read it ##line:71f814
    but you know ##line:3c6b43
    it's not that bad ##line:34394e
<<else>> 
    what's new, claire? ##line:e81639
<<endif>> 

<<SetSpeaker Player>> 
-> you wanna go boating again? ##line:c80bd3
    <<SetSpeaker Original>> 
    i thought you'd never ask! ##line:29b3e1
    <<SendMessage BoatScripting GetIntoBoat>>
-> see ya! ##line:395498
    <<if $SickDiss is false>> 
        <<SetSpeaker Original>> 
        wouldn't wanna be ya! ##line:64bfab
        jk ##line:6c531c
        you're cool ##line:6e6195
        <<set $SickDiss to true>> 
    <<else>> 
        peace ##line:8d7d52
    <<endif>> 
===
title: KidBoatFastPlease
tags: 
colorID: 0
position: 11723,-7696
---
<<if visited() is false>> 
    so i was wondering... ##line:dad4d7
    <<SetSpeaker Player>>
    yeah? ##line:3b9b4a
    <<SetSpeaker Original>> 
    can we go fast? ##line:52bae3
    like ##line:52d037
    REALLY fast? ##line:301a90
    as fast as the boat will go? ##line:4e379f
    <<SetSpeaker Player>> 
    i'll try! ##line:e0dbbd
<<else>>
    hold {{JumpButton}} to rev the engine! ##line:dacc48
    let's see how fast\nthis thing can go!! ##line:2447a4
<<endif>> 
===
title: KidBoatRequestChallenge
tags: 
colorID: 0
position: 11402,-7212
---
<<if $_BoatIsFixed is true>> 
    [[KidRequestChallengeFixed]] 
<<elseif visited() is false>> 
    <<SetSpeaker Original>> 
    now THAT'S speed! ##line:a87539
    my dad never goes\nthat fast for me! ##line:9d3033
    which reminds me... ##line:ba1488
    a few days ago i came\nup with this sick\nboating challenge ##line:eaea8e
    but dad wouldn't even try it! ##line:334cee
    would you give it a try? ##line:b8d344
    <<SetSpeaker Player>> 
    -> why not? ##line:f4fed1
        [[StartBoatChallenge]] 
    -> maybe later ##line:3dc436
        <<SetSpeaker Original>> 
        aw ##line:47be8c
        okay ##line:755c92
<<else>> 
    <<SetSpeaker Original>> 
    <<if $AgreedToBoatRace is false>> 
        so... ##line:b4399f
        um ##line:3f999a
        you wanna try my boating challenge? ##line:e3652a
    <<else>>
        you wanna try my\nboating challenge again? ##line:80c6ea
    <<endif>> 
    <<SetSpeaker Player>> 
    -> let's do it! ##line:c12810
        [[StartBoatChallenge]]
    -> not now ##line:b83e30
        <<SetSpeaker Original>> 
        aw ##line:a64e7b
        okay ##line:8ea37b
    -> can i drop you off? ##line:eabb51
        [[DropOffKid]] 
<<endif>> 
===
title: StartBoatChallenge
tags: 
colorID: 0
position: 11404,-6965
---
<<set $AgreedToBoatRace to true>> 
<<SetSpeaker Original>>
<<if visited() is false>> 
    <<SetEmotion Happy>> 
    AW YES!! ##line:d00d5a
    YOU ROCK CLAIRE!! ##line:55002d
    <<ClearEmotion>>     
    i gotta set some stuff up ##line:aed3dd
    i'll be right back! ##line:ab39a2
<<else>>
    awwwwwwww yeah! ##line:ea4014
    let's do it! ##line:c79efb
<<endif>>
<<SendMessage BoatScripting StartChallenge>>
===
title: BeforeBoatRaceStart
tags: 
colorID: 0
position: 11409,-6736
---
<<if $_BoatIsFixed is true>> 
    [[BeforeBoatRaceStartFixed]] 
<<elseif visited() is false>> 
    <<LookAt Player Original>> 
    so here's the sitch! ##line:e24796
    i set up a bunch of\ncheckpoints in the water ##line:0d551d
    all you've got to do is\nhit them all in order! ##line:a38f6c
    if you finish in under 2 minutes ##line:5c92db
    then you win! ##line:a00093
    <<ClearLookAt>> 
    <<SetSpeaker Player>> 
    okay ##line:b82644
    i think i can do that! ##line:1423bf
<<else>>
    get ready! ##line:ea6bd1
<<endif>> 
===
title: KidBoatRaceActive
tags: 
colorID: 0
position: 11132,-7218
---
we've gotta get moving! ##line:d7b2d5
what is it? ##line:4ef106
<<SetSpeaker Player>> 
-> i wanna start over ##line:d4c7e0
    <<SetSpeaker Original>>
    fine fine ##line:b4c94a
    let's head back then ##line:0f388e
    <<SendMessage BoatScripting ResetChallenge>> 
-> i give up ##line:8256ba
    <<SetSpeaker Original>> 
    aw ##line:4455c6
    ok ##line:ebefab
    <<SendMessage BoatScripting CancelChallenge>> 
-> nothing really ##line:494ec0
    <<SetSpeaker Original>> 
    we're wasting precious seconds!! ##line:1d011e
    
===
title: KidBoatRaceRunAway
tags: 
colorID: 0
position: 11330,-7895
---
<<if visited() is false>> 
    hey claire ##line:f6f689
    did you... ##line:ba27f5
    ...forget about my challenge? ##line:4de3b8
    i was surprised when\nyou got off the boat ##line:f76fad
    <<SetSpeaker Player>> 
    sorry! ##line:bad460
    i just got distracted! ##line:9192d3
<<else>>
    you've gotta stay on the\nboat for the whole challenge! ##line:9a9206
    <<SetSpeaker Player>> 
    sorry! ##line:455f3f
<<endif>> 
<<set $_RanAwayBoatRace to false>>
<<set $_AbandonedKid to false>>
===
title: FinishedBoatRaceStart
tags: 
colorID: 0
position: 11559,-6462
---
<<if $_BoatRaceWon is true>>
    [[BoatRaceWon]] 
<<else>>
    [[BoatRaceLost]] 
<<endif>> 
===
title: KidBoatAbandoned
tags: 
colorID: 0
position: 11559,-7901
---
<<if visited() is false>> 
    so uh ##line:a7e924
    i got a little bored waiting\naround in the boat ##line:523841
    so i drove it back here ##line:0abeee
    um ##line:cf4776
    don't tell dad, ok? ##line:f9a15b
<<else>> 
    i drove the boat home again! ##line:f1f691
    <<SetEmotion Happy>> 
    i think i'm pretty good at this! ##line:cfd474
    <<ClearEmotion>>
<<endif>>
<<set $_RanAwayBoatRace to false>>
<<set $_AbandonedKid to false>>
===
title: CrashedBoatStart
tags: 
colorID: 0
position: 12122,-7582
---
... ##line:234b93
WOAH ##line:510557
<<Hide>>
<<Wait 1>> 
well ##line:ab7e27
that was awesome ##line:516f65
<<SetSpeaker Player>> 
that was crazy ##line:e1315b
<<SetSpeaker Original>> 
crazy awesome ##line:a7bdf9
===
title: BrokenBoatNoticedStart
tags: 
colorID: 0
position: 12804,-7583
---
<<SetSpeaker Player>>
is it just me, ##line:bd364d
or does something not sound right? ##line:a015df
<<SetSpeaker Original>> 
... ##line:552dc8
it's the engine ##line:0bb254
<<SetSpeaker Player>> 
that's not good ##line:31d5e4
<<SetSpeaker Original>> 
no... ##line:ec602f
it's not! ##line:35e776
<<SetSpeaker Player>>
maybe ##line:a16c90
it will just go away? ##line:f18530
<<SetSpeaker Original>>
i don't know ##line:7f6aec
... ##line:8bb793
shoot ##line:691082
SHOOT! ##line:91d2be
THIS IS MY FAULT ##line:b44318
I MADE THE RAMP ##line:30181e
<<SetSpeaker Player>>
it's alright ##line:79bb42
<<SetSpeaker Original>>
WHAT IF THE BOAT IS REALLY BROKEN ##line:ead7e9
WHAT IF IT SINKS!? ##line:5280bc
<<SetSpeaker Player>>
don't worry! ##line:11fc99
<<SetSpeaker Original>>
WHAT IF IT EXPLODES!? ##line:c200d5
<<SetSpeaker Player>>
it's not gonna explode! ##line:5aa153
but  ##line:103d20
uhh ##line:c1eb7e
we should probably have\nsomeone take a look ##line:dface3
<<SetSpeaker Original>>
dad would know how to fix it... ##line:086b5f
but ##line:554faa
he kinda told me ##line:e2d14e
<color=#B7A288>not to use the ramp...</color> ##line:b28245
<<SetSpeaker Player>>
oh ##line:26032d
<<SetSpeaker Original>>
he's gonna be all "disappointed" in me ##line:63c3f3
ughhhhhhhhh ##line:63ba67
<<SetSpeaker Player>>
maybe there's someone else around? ##line:f2e661
that knows, like ##line:0abe4f
boat stuff? ##line:754325
<<SetSpeaker Original>>
there's gotta be someone!! ##line:39c03c
<<set $NoticedGrossBoatNoise to true>> 
===
title: DadFixedBoatStart
tags: 
colorID: 0
position: 13654,-7008
---
<<SetSpeaker Player>> 
<<LookAt Original Player>> 
so... ##line:1a1215
is it fixed? ##line:f8b5f0
<<SetSpeaker Original>>
yeah ##line:ebbd58
one of the props got a\nlittle bent out of shape ##line:88fe7c
you hit a log or somethin? ##line:a532a3
<<SetSpeaker Player>> 
well... ##line:64aa8f
<<if $_DeerInBoat is true>> 
    <<SetSpeaker DeerKidBoat>> 
    <<LookAt DeerKidBoat Original>> 
    <<LookAt DeerKidBoat Player>> 
    yup! ##line:a3f2ae
    a log! ##line:b663f4
    classic log ##line:d2cd54
    ha ##line:7f075b
<<else>>
    ...not exactly ##line:6636f3
<<endif>>
<<SetSpeaker Original>>
well ##line:67ce9d
just make sure you're\nmore careful next time ##line:3db378
alright? ##line:9fbc55
<<if $_DeerInBoat is true>> 
    <<SetSpeaker DeerKidBoat>> 
    <<LookAt Original DeerKidBoat>> 
    yeah... ##line:a99050
    we will ##line:662d6e
    thanks dad ##line:58fd6a
    <<SetSpeaker Original>>
    <<SetEmotion Happy>>
    no problem! ##line:3f72cc
    <<ClearEmotion>> 
<<else>>
    yeah, i will! ##line:1f3085
<<endif>>
<<ClearLookAt DeerKidBoat>>
<<ClearLookAt Player>> 
<<ClearLookAt Original>> 
===
title: DuckFixedBoatStart
tags: 
colorID: 0
position: 5850,-5969
---
<<LookAt Original Player>>
...aaaand that should be it! ##line:4d1ca5
<<SetSpeaker Player>> 
that's all!? ##line:acf92d
<<SetSpeaker Original>>
that's all! ##line:de2a61
just bent a prop back into shape ##line:7580e5
should be purring like a kitten now! ##line:0346f5
<<SetSpeaker DeerKidBoat>> 
<<LookAt DeerKidBoat Player>>
<<LookAt DeerKidBoat Original>>
<<LookAt Original DeerKidBoat>>
OMIGOSH THANK YOU SO MUCH!! ##line:dc8639
i was so worried!! ##line:098ee1
<<SetSpeaker Original>>
no problemo! ##line:836530
how'd this happen, anyway? ##line:7b6963
<<SetSpeaker DeerKidBoat>> 
uh ##line:8dc13c
we kinda ##line:1bb085
drove it off a ramp ##line:98d131
<<SetSpeaker Original>>
heh ##line:5e5232
nice ##line:1c8f99
<<Hide>> 
<<Wait 1>> 
that boat's not really built\nfor that kinda action, though ##line:7a82dc
<<SetSpeaker DeerKidBoat>> 
huh ##line:160609
i wonder... ##line:810552
...could we MAKE it that kinda boat!? ##line:ff1a90
<<SetSpeaker Original>>
<<LookAt Original Player>>
hmm!! ##line:c98240
good question! ##line:6cc9f6
<<LookAt Player Original>>
<<SetSpeaker Player>> 
uhh ##line:5d5645
i think we'd better get going ##line:b6aea4
<<SetSpeaker Original>>
alrighty ##line:59d92c
i gotta get back to\nslacking off, anyway ##line:ef1e2a
<<SetSpeaker Player>>
thanks again! ##line:60a587
<<ClearLookAt Player>>
<<ClearLookAt Original>>
<<ClearLookAt DeerKidBoat>>
<<set $DuckFixedBoat to true>> 
===
title: FishBuyerBrokenBoat
tags: 
colorID: 0
position: 5848,-6203
---
<<SetSpeaker Player>> 
um ##line:a2346b
i was just curious... ##line:ed810d
but do you happen to know\nanything about boat repair? ##line:a51dd6
<<SetSpeaker Original>> 
heh ##line:12c5ef
i sure do! ##line:7c72dc
now ##line:fd9684
i ain't gonna be modest about this ##line:0abcfc
when it comes to boats... ##line:b37395
i know the ins and outs ##line:eb8dca
the lefts and rights ##line:28267f
the ups and downs ##line:8cc70e
and any other this ways and\nthat ways you can think of! ##line:3be7b3
why do ya ask? ##line:83350a
<<SetSpeaker Player>> 
well ##line:e87663
our boat is making a weird noise... ##line:ac7842
<<SetSpeaker Original>> 
oh yeah ##line:9ba3c2
boats'll make all sorts of noises ##line:e1455f
they're awfully chatty for\na chunk of metal! ##line:28894b
i reckon you want me to\ntake a look at it? ##line:28a2b7
<<SetSpeaker Player>> 
if you don't mind! ##line:b71cce
<<SetSpeaker Original>> 
well o' course! ##line:e63629
let's see what we can do ##line:07e4c1
<<SendMessage BoatScripting ResetAndFixBoatDuck>>
===
title: CrashedBoat2
tags: 
colorID: 0
position: 12345,-7584
---
<<SetSpeaker Player>> 
uhhhh ##line:4b3f03
it's not starting!? ##line:c3309c
<<SetSpeaker Original>> 
wait WHAT?! ##line:89b64c
===
title: CrashedBoat3
tags: 
colorID: 0
position: 12573,-7586
---
<<SetSpeaker Player>> 
okay i think it's working now ##line:288afe
<<SetSpeaker Original>> 
PHEW ##line:6712de
ha ##line:8f7b2a
i was getting ready to freak out! ##line:d2a8d6
===
title: KidBoatBroken
tags: 
colorID: 0
position: 11718,-7470
---
<<if $NoticedGrossBoatNoise is false>>
    ... ##line:c1da40
    <<SetSpeaker Player>> 
    you ok? ##line:966e00
    <<SetSpeaker Original>> 
    yeah ##line:532001
    i'm still like... ##line:ef0c37
    wow ##line:14db5d
    that ramp was sick ##line:611289
<<else>>
    it's really not gonna explode, right? ##line:ef052c
    <<SetSpeaker Player>> 
    definitely not ##line:64710a
    <<SetSpeaker Original>> 
    okay... ##line:709f41
<<endif>> 
===
title: KidRequestChallengeFixed
tags: 
colorID: 0
position: 11700,-7232
---
<<SetSpeaker Original>> 
<<if visited() is false>> 
    so ##line:fef686
    now that we're\nout of that pickle... ##line:902ab8
    did you maybe ##line:10f389
    wanna ##line:e55d9c
    ...try my challenge again? ##line:72c468
    <<SetSpeaker Player>> 
    uh ##line:f46919
    <<SetSpeaker Original>> 
    BUT this time with no ramp! ##line:edbbcd
    absolutely ZERO ramps! ##line:da1e55
    just regular good old\nrampless boating fun?! ##line:fdd047
<<elseif visitCount() is 1>>
    [[BoatKidThanks]] 
<<else>>
    <<if $AcceptedTheBoatChallengeAgain is true>>
        wanna try my challenge again? ##line:16a42d
    <<else>>
        if you're willing to\ngive it another go ##line:1d09fe
        i'd love it if you'd\ntry my challenge again! ##line:d12633
    <<endif>> 
<<endif>> 
    
<<SetSpeaker Player>> 
-> oh alright! ##line:7c10e6
    <<set $AcceptedTheBoatChallengeAgain to true>> 
    [[StartBoatChallenge]]
-> maybe later? ##line:8e2559
    <<SetSpeaker Original>> 
    fair enough ##line:38015b
-> can i drop you off? ##line:313674
    [[DropOffKid]] 
===
title: DropOffKid
tags: 
colorID: 0
position: 11691,-6964
---
<<if $DroppedOffBefore is true>>
    <<SetSpeaker Original>>
    okay then ##line:7a6f5e
    <<SetEmotion Happy>> 
    take it easy, claire! ##line:9a4faa
    <<ClearEmotion>>
<<else>>
    <<SetSpeaker Original>>
    oh ##line:a24209
    sure ##line:e1ed39
    <<SetSpeaker Player>>     
    sorry ##line:890fa3
    i've got some\nstuff i need to do ##line:53e7de
    <<SetSpeaker Original>>
    that's ok ##line:9683a6
    <<SetEmotion Happy>> 
    this was fun! ##line:7b41d2
    <<ClearEmotion>>
    <<set $DroppedOffBefore to true>>
<<endif>> 
<<SendMessage BoatScripting GetOffBoat>>
===
title: BeforeBoatRaceStartFixed
tags: 
colorID: 0
position: 11690,-6733
---
<<if visited() is false>>
    so i was thinking\nabout last time ##line:e0cabf
    and i thought i'd\nadd an extra rule... ##line:29b389
    ...a rule that promotes\nsafe boating practices! ##line:91b87e
    <<SetSpeaker Player>> 
    ok ##line:39a70d
    <<SetSpeaker Original>>
    whenever you hit a rock or something ##line:4cdb7c
    i'll penalize you by adding\n10 seconds to your score ##line:6e59ad
    how does that sound? ##line:8763d9
    <<SetSpeaker Player>> 
    sure! ##line:bd7315
    i can handle that! ##line:3732b7
<<else>>
    <<if $BeatFirstBoatTime is true>>
        your previous best time was {{BoatRaceBestTimeText}} seconds ##line:70a61e
        think you can beat that? ##line:2992c5
        <<SetSpeaker Player>> 
        i'll give it a shot! ##line:9877ff
    <<else>>
        <<SetSpeaker Player>> 
        how do i win again? ##line:a62945
        <<SetSpeaker Original>>
        finish in under 2 minutes! ##line:00d88c
        and try to avoid hitting things! ##line:952058
        <<SetSpeaker Player>> 
        right ##line:56d5e9
    <<endif>> 
<<endif>> 
===
title: BoatRaceWon
tags: 
colorID: 0
position: 11446,-6192
---
aaand that's TIME! ##line:d574cf
you finished in just {{BoatRaceTimeText}} seconds! ##line:af97f6
<<if $_BoatRacePenalties > 1>> 
    and that's AFTER your {{BoatRacePenalties}} penalties ##line:2abab6
<<elseif $_BoatRacePenalties > 0>>
    and that's AFTER your penalty! ##line:506eb1
<<endif>> 
<<if $BeatFirstBoatTime is false>>
    <<set $BeatFirstBoatTime to true>>
    <<SetEmotion Happy>> 
    so guess what? ##line:72712c
    you WON! ##line:0182a2
    <<ClearEmotion>>
    <<SetSpeaker Player>>
    <<SetEmotion Happy>> 
    yesss! ##line:45c95a
    high five? ##line:0848a7
    <<ClearEmotion>> 
    <<SetSpeaker Original>>
    eh ##line:21e98c
    too lazy to move ##line:b70adb
    but i do have a gift for you! ##line:9a6bfe
    <<Hide>> 
    <<GiveItem GoldenFeather 1>>
    that'll help you get air,\neven without ramps! ##line:c6c2df
<<else>>
    <<if $BoatWinCount == 0>>
        you ABSOLUTELY DESTROYED your\nlast score of {{BoatRaceBestTimeText}} seconds! ##line:4cc323
        i think that deserves a "HECK YEAH!" ##line:8283d1
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        heck yeah!! ##line:e78f9d
        <<ClearEmotion>>
    <<elseif $BoatWinCount == 1>> 
        you COMPLETELY ANNIHILATED your\nlast score of {{BoatRaceBestTimeText}} seconds! ##line:ab0ea7
        i think that deserves a "COOL BEANS!" ##line:f9a711
        <<SetSpeaker Player>>
        <<SetEmotion Happy>>
        cool beans!! ##line:95b1b6
        <<ClearEmotion>>
    <<else>> 
        you UTTERLY OBLITERATED your\nlast score of {{BoatRaceBestTimeText}} seconds! ##line:0492dd
        i think that deserves a "WA WA BOOP BOOP!" ##line:4910de
        <<SetSpeaker Player>>
        uh ##line:62b09b
        wa wa boop boop? ##line:b414bf
        <<SetSpeaker Original>>
        oops ##line:8d42d8
        that one's an old friend meme ##line:8f7699
        <<SetSpeaker Player>>
        old friend meme... ##line:8f4053
        you mean like ##line:5f96e8
        inside joke? ##line:99f37c
        <<SetSpeaker Original>>
        yeah same diff ##line:0b6ac8
    <<endif>> 

    <<set $BoatWinCount to $BoatWinCount + 1>> 
    <<if $BoatWinCount > 2>>
        <<set $BoatWinCount to 0>>
    <<endif>>
<<endif>> 
===
title: BoatRaceLost
tags: 
colorID: 0
position: 11694,-6189
---
aaand that's TIME! ##line:649294
you finished in {{BoatRaceTimeText}} seconds! ##line:98be75
<<if $_BoatRacePenalties > 1>> 
    if you include your {{BoatRacePenalties}}\npenalties, that is ##line:91d815
<<elseif $_BoatRacePenalties > 0>>
    if you include your\npenalty, that is ##line:7996d1
<<endif>>
<<if $BeatFirstBoatTime is false>>
    <<SetSpeaker Player>> 
    drat, not fast enough! ##line:052f51
    <<SetSpeaker Original>> 
    it was still fun tho! ##line:4b6c1f
    lemme know if you wanna\ngive it another shot ##line:0c786a
<<else>>
    nice score! ##line:d977ac
    not as good as your previous score of {{BoatRaceBestTimeText}} seconds ##line:292d27
    but still good! ##line:4614f5
<<endif>> 
===
title: BoatKidThanks
tags: 
colorID: 0
position: 11960,-7234
---
um ##line:08d0a6
i just wanted to say ##line:df481d
thanks for being cool\nabout the ramp thing ##line:e3ea2f
<<if $DuckFixedBoat to true>>  
    and ##line:896fd6
    not telling dad... ##line:17bd81
<<endif>>
<<SetSpeaker Player>> 
it's all good! ##line:95d0a0
===
title: WhatchaReading
tags: 
colorID: 0
position: 13089,-6756
---
<<if $_DadIsDoneBookChat is false>>
    <<set $_DadIsDoneBookChat to true>> 
    [[WhatchaReadingActualText]]
<<else>>
    <<SetSpeaker Original>> 
    it's pretty good ##line:d3edae
    i'm a fast reader ##line:c0a0fa
    so i should be finished soon ##line:70aff2
<<endif>> 
===
title: DadDeerNoQuestions
tags: 
colorID: 0
position: 13082,-7020
---
<<if visitCount() is 0>>
    <<SetSpeaker Player>> 
    so whatcha reading? ##line:b4aee5
    <<SetSpeaker Original>> 
    oh this? ##line:891680
    just some fiction ##line:7e841d
    <<SetSpeaker Player>> 
    cool ##line:9ed0ed
<<elseif visitCount() is 1>> 
    <<SetSpeaker Player>> 
    so what's it about? ##line:10778e
    <<SetSpeaker Original>> 
    what's what about? ##line:fc45af
    <<SetSpeaker Player>> 
    your book! ##line:4c0f79
    <<SetSpeaker Original>> 
    oh, uh ##line:f4254e
    it's a fantasy novel ##line:21600f
    so, you know ##line:22551d
    it's about magic and stuff ##line:9b441c
    <<SetSpeaker Player>>
    nice ##line:c12e5d
<<else>>
    <<set $BookIntroComplete to true>> 
    [[WhatchaReading]] 
<<endif>>
===
title: DeerWhatsUpToBook
tags: 
colorID: 0
position: 12818,-7022
---
<<SetSpeaker Original>>
<<if visitCount() is 0>>     
    just killing time ##line:12da43
<<elseif visitCount() is 1>>     
    not much either ##line:678f25
<<else>>     
    nothing really ##line:3a5e91
<<endif>> 
<<if $BookIntroComplete is false>>
    [[DadDeerNoQuestions]]
<<endif>> 
===
title: DeerDadSeeYa
tags: 
colorID: 0
position: 13652,-7267
---
<<SetSpeaker Player>>
see ya! ##line:a3b613
===
title: WhatchaReadingActualText
tags: 
colorID: 0
position: 13085,-6510
---
<<if visitCount() is 0>>
    <<SetSpeaker Player>>
    soo ##line:892b16
    what's the name of the book? ##line:2532c6
    <<SetSpeaker Original>>
    well... ##line:20d52d
    it's actually the first\nbook in a series ##line:c1938d
    i doubt you'll have heard of it ##line:015704
    it's still pretty niche ##line:760b66
    but to sum it up quickly... ##line:0e17f1
    <<SendMessage BookClubCamera Enable>>
    it's about an ancient fantasy world where\nsorcerers battle for glory by enchanting\nstones and rocks into powerful Stone Beasts\nthat fight each other in epic arenas filled\nwith perilous traps and fearsome monsters ##line:81e180
    and although the fight sequences seem like\nthe focus at first it's really the characters\nthat drive the story and all the battles are\nsort of metaphorically tied to each wizard's\ninternal conflicts as they struggle to survive\nin a ruthless and uncaring world ##line:b3921b
    <<SendMessage BookClubCamera Disable>>
    <<Hide>> 
    <<Wait 1>> 
    it's pretty good ##line:8ce110
    <<SetSpeaker Player>> 
    huh ##line:254ac0
    but what's it called? ##line:6526af
    <<SetSpeaker Original>> 
    oh, uh ##line:d940c4
    The Bolder Boulders Book 1:\nThe Bedrock Battalion ##line:9c573e
    <<SetSpeaker Player>> 
    cool! ##line:f77238
    <<SetSpeaker Original>> 
    yeah... ##line:bb88eb
    it is ##line:ce9f58
<<elseif visitCount() is 1>> 
    <<SetSpeaker Original>>
    i'm onto the next book now! ##line:eee6f6
    The Bolder Boulders Book 2:\nBetween A Rock and a Hard Case ##line:e54497
    it's a little darker\nthan the first book ##line:825447
    people are vanishing\nand no one knows why ##line:e60e76
    also everyone is very moody\nthis time around ##line:f9ed87
    <<SetSpeaker Player>> 
    well ##line:816086
    i hope they solve the mystery! ##line:7d5122
<<elseif visitCount() is 2>> 
    <<SetSpeaker Original>>
    i'm reading the next one now ##line:b91fd7
    The Bolder Boulders Book 3:\nAll Ore Nothing ##line:5746d1
    the two greatest wizards... ##line:eb185b
    PREVIOUSLY FRIENDS ##line:b7228e
    ...are now pitted against one another! ##line:8de90a
    and the stakes are high ##line:84e697
    because the loser will be forced\nto give up magic forever! ##line:f5f662
    <<SetSpeaker Player>> 
    uh oh!! ##line:77b8f7
    <<SetSpeaker Original>>
    you can say that again! ##line:a98b9d
    <<SetSpeaker Player>> 
    i'm good ##line:e5b9c3
<<elseif visitCount() is 3>>
    <<SetSpeaker Original>>
    moved onto the next book ##line:8e926a
    The Bolder Boulders Book 4:\nHearts of Stone ##line:709dda
    this is a good one ##line:b31881
    to quote the back of the book: ##line:ff0d6c
    "a blossoming romance between Stone Beasts\nhas caused an uproar on the battlefield!" ##line:6f58ee
    "can Mega Fist and Stone Crusher find\ntrue love in the midst of war?" ##line:3ea37e
    <<SetSpeaker Player>> 
    i'm rooting for them! ##line:7afe8f
<<elseif visitCount() is 4>> 
    <<SetSpeaker Player>> 
    you finished it yet? ##line:fca29d
    <<SetSpeaker Original>>
    yup ##line:eb3ed2
    i'm now reading The Bolder Boulders\nBook 5: Tectonic Fates ##line:83967e
    "when a mysterious enemy threatens\nthe very existence of the planet," ##line:d6faef
    "wizards and Stone Beasts alike must fight\nto protect the ones they love!" ##line:e6e8ca
    <<SetSpeaker Player>> 
    that's intense ##line:1ae489
    <<SetSpeaker Original>>
    you bet ##line:8508d0
<<elseif visitCount() is 5>>
    <<SetSpeaker Player>>
    are you done yet? ##line:2ae2e5
    <<SetSpeaker Original>>
    i'm on the final book now ##line:dfbb31
    it's simply called ##line:f28f05
    The Bolder Boulders: Infinity ##line:23f34d
    "Centuries after the events of Tectonic Fates,\nMega Fist and Stone Crusher's son Ronaldo\nwanders a barren earth, searching for any\nsigns of life" ##line:824a8e
    this one is really a masterpiece ##line:b80a49
<<elseif visitCount() is 6>>
    <<SetSpeaker Original>>
    i'm done! ##line:bda79c
    <<SetSpeaker Player>>
    all of them? ##line:8f3d8e
    <<SetSpeaker Original>>
    all of them ##line:a3d23f
    <<SetSpeaker Player>>
    so what now? ##line:a3840b
    <<SetSpeaker Original>>
    i'll probably start over again ##line:0873b5
    i hear they're even better\nthe fifth time around! ##line:aa1033
<<else>>
    <<SetSpeaker Original>>
    it's good ##line:8af30b
    <<SetSpeaker Player>>
    you read fast ##line:56f562
    <<SetSpeaker Original>>
    i sure do ##line:f3f875
<<endif>> 

===
title: OrangeIslandsSign
tags: 
colorID: 0
position: 7411,-3653
---
<sprite name="DownArrow" tint=1> The Orange Islands  ##line:2eab83
===
title: OrangeIslandsInfoStart
tags: 
colorID: 0
position: 7450,-3436
---
Welcome to the Orange Islands! ##line:96eaae
Despite living in the shadow of Hawk Peak,\nthis small cluster of islands has garnered\na unique reputation of its own! ##line:ad91c8
Are YOU able to guess how\nthese islands got their name? ##line:bb9b11

<<if visited() is false>> 
    <<SetSpeaker Player>> 
    -> it's the color of the rock, obviously ##line:f38701
    -> this is a sign and i'm just going to keep reading ##line:0dc4d3
    <<SetSpeaker Original>>
<<endif>> 

It will probably surprise you, but the\nislands are named after the\ncolor of their ROCK! ##line:86d45d
Now THAT rocks! ##line:087069
For centuries, travelers have been dazzled\nby the streams of red and orange\nthat flow through these cliffs. ##line:cf91eb
But what gives these stones\ntheir vibrant colors, you ask? ##line:7f8e07
Well, that's simply iron\noxide you're seeing! ##line:a6a118
Now you know! You've actually\nlearned something new today! ##line:0755b8

<<if visited() is false>> 
    <<SetSpeaker Player>> 
    ... ##line:e96bd9
    i feel ##line:0eaeb5
    patronized somehow ##line:fdca08
<<endif>> 
===
title: GIFStart
tags: 
colorID: 0
position: 8852,-1968
---
<<SetSpeaker Player>>
<<SetPose RaiseArms>>
<<SetEmotion Surprise>> 
AHHHH!!
COOL NEWS DELIVERY SERVICE!!
<<ClearEmotion>> 
<<ClearPose>>
<<SetSpeaker Original>> 
huh?
<<SetSpeaker Player>>
<<SetEmotion Surprise>> 
A SHORT HIKE is out NOW...
<<SetPose RaiseArms>>
<<ClearEmotion>> 
<<SetEmotion Happy>> 
...on the SWITCH!!
<<ClearEmotion>> 
<<ClearPose>>
<<SetSpeaker Original>> 
what's "the switch"
<<SetSpeaker Player>>
<<SetPose RaiseArms>>
ALSO I FOUND A BOAT
<<ClearPose>>
===
